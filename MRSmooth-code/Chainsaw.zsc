Class MRIntWeaps_Chainsaw: Chainsaw Replaces Chainsaw
{
	Default{
		Weapon.SlotNumber 1;
		Decal "BulletChip";
		MissileHeight 10;
	}
	
	Const Lay1 = 2;
	Const Lay2 = 3;//Starting rope
	Const Lay3 = 4;//Starting hand
	
	int SawFrame; //Idle frame
	Double SawPos, //Saw towards/backwards distance
		CuttingWay, //Cutting state
		SawAngle, //Cutting direction
		SawCurAngle, //Current cutting angle (interpolated)
		SawCurOffset, //Cutting angle used for chainsaw offset (interpolated)
		SawSpeed; //Chain speed
	Bool CutDir, //Moving saw backwards ro forwards
		NoCutting; //Just cutted someone
	Actor Cutting; //Actor being cutted
		
	Float Recoil;
		
	Override Void DoEffect()
	{
		if(Recoil>0){Recoil-=.5;Owner.A_SetPitch(Owner.Pitch+.5*MRIntW_RecoilAmount);}
		
		//Disabling weapon interpolation while game is loading
		Let Handler = MRIntWeaps_Handler(EventHandler.Find("MRIntWeaps_Handler"));
		if(Handler && Handler.LoadingDelay>0 && Owner.Player.FindPSprite(Lay1) && Owner.Player.FindPSprite(Lay1).CurState){
			Owner.Player.GetPSprite(Lay1).bInterpolate = false;
			Owner.Player.GetPSprite(Lay1).Y = Max(Owner.Player.GetPSprite(Lay1).Y, 0);
		}
	}
	
	States{
		Select:
			TNT1 A 0 {invoker.ResetDead();A_Overlay(Lay1, "SawRaise");}
		Raise:
			#### A 1 A_Raise();
			Loop;
		Deselect:
			TNT1 A 0 {
				if(Health<1){A_Overlay(Lay1, "GunDie");Return;}
				A_Overlay(Lay1, "SawLower");
			}
		Lower:
			#### A 1 A_Lower();
			Loop;
		
		SawStartHand:
			SAW1 Y 1 {
				if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(Lay3).Sprite = GetSpriteIndex("SAW2A0");
				A_OverlayFlags(Lay3, PSPF_ADDWEAPON, false);
				A_OverlayOffset(Lay3, -30, 32+33);
				A_OverlayScale(Lay3, .8, .8);
				A_OverlayPivot(Lay3, 0, 1);
			}
			#### # 0 {
				A_OverlayFlags(Lay3, PSPF_INTERPOLATE, true);
				
				A_OverlayRotate(Lay2, -48, WOF_ADD);
				A_OverlayScale(Lay2, 1, 1);
			}
			#### ## 1{
				A_OverlayOffset(Lay3, CFRandom(24, 25), -26+CFRandom(-1, 1), WOF_ADD);
				A_OverlayRotate(Lay3, -1.5, WOF_ADD);
				Double Scal = -Player.GetPSprite(Lay3).Y;
				Player.GetPSprite(Lay3).Scale = (1,1) + (1,1)*Scal*.01;
				
				A_OverlayRotate(Lay2, -9, WOF_ADD);
				A_OverlayScale(Lay2, 0, 2.5, WOF_ADD);
			}
			#### # 1{
				A_OverlayOffset(Lay3, CFRandom(2, 3), -CFRandom(2, 3), WOF_ADD);
				A_OverlayRotate(Lay3, -1, WOF_ADD);
				Double Scal = -Player.GetPSprite(Lay3).Y;
				Player.GetPSprite(Lay3).Scale = (1,1) + (1,1)*Scal*.01;
				
				A_OverlayRotate(Lay2, -5, WOF_ADD);
				A_OverlayScale(Lay2, 0, 1.5, WOF_ADD);
			}
			#### ## 1{//Hold
				A_OverlayOffset(Lay3, CFRandom(-1, 1), CFRandom(-1, 0), WOF_ADD);
				A_OverlayRotate(Lay3, CFRandom(-1, 1), WOF_ADD);
				Double Scal = -Player.GetPSprite(Lay3).Y;
				Player.GetPSprite(Lay3).Scale = (1,1) + (1,1)*Scal*.01;
				
				A_OverlayRotate(Lay2, -2, WOF_ADD);
			}
			#### #### 1{//Back
				A_OverlayOffset(Lay3, -7, 8, WOF_ADD);
				A_OverlayScale(Lay3, -.04, -.04, WOF_ADD);
				A_OverlayRotate(Lay3, -2, WOF_ADD);
				
				A_OverlayScale(Lay2, 0, -.85, WOF_ADD);
			}
			#### # 1{
				A_OverlayOffset(Lay3, -5, 17, WOF_ADD);
				A_OverlayRotate(Lay3, -2, WOF_ADD);
				
				A_OverlayScale(Lay2, .1, .1);
			}
			#### # 0 A_ClearOverlays(Lay2, Lay2);
			#### # 1{
				A_OverlayOffset(Lay3, 0, 17, WOF_ADD);
				A_OverlayScale(Lay3, -.1, -.1, WOF_ADD);
				A_OverlayRotate(Lay3, -2, WOF_ADD);
			}
			Stop;
		SawStartRope:
			SAW1 Z 1{
				A_OverlayOffset(Lay2, -30, 0);
				A_OverlayPivot(Lay2, .5, 1);
			}
			#### # 1 A_OverlayFlags(Lay2, PSPF_INTERPOLATE, 1);
			Wait;
		
		SawRaise:
			#### # 0 A_JumpIf(!GetCVar("MRIntW_ChainsawStart"), 5);
			#### # 0 {A_Overlay(Lay3, "SawStartHand");A_Overlay(Lay2, "SawStartRope");}
			TNT1 AAA 1 {
				A_OverlayFlags(Lay1, PSPF_ADDWEAPON, false);
				A_OverlayPivot(Lay1, .5, 1);
				A_OverlayOffset(Lay1, -12, 65);
				A_OverlayScale(Lay1, .4, .4);
				A_OverlayRotate(Lay1, 20);
			}
			TNT1 AAAA 1 {
				A_OverlayFlags(Lay1, PSPF_ADDWEAPON, false);
				A_OverlayPivot(Lay1, .5, 1);
				A_OverlayOffset(Lay1, -12, 65);
				A_OverlayScale(Lay1, .4, .4);
				A_OverlayRotate(Lay1, 20);
			}
			SAW1 F 1{if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("SAW2A0");}
			#### #### 1 {
				A_OverlayOffset(Lay1, 1, -6, WOF_ADD);
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayScale(Lay1, .07, .07, WOF_ADD);
				A_OverlayRotate(Lay1, -3, WOF_ADD);
			}
			#### # 0 A_OverlayScale(Lay1, Player.GetPSprite(Lay1).Scale.X, Player.GetPSprite(Lay1).Scale.X-.2);
			#### CC 1 {
				A_OverlayOffset(Lay1, 1, -2, WOF_ADD);
				A_OverlayScale(Lay1, .07, .16, WOF_ADD);
				A_OverlayRotate(Lay1, -2, WOF_ADD);
				if(GetCVar("MRIntW_ChainsawStart")){
					A_OverlayOffset(Lay1, 1, -2, WOF_ADD);
					A_OverlayScale(Lay1, .05, .05, WOF_ADD);
					A_OverlayRotate(Lay1, -1, WOF_ADD);
				}
			}
			#### # 0 A_JumpIf(GetCVar("MRIntW_ChainsawStart"), 2);
			#### CC 1 {
				A_OverlayOffset(Lay1, 1, -2, WOF_ADD);
				A_OverlayScale(Lay1, .05, .05, WOF_ADD);
				A_OverlayRotate(Lay1, -1, WOF_ADD);
			}
			#### D 1 {
				A_OverlayOffset(Lay1, 0, 32, WOF_INTERPOLATE);
				A_OverlayScale(Lay1, 1, 1, WOF_INTERPOLATE);
				A_OverlayRotate(Lay1, 0, WOF_INTERPOLATE);
				if(GetCVar("MRIntW_ChainsawStart")){
					A_OverlayOffset(Lay1, 0, 32, WOF_INTERPOLATE);
					A_OverlayScale(Lay1, 1, 1, WOF_INTERPOLATE);
					A_OverlayRotate(Lay1, 0, WOF_INTERPOLATE);
				}
			}
			#### # 0 A_JumpIf(GetCVar("MRIntW_ChainsawStart"), 2);
			#### E 1 {
				A_OverlayOffset(Lay1, 0, 32, WOF_INTERPOLATE);
				A_OverlayScale(Lay1, 1, 1, WOF_INTERPOLATE);
				A_OverlayRotate(Lay1, 0, WOF_INTERPOLATE);
			}
			#### F 1 {
				A_OverlayFlags(Lay1, PSPF_ADDWEAPON , true);
				A_OverlayOffset(Lay1, 0, 0);
				A_OverlayScale(Lay1, 1, .9);
				A_OverlayPivot(Lay1, .5, 1);
				A_OverlayRotate(Lay1, 0);
				invoker.SawFrame = 2;
			}
		SawReady:
			SAW1 # 1 {
				A_OverlayOffset(Lay1, CFRandom(-.5, .5), CFRandom(-.5, .5), WOF_INTERPOLATE);
				A_OverlayRotate(Lay1, CFRandom(-.25, .25), WOF_INTERPOLATE);
				Player.GetPSprite(Lay1).Frame = invoker.SawFrame;
				A_OverlayScale(Lay1, CFRandom(1, 1.025), CFRandom(1, 1.025), WOF_INTERPOLATE);
				if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("SAW2A0");
				
				if(MRIntW_ChainsawSmoke){
					int index;
					For(index=0;index<invoker.Smokes.Size()+1;index++){
						if(index>=invoker.Smokes.Size()){index=-1;Break;}
						if(!invoker.Smokes[index])Break;
					}
					
					if(index<invoker.Smokes.Size() && index>-1){
						MRIntWeaps_SmokeContainer a = New('MRIntWeaps_SmokeContainer');
						a.Vel = (CFRandom(2.5, 3), -CFRandom(1, 1.2), CFRandom(.2, .6));
						invoker.Smokes[index] = a;
						
						Vector2 Bob = PlayerPawn(Self).BobWeapon(2);
						Bob.Y = 0;//Clamp(Bob.Y, 0, 50);
						
						A_Overlay(SmokeStart+index, "SmokeLay");
						A_OverlayOffset(SmokeStart+index, 190+Bob.X, 180+Bob.Y);
						Player.GetPSprite(SmokeStart+index).Scale = (1,1)*CFRandom(.5, .6);
						A_OverlayAlpha(SmokeStart+index, CFRandom(.01, .08)*4);
					}
				}
			}
			#### ### 1 {
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, CFRandom(-.5, .5), CFRandom(-.5, .5), WOF_INTERPOLATE);
				A_OverlayRotate(Lay1, CFRandom(-.25, .25), WOF_INTERPOLATE);
				A_OverlayScale(Lay1, CFRandom(1, 1.025), CFRandom(1, 1.025), WOF_INTERPOLATE);
				if(GetAge()%Max(1, int(2-invoker.SawSpeed))==0)invoker.SawFrame++;
				if(invoker.SawFrame>5)invoker.SawFrame = 2;
				Player.GetPSprite(Lay1).Frame = invoker.SawFrame;
				if(invoker.SawSpeed>0)invoker.SawSpeed-=Min(invoker.SawSpeed, .5);
			}
			#### # 0{A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);A_OverlayPivot(Lay1, .5, 1);}
			Loop;
		SawLower:
			SAW1 # 1 {
				A_OverlayPivot(Lay1, .5, 1);
				A_OverlayOffset(Lay1, CFRandom(-.5, .5), CFRandom(-.5, .5));
				A_OverlayRotate(Lay1, CFRandom(-.5, .5));
				Player.GetPSprite(Lay1).Frame = invoker.SawFrame;
				A_OverlayScale(Lay1, CFRandom(1, 1.05), CFRandom(1, 1.05), WOF_INTERPOLATE);
				if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("SAW2A0");
			}
			#### # 1{
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, -2, 8, WOF_ADD);
				A_OverlayRotate(Lay1, 1, WOF_ADD);
				A_OverlayScale(Lay1, 0, -.1, WOF_ADD);
			}
			#### ####### 1 {
				A_OverlayOffset(Lay1, -2, 1, WOF_ADD);
				A_OverlayRotate(Lay1, 1, WOF_ADD);
				A_OverlayScale(Lay1, -.025, -.025, WOF_ADD);
			}
			Stop;
		
		Ready:
			TNT1 AA 4 A_WeaponReady();
			#### # 0 {A_Overlay(Lay1, "SawReady", false);if(MRIntW_BetterAlert)A_AlertMonsters(1000);}
			Loop;
		
		SawStartCut:
			SAW1 # 1{
				Player.GetPSprite(Lay1).Frame = invoker.SawFrame;
				A_OverlayPivot(Lay1, .5, 1);
				if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("SAW2A0");
			}
			#### # 1 {A_OverlayScale(Lay1, 1, .8, WOF_INTERPOLATE);}
			#### # 0 A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);
			#### G 1 {
				invoker.Cutting = Null;
				invoker.CuttingWay = 0;
				invoker.SawPos = 1;
				invoker.NoCutting = false;
				invoker.SawSpeed = 0;
				invoker.SawAngle = invoker.SawCurAngle = invoker.SawCurOffset = 0;
				A_OverlayScale(Lay1, 1, 1);
				A_OverlayOffset(Lay1, -invoker.SawPos*2, 6-invoker.SawPos*2.5);
			}
		SawCutLoop:
			#### # 0 {
				FLineTraceData Blade;
				
				double PZ = height * 0.5 - floorclip + player.mo.AttackZOffset*player.crouchFactor;
				LineTrace(Angle, 65, Pitch, OffsetZ:PZ, data:Blade);
				if(!Blade.HitActor||!GetCVar("MRIntW_ChainsawCutting")){invoker.SawPos = 1;invoker.CutDir = false;}
				else invoker.CutDir = !invoker.CutDir;
				
				if(invoker.SawSpeed<30)invoker.SawSpeed+=.25;
			}
			#### #### 1{
				Double RandOfst, Distance;
				if(GetCVar("MRIntW_ChainsawCutting")||GetCVar("MRIntW_ChainsawCuttingWall")){
					FLineTraceData Blade;
					
					double PZ = height * 0.5 - floorclip + player.mo.AttackZOffset*player.crouchFactor;
					LineTrace(Angle, 65, Pitch, OffsetZ:PZ, data:Blade);
					
					if(Blade.HitType!=TRACE_HitNone){
						invoker.SawSpeed = 0;
						if(!Blade.HitActor && GetCVar("MRIntW_ChainsawCuttingWall")){//Cutting a surface
							RandOfst = .3+1-Blade.Distance/65;
							invoker.CuttingWay *= .5;
						}
						else if(GetCVar("MRIntW_ChainsawCutting") && Blade.HitActor){//Cutting someone

							Double Health = Blade.HitActor.Health;
							Double MaxHealth = Max(1, GetDefaultByType(Blade.HitActor.GetClass()).Health);
							invoker.CuttingWay = Clamp((.7-Health/MaxHealth), invoker.CuttingWay -.3, invoker.CuttingWay +.3);//Cutting state
							
							if(!invoker.Cutting||invoker.Cutting!=Blade.HitActor){
								//if just started cutting, select direction
								if(abs(invoker.SawCurAngle)>5){
									if(invoker.SawAngle<90)invoker.SawAngle += 180;
									else invoker.SawAngle -= 180;
									if(invoker.SawAngle<90)invoker.SawAngle = Clamp(invoker.SawAngle, -35, 20);
									else invoker.SawAngle = Clamp(invoker.SawAngle, 160, 200);
								}
								else invoker.SawAngle = CRandomPick(CFRandom(-35, 20), CFRandom(170, 200));
								invoker.CuttingWay = Clamp(-.3, invoker.CuttingWay -.3, invoker.CuttingWay +.3);
							}
							
							invoker.NoCutting = false;
							invoker.Cutting = Blade.HitActor;
						}
						Distance = (1-Blade.Distance/65) * 2;//Distance to object being cutted
					}
					else if(GetCVar("MRIntW_ChainsawCutting")){
						if(invoker.Cutting){//Cutted someone in half
							if(invoker.NoCutting){invoker.Cutting = Null;invoker.NoCutting = false;}
							else {
								invoker.CuttingWay += CFRandom(.3, .5);
								invoker.NoCutting = true;
								invoker.SawSpeed = 2;
							}
						}
						else {//Back to normal position after cutting someone
							invoker.CuttingWay *= .9;
							if(invoker.SawAngle<90)invoker.SawAngle *= .7;
							else invoker.SawAngle = (invoker.SawAngle-180)*.7+180;
							if(invoker.SawAngle<90)invoker.SawCurAngle = Clamp(invoker.SawAngle, invoker.SawCurAngle-10, invoker.SawCurAngle+10);
							else invoker.SawCurAngle = Clamp(invoker.SawAngle-180, invoker.SawCurAngle-10, invoker.SawCurAngle+10);
							invoker.SawCurOffset = Clamp(invoker.SawAngle, invoker.SawCurOffset-10, invoker.SawCurOffset+10);
						}
					}
					
					//Interpolate cutting state
					if(invoker.SawAngle!=invoker.SawCurAngle){
						if(invoker.SawAngle<90)invoker.SawCurAngle = Clamp(invoker.SawAngle, invoker.SawCurAngle -20, invoker.SawCurAngle +20);
						else invoker.SawCurAngle = Clamp(invoker.SawAngle-180, invoker.SawCurAngle -20, invoker.SawCurAngle +20);
						invoker.SawCurOffset = Clamp(invoker.SawAngle, invoker.SawCurOffset -20, invoker.SawCurOffset + 20);
					}
				}
				
				Double BladeOfst = .2;
				if(invoker.SawCurAngle>0)BladeOfst = invoker.SawCurAngle/180;
				
				A_OverlayPivot(Lay1, .8, .2);
				A_OverlayOffset(Lay1, -invoker.SawPos*2 + CRandom(-6, 6)*RandOfst - invoker.CuttingWay*70*BladeOfst*Cos(invoker.SawCurOffset),
					6-invoker.SawPos*2.5 + CRandom(-3, 3)*RandOfst + Distance*2 + invoker.CuttingWay*70*Sin(invoker.SawCurOffset) + CFRandom(-1, 1), WOF_INTERPOLATE);

				A_OverlayRotate(Lay1, CFRandom(-1, 1) + CRandom(-3, 3)*RandOfst - invoker.SawCurAngle, WOF_INTERPOLATE);
				
				Float Scal = 1-.1*invoker.SawPos+ CFRandom(-.1, .1)*RandOfst + Distance*.1 - invoker.CuttingWay*.15 + CFRandom(0, .01);
				Scal = Max(.7, Scal);
				A_OverlayScale(Lay1, Scal, Scal, WOF_INTERPOLATE);
				
				if(invoker.SawPos>0 && !invoker.CutDir)invoker.SawPos -= .25;
				else if(invoker.SawPos<1 && invoker.CutDir)invoker.SawPos += .25;
				
				if(MRIntW_ChainsawSmoke){
					int index;
					For(index=0;index<invoker.Smokes.Size()+1;index++){
						if(index>=invoker.Smokes.Size()){index=-1;Break;}
						if(!invoker.Smokes[index])Break;
					}
					
					if(index<invoker.Smokes.Size() && index>-1){
						MRIntWeaps_SmokeContainer a = New('MRIntWeaps_SmokeContainer');
						a.Vel = (CFRandom(5, 6)*Cos(invoker.SawCurAngle), -CFRandom(1, 1.2) + Sin(invoker.SawCurAngle), -CFRandom(.6, 1));
						invoker.Smokes[index] = a;
												
						A_Overlay(SmokeStart+index, "SmokeLay");
						A_OverlayOffset(SmokeStart+index, 200+Player.GetPSprite(Lay1).X-20*Cos(invoker.SawCurAngle), 170+Player.GetPSprite(Lay1).Y-20*Sin(invoker.SawCurAngle));
						Player.GetPSprite(SmokeStart+index).Scale = (1,1)*CFRandom(.3, .4);
						A_OverlayAlpha(SmokeStart+index, CFRandom(.01, .08)*3);
					}
				}
				
				int Frame = Player.GetPSprite(Lay1).Frame-6;
				int ChainSpeed = Clamp(Min(2, invoker.SawSpeed)+GetAge()%2+CRandomPick(0,0,0,0,0,0,0,0,0,0,0, 1), 0, 2);
				Frame += ChainSpeed;
				
				if(Frame>3)Frame = Frame%3;
				
				Player.GetPSprite(Lay1).Frame = Frame+6;
				
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
			}
			Loop;
		SawStopCut:
			SAW1 G 1{if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("SAW2A0");}
			#### # 1{
				Player.GetPSprite(Lay1).Frame = invoker.SawFrame;
				A_OverlayPivot(Lay1, .5, 1);
				A_OverlayScale(Lay1, 1, .8);
			}
			#### ## 1{
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayScale(Lay1, 0, .1, WOF_ADD);
				if(GetAge()%2==0)invoker.SawFrame++;
				if(invoker.SawFrame>5)invoker.SawFrame = 2;
				Player.GetPSprite(Lay1).Frame = invoker.SawFrame;
			}
			Goto SawReady;
		
		Fire:
			TNT1 A 0 A_Overlay(Lay1, "SawStartCut");
		FireLoop:
			TNT1 AA 4 {
				A_Saw();
				if(MRIntW_Recoil && Player.GetPSprite(Overlayid()).Tics%2==0){//Screen shake
					invoker.Recoil+=1;
					A_SetPitch(Pitch-1*MRIntW_RecoilAmount);
				}
			}
			SAW1 B 0 A_ReFire("FireLoop");
			TNT1 A 0 A_Overlay(Lay1, "SawStopCut");
			Goto Ready;
			
		Sprites:
			SAW2 A 0;
			PUN2 A 0;
			MRD2 A 0;
			Stop;
	}
	
	//Making sound pitch affected by i_timescale
	
	action void A_WeaponReady(int flags = 0)
	{
		if(!player) return;
		
		DoReadyWeaponToSwitch(player, !(flags & WRF_NoSwitch));
		if((flags & WRF_NoFire) != WRF_NoFire)invoker.DoReadyWeaponToFire(player.mo, !(flags & WRF_NoPrimary), !(flags & WRF_NoSecondary));
		if(!(flags & WRF_NoBob))DoReadyWeaponToBob(player);

		player.WeaponState |= GetButtonStateFlags(flags);
		DoReadyWeaponDisableSwitch(player, flags & WRF_DisableSwitch);
	}
	
	static void DoReadyWeaponToFire (PlayerPawn pawn, bool prim, bool alt)
	{
		let player = pawn.player;
		let weapon = player.ReadyWeapon;
		
		if(!weapon)return;

		if(pawn.InStateSequence(pawn.curstate, pawn.MissileState) ||
			pawn.InStateSequence(pawn.curstate, pawn.MeleeState))
		{
			pawn.PlayIdle ();
		}

		let psp = player.GetPSprite(PSP_WEAPON);
		if(weapon.ReadySound && psp && psp.curState == weapon.FindState('Ready'))
		{
			if(!weapon.bReadySndHalf || random[WpnReadySnd]() < 128)pawn.A_StartSound(weapon.ReadySound, CHAN_WEAPON, pitch:i_timescale);
		}

		player.WeaponState |= ((prim ? WF_WEAPONREADY : 0) | (alt ? WF_WEAPONREADYALT : 0));
		return;
	}
	
	Override void PlayUpSound(Actor origin)
	{
		if(UpSound)origin.A_StartSound(UpSound, CHAN_WEAPON, pitch:i_timescale);
	}
	
	action void A_Saw(sound fullsound = "weapons/sawfull", sound hitsound = "weapons/sawhit", int damage = 2, class<Actor> pufftype = "BulletPuff", int flags = 0, double range = 0, double spread_xy = 2.8125, double spread_z = 0, double lifesteal = 0, int lifestealmax = 0, class<BasicArmorBonus> armorbonustype = "ArmorBonus")
	{
		FTranslatedLineTarget t;

		if(!player)return;

		if(!(flags & SF_NORANDOM))damage *=  random[Saw](1, 10);
		if(range == 0)range = MeleeRange + MELEEDELTA + (1. / 65536.); // MBF21 SAWRANGE;

		double ang = angle + spread_xy * (Random2[Saw]() / 255.);
		double slope = AimLineAttack (ang, range, t) + spread_z * (Random2[Saw]() / 255.);

		Weapon weap = player.ReadyWeapon;

		int puffFlags = (flags & SF_NORANDOMPUFFZ) ? LAF_NORANDOMPUFFZ : 0;

		Actor puff;
		int actualdamage;
		[puff, actualdamage] = LineAttack (ang, range, slope, damage, 'Melee', pufftype, puffFlags, t);

		if(!t.linetarget)
		{
			if((flags & SF_RANDOMLIGHTMISS) && (Random[Saw]() > 64))player.extralight = !player.extralight;
			A_StartSound (fullsound, CHAN_WEAPON, pitch:i_timescale);
			return;
		}

		if(flags & SF_RANDOMLIGHTHIT)
		{
			int randVal = Random[Saw]();
			
			if(randVal < 64)player.extralight = 0;
			else if(randVal < 160)player.extralight = 1;
			else player.extralight = 2;
		}

		if(lifesteal && !t.linetarget.bDontDrain)
		{
			if(flags & SF_STEALARMOR)
			{
				if(!armorbonustype)armorbonustype = "ArmorBonus";
				else
				{
					BasicArmorBonus armorbonus = BasicArmorBonus(Spawn(armorbonustype));
					armorbonus.SaveAmount = int(armorbonus.SaveAmount * actualdamage * lifesteal);
					armorbonus.MaxSaveAmount = lifestealmax <= 0 ? armorbonus.MaxSaveAmount : lifestealmax;
					armorbonus.bDropped = true;
					armorbonus.ClearCounters();

					if(!armorbonus.CallTryPickup (self))armorbonus.Destroy ();
				}
			}

			else
			{
				GiveBody(int(actualdamage * lifesteal), lifestealmax);
			}
		}

		A_StartSound(hitsound, CHAN_WEAPON, pitch:i_timescale);
			
		// turn to face target
		if(!(flags & SF_NOTURN))
		{
			double anglediff = deltaangle(angle, t.angleFromSource);

			if(anglediff < 0.0)
			{
				if (anglediff < -4.5)
					angle = t.angleFromSource + 90.0 / 21;
				else
					angle -= 4.5;
			}
			else
			{
				if(anglediff > 4.5)
					angle = t.angleFromSource - 90.0 / 21;
				else
					angle += 4.5;
			}
		}
		if(!(flags & SF_NOPULLIN))
			bJustAttacked = true;
	}
}