Class MRIntWeaps_Chaingun: Chaingun Replaces Chaingun
{
	Default{
		Weapon.SlotNumber 4;
		Weapon.BobStyle "Smooth";
	}
	Const Muzzle0 = 2;//Muzzle corner behind further disk
	Const Muzzle1 = Muzzle0+1;
	Const Muzzle2 = Muzzle0+2;
	Const Muzzle3 = Muzzle0+3;
	Const Muzzle4 = Muzzle0+4;
	Const Muzzle5 = Muzzle0+5;
	Const Lay1 = Muzzle5+1;//Main layer, chaingun base
	Const BarrShadow0 = Lay1+1;//Barrel shadow on disk
	Const BarrShadow1 = BarrShadow0+1;
	Const BarrShadow2 = BarrShadow0+2;
	Const BarrShadow3 = BarrShadow0+3;
	Const BarrShadow4 = BarrShadow0+4;
	Const BarrShadow5 = BarrShadow0+5;
	Const Barrel0 = BarrShadow5+1;//Barrel
	Const Barrel1 = Barrel0+1;
	Const Barrel2 = Barrel0+2;
	Const Barrel3 = Barrel0+3;
	Const Barrel4 = Barrel0+4;
	Const Barrel5 = Barrel0+5;
	Const BarrShadowOver0 = Barrel5+1;//Barrel shadow on other barrels
	Const BarrShadowOver1 = BarrShadowOver0+1;
	Const BarrShadowOver2 = BarrShadowOver0+2;
	Const BarrShadowOver3 = BarrShadowOver0+3;
	Const BarrShadowOver4 = BarrShadowOver0+4;
	Const BarrShadowOver5 = BarrShadowOver0+5;
	Const BarrelOver0 = BarrShadowOver5+1;//Barrel on top of other barrels
	Const BarrelOver1 = BarrelOver0+1;
	Const BarrelOver2 = BarrelOver0+2;
	Const BarrelOver3 = BarrelOver0+3;
	Const BarrelOver4 = BarrelOver0+4;
	Const BarrelOver5 = BarrelOver0+5;
	Const Ring = BarrelOver5+1;//Front disk
	Const Hand = Ring+1;
	Const Lay2 = -2;//Muzzle flash
	Const AnimLay = PSP_FLASH+1;//Animator layer
	
	Const MaxSpeed = 15.;
	
	int BobState, IdleDelay;
	Float Rotation, Speed, GunFrame, HandPos;
	Bool CustomSound, ConterClock;
	
	Vector2 HandVector;
	
	Float Recoil;
	Override Void DoEffect()
	{
		if(Recoil>0){
			Recoil-=.5;
			Owner.A_SetPitch(Owner.Pitch+.5*MRIntW_RecoilAmount);
		}
		
		For(int i=0;i<Casings.Size();i++){
			if(Casings[i])DoCasing(i);
		}
		
		if(abs(Speed)>0){
			if(!(Owner.Player.FindPSprite(PSP_WEAPON) && InStateSequence(Owner.Player.FindPSprite(PSP_WEAPON).CurState, FindState("Fire"))))Speed *= .7;
			Rotation += ConterClock?-Speed:Speed;
			if(Rotation!=0 && (Rotation>360||Rotation<-360))Rotation %= 360;
			
			GunFrame += Speed*.03;
			if(GunFrame>=4)GunFrame -= 4;
			else if(GunFrame<0)GunFrame += 4;
			
			if(Owner.Player.FindPSprite(Lay1))Owner.Player.GetPSprite(Lay1).Frame = GunFrame;
		}
		
		//Disabling weapon interpolation while game is loading
		Let Handler = MRIntWeaps_Handler(EventHandler.Find("MRIntWeaps_Handler"));
		if(Handler && Handler.LoadingDelay>0 && Owner.Player.FindPSprite(Lay1) && Owner.Player.FindPSprite(Lay1).CurState){
			Owner.Player.GetPSprite(Lay1).bInterpolate = false;
			Owner.Player.GetPSprite(Lay1).Y = Max(Owner.Player.GetPSprite(Lay1).Y, 0);
		}
	}
	
	Override Void PostBeginPlay()
	{
		Super.PostBeginPlay();
		Rotation = 30;
	}
	
	action void A_FireCGun()
	{
		if (!player)return;

		Weapon weap = player.ReadyWeapon;
		if (weap != null && invoker == weap && stateinfo != null && stateinfo.mStateType == STATE_Psprite)
		{
			if (!weap.DepleteAmmo (weap.bAltFire, true, 1))
				return;
				
			invoker.CustomSound = CVar.GetCVar("MRIntW_ChaingunSound", players[ConsolePlayer]) && CVar.GetCVar("MRIntW_ChaingunSound", players[ConsolePlayer]).GetBool();
			
			A_StartSound(invoker.CustomSound?"MRIntW/ChaingunFireCustom":"MRIntW/ChaingunFire", CHAN_WEAPON, pitch:i_timescale);

			State flash = weap.FindState('Flash');
			if (flash != null)
			{
				// Removed most of the mess that was here in the C++ code because SetSafeFlash already does some thorough validation.
				State atk = weap.FindState('Fire');
				let psp = player.GetPSprite(PSP_WEAPON);
				if (psp) 
				{
					State cur = psp.CurState;
					int theflash = atk == cur? 0:1;
					player.SetSafeFlash(weap, flash, theflash);
				}
			}
		}
		player.mo.PlayAttacking2 ();

		GunShot (!player.refire, "BulletPuff", BulletSlope ());
	}
		
	Action Void A_BarrelPos(int Num=0, int Lay=Barrel0, bool Anim = true, bool Shadow=false, bool DrawFuzz=false)
	{
		Lay += Num;
		Double CurRotation = invoker.Rotation-Num*60;
		
		A_OverlayOffset(Lay, 
			Player.GetPSprite(Lay1).X+Cos(CurRotation)*30-2, 
			Player.GetPSprite(Lay1).Y+Sin(CurRotation)*20+20, 
			WOF_INTERPOLATE);
			
		if(Shadow){
			if(abs(CurRotation)>360)CurRotation %= 360;

			int Offset = CurRotation;
			if(Offset<0)Offset += 360;
			
			A_OverlayRenderStyle(Lay, STYLE_Normal);
			if(Offset>200 && Offset<340)A_OverlayRenderStyle(Lay, STYLE_None);
		}
		
		if(!DrawFuzz){
			A_OverlayFlags(Lay, PSPF_RENDERSTYLE|PSPF_ALPHA|PSPF_FORCESTYLE|PSPF_FORCEALPHA, true);
			if(GetRenderStyle()!=STYLE_Normal && GetCVar("r_drawfuzz")!=0){//Not drawing overlays if got partial invisibility and fuzz mode is not set to tranlsucent
				A_OverlayRenderStyle(Lay, STYLE_Normal);
				A_OverlayAlpha(Lay, .01);
			}
			else {A_OverlayRenderStyle(Lay, GetRenderStyle());A_OverlayAlpha(Lay, Alpha);}
		}
		
		if(!Anim)Return;
		Double Frame = (Cos(CurRotation)*30+2+30);
		Frame /= 10;
		Player.GetPSprite(Lay).Frame = Frame;
		
		if(Player.GetPSprite(Lay).Frame<1||Player.GetPSprite(Lay).Frame>5){
			A_OverlayTranslation(Lay, "MrChGnBarShad"..int(Min(8, Sin(CurRotation)*10+3)));
		}
		else A_OverlayTranslation(Lay, "None");
	}
	
	Action Void A_BarrelOverPos(int Num=0, int Lay=BarrelOver0, bool Anim=true)
	{
		A_BarrelPos(Num, Lay, Anim);
		Lay += Num;
		Double CurRotation = invoker.Rotation-Num*60;
		
		if(abs(CurRotation)>360)CurRotation %= 360;

		int Offset = CurRotation;
		if(Offset<0)Offset += 360;
		
		A_OverlayRenderStyle(Lay, STYLE_None);
		if(Offset>200 && Offset<340)A_OverlayRenderStyle(Lay, STYLE_Normal);
	}
	
	States{
		Select:
			TNT1 A 0 {invoker.ResetDead();A_Overlay(Lay1, "GunRaise");}
		Raise:
			#### A 1 A_Raise();
			Loop;
		Deselect:
			TNT1 A 0 {
				if(Health<1){A_Overlay(Lay1, "GunDie");Return;}
				A_Overlay(Lay1, "GunLower");
			}
		Lower:
			#### A 1 A_Lower();
			Loop;
			
		Muzzle://End of the barrel behind the furthest disk
			#### # 0 {
				A_OverlayPivot(OverlayId(), .5, 1);
			}
			CH1M # 1 {
				A_BarrelPos(OverlayId()-Muzzle0, Muzzle0, false, DrawFuzz:true);
				Double Scale = (1.-(abs(Player.GetPSprite(OverlayId()).X-Player.GetPSprite(Lay1).X)/120.));
				A_OverlayScale(OverlayId(), Scale, Scale, WOF_INTERPOLATE);
				
				Double CurRotation = invoker.Rotation-(OverlayId()-Muzzle0)*60;
				Double Frame = (Cos(CurRotation)*30+2+30);
				Frame /= 10;
				Player.GetPSprite(OverlayId()).Frame = Clamp(Frame-1, 0, 4);
			}
			Wait;
		
		Shadow://Shadow from barrel on the disk
			#### # 0 A_OverlayFlags(OverlayId(), PSPF_RENDERSTYLE|PSPF_ALPHA, true);
			CH1S A 1 A_BarrelPos(OverlayId()-BarrShadow0, BarrShadow0, false, true);
			Wait;
		ShadowOver://Shadow from barrel on the disk and other barrels
			#### # 0 A_OverlayFlags(OverlayId(), PSPF_RENDERSTYLE|PSPF_ALPHA, true);
			CH1S A 1 A_BarrelOverPos(OverlayId()-BarrShadowOver0, BarrShadowOver0, false);
			Wait;
		Barrel://One of the barrels
			CH1B # 1 A_BarrelPos(OverlayId()-Barrel0);
			Wait;
		BarrelOver://In case the barrel must be drawn on top of the other barrel
			#### # 0 A_OverlayFlags(OverlayId(), PSPF_RENDERSTYLE|PSPF_ALPHA, true);
			CH1B # 1 A_BarrelOverPos(OverlayId()-BarrelOver0);
			Wait;
		Ring://Front disk
			CH1R # 1 {
				Player.GetPSprite(OverlayId()).Frame = Player.GetPSprite(Lay1).Frame;
				Player.GetPSprite(OverlayId()).X = Player.GetPSprite(Lay1).X;
				Player.GetPSprite(OverlayId()).Y = Player.GetPSprite(Lay1).Y;
			}
			Wait;
			
		GunRaise:
			TNT1 A 4 {
				//A_OverlayFlags(Lay1, PSPF_ADDWEAPON , false);
				A_Overlay(Muzzle0, "Muzzle");
				A_Overlay(Muzzle1, "Muzzle");
				A_Overlay(Muzzle2, "Muzzle");
				A_Overlay(Muzzle3, "Muzzle");
				A_Overlay(Muzzle4, "Muzzle");
				A_Overlay(Muzzle5, "Muzzle");
				A_Overlay(BarrShadow0, "Shadow");
				A_Overlay(BarrShadow1, "Shadow");
				A_Overlay(BarrShadow2, "Shadow");
				A_Overlay(BarrShadow3, "Shadow");
				A_Overlay(BarrShadow4, "Shadow");
				A_Overlay(BarrShadow5, "Shadow");
				A_Overlay(Barrel0, "Barrel");
				A_Overlay(Barrel1, "Barrel");
				A_Overlay(Barrel2, "Barrel");
				A_Overlay(Barrel3, "Barrel");
				A_Overlay(Barrel4, "Barrel");
				A_Overlay(Barrel5, "Barrel");
				A_Overlay(BarrShadowOver0, "ShadowOver");
				A_Overlay(BarrShadowOver1, "ShadowOver");
				A_Overlay(BarrShadowOver2, "ShadowOver");
				A_Overlay(BarrShadowOver3, "ShadowOver");
				A_Overlay(BarrShadowOver4, "ShadowOver");
				A_Overlay(BarrShadowOver5, "ShadowOver");
				A_Overlay(BarrelOver0, "BarrelOver");
				A_Overlay(BarrelOver1, "BarrelOver");
				A_Overlay(BarrelOver2, "BarrelOver");
				A_Overlay(BarrelOver3, "BarrelOver");
				A_Overlay(BarrelOver4, "BarrelOver");
				A_Overlay(BarrelOver5, "BarrelOver");
				A_Overlay(Ring, "Ring");
				
				For(int i=Muzzle0;i<=Ring;i++)A_OverlayPivot(i, .5, 0);
				For(int i=Lay1;i<=Ring;i++){
					A_OverlayFlags(i, PSPF_INTERPOLATE, false);
				}
				A_OverlayOffset(Lay1, 51, 0);
			}
			CH1G # 0 {invoker.Speed=-205.6;}
			CH1G ########### 1 {
				For(int i=Muzzle0;i<=Ring;i++)A_OverlayOffset(i, -4, -.5, WOF_ADD);
				For(int i=Muzzle0;i<=Muzzle5;i++){
					if(abs(invoker.Speed)>.1)A_OverlayFlags(i, PSPF_INTERPOLATE, true);
					else A_OverlayFlags(i, PSPF_INTERPOLATE, false);
				}
			}
			CH1G # 1 {
				Vector2 Ofst = (Player.GetPSprite(Lay1).X, Player.GetPSprite(Lay1).Y);
				For(int i=Muzzle0;i<=Ring;i++)A_OverlayOffset(i, -Ofst.X, -Ofst.Y, WOF_ADD);
			}
			#### # 0 {if(CRandom(0, 100)==1)invoker.Speed += CRandom(1, 7)*5;}
		GunWait:
			CH1G # 1 A_IdleAnimation(Lay1);
			#### # 0 {
				For(int i=Muzzle0;i<=Ring;i++){
					if(i<=Muzzle5 && abs(invoker.Speed)>.1)Continue;
					A_OverlayFlags(i, PSPF_INTERPOLATE, false);
				}
			}
			#### # 0 {if(invoker.IdleDelay>35 && abs(invoker.BobState)<2)A_Overlay(Hand, "HandIdle1");}
			Loop;
		GunLower:
			CH1G # 1 {
				A_ClearOverlays(AnimLay, AnimLay);
				Double Ofst = Player.GetPSprite(PSP_WEAPON).Y-Player.GetPSprite(Lay1).Y;
				For(int i=Muzzle0;i<=Ring;i++)A_OverlayPivot(i, .5, 0);
				For(int i=Muzzle0;i<=Ring;i++)A_OverlayFlags(i, PSPF_ADDWEAPON|PSPF_INTERPOLATE, false);
				For(int i=Muzzle0;i<=Ring;i++){Player.GetPSprite(i).Y += Ofst;Player.GetPSprite(i).OldY = Player.GetPSprite(i).Y;}//A_OverlayOffset(i, 0, 32, WOF_ADD|WOF_INTERPOLATE);}
			}
			CH1G ## 1 {
				
				For(int i=Muzzle0;i<=Muzzle5;i++){
					if(abs(invoker.Speed)>.1)A_OverlayFlags(i, PSPF_INTERPOLATE, true);
					else A_OverlayFlags(i, PSPF_INTERPOLATE, false);
				}
				For(int i=Muzzle0;i<=Ring;i++)A_OverlayOffset(i, -1.5, -1.5, WOF_ADD);
				For(int i=Muzzle0;i<=Ring;i++)A_OverlayRotate(i, 1, WOF_ADD);
			}
			CH1G ############# 1 {
				For(int i=Muzzle0;i<=Muzzle5;i++){
					if(abs(invoker.Speed)>.1)A_OverlayFlags(i, PSPF_INTERPOLATE, true);
					else A_OverlayFlags(i, PSPF_INTERPOLATE, false);
				}
				For(int i=Muzzle0;i<=Ring;i++)A_OverlayOffset(i, 5, 7.5, WOF_ADD);
				For(int i=Muzzle0;i<=Ring;i++)A_OverlayRotate(i, -1, WOF_ADD);
				For(int i=Muzzle0;i<=Ring;i++)A_OverlayScale(i, .025, .025, WOF_ADD);
			}
			Wait;
		
		Ready:
			TNT1 A 1 A_WeaponReady();
			Loop;
			
		Fire:
			#### # 0 {if(Player.FindPSprite(Hand))A_Overlay(Hand, "HandIdleBreak");}
			TNT1 AA 4 {
				invoker.ConterClock = GetCVar("MRIntW_ChaingunCounterClock");
				A_FireCGun();
				A_Overlay(Lay2, "GunFlash");
				if(MRIntW_Recoil){//Recoil
					invoker.Recoil+=1;
					A_SetPitch(Pitch-1*MRIntW_RecoilAmount);
					A_SetAngle(Angle);
				}
				A_Overlay(AnimLay, "GunFire");
				
				if(!GetCVar("MRIntW_Casings"))Return;
				int index;
				For(index=0;index<invoker.Casings.Size()+1;index++){
					if(index>=invoker.Casings.Size()){index=-1;Break;}
					if(!invoker.Casings[index]){invoker.Casings[index] = true;Break;}
				}
				
				if(index<invoker.Casings.Size() && index>-1){
					A_Overlay(CaseStart+index, "CasingLay");
					invoker.CasingDir[index] = CFRandom(-.7, 1.5);
				}
			}
			TNT1 A 0 A_ReFire();
			#### # 0 {invoker.IdleDelay = -35*CRandom(20, 40);}
			Goto Ready;
		GunFire://Animator layer
			TNT1 A 1 {//Recoil
				if(invoker.Speed<MaxSpeed)invoker.Speed+=8;
				
				For(int i=Muzzle0;i<=Muzzle5;i++){
					A_OverlayFlags(i, PSPF_INTERPOLATE, true);
				}
				
				Player.GetPSprite(Lay1).OldY = 0;
				
				Vector2 Ofst = (CFRandom(-1.5, 1.5), CFRandom(3, 5));
				For(int i=Muzzle0;i<=Ring;i++)A_OverlayOffset(i, Ofst.X, Ofst.Y, WOF_ADD);
				
				A_OverlayOffset(Lay2, Ofst.X, Ofst.Y, WOF_ADD);
				
			}
			TNT1 A 1 {//Recoil
				if(invoker.Speed<MaxSpeed)invoker.Speed+=8;
				
				Vector2 Ofst = (Player.GetPSprite(Lay1).X*.5, CFRandom(1, 2));
				For(int i=Muzzle0;i<=Ring;i++)A_OverlayOffset(i, Ofst.X, Ofst.Y, WOF_ADD);
				A_OverlayOffset(Lay2, Ofst.X, Ofst.Y, WOF_ADD);
			}
			TNT1 A 1 {//Recover
				if(invoker.Speed<MaxSpeed)invoker.Speed+=8;
				
				Vector2 Ofst = (Player.GetPSprite(Lay1).X, Player.GetPSprite(Lay1).Y);
				For(int i=Muzzle0;i<=Ring;i++){
					A_OverlayOffset(i, -Ofst.X*.5, -Ofst.Y*.5, WOF_ADD);
				}
				A_OverlayOffset(Lay2, -Ofst.X*.5, -Ofst.Y*.5, WOF_ADD);
			}
			TNT1 A 1 {
				if(invoker.Speed<MaxSpeed)invoker.Speed+=8;
				
				Vector2 Ofst = (Player.GetPSprite(Lay1).X, Player.GetPSprite(Lay1).Y);
				Ofst.Y *= CFRandom(.8, .9);
				For(int i=Muzzle0;i<=Ring;i++){
					A_OverlayOffset(i, -Ofst.X, -Ofst.Y, WOF_ADD);
				}
				A_OverlayOffset(Lay2, -Ofst.X, -Ofst.Y, WOF_ADD);
				
				int Dif = (invoker.Rotation+30+Speed)%30;
				if(Dif>1){
					if(Dif>15)invoker.Rotation+=2;
					else invoker.Rotation-=2;
				}
			}
			TNT1 A 1 {//Offset weapon in random direction after firing
				Vector2 Ofst = (CFRandom(-1, 1), CFRandom(-1, 1)*3);
				For(int i=Muzzle0;i<=Ring;i++){
					A_OverlayOffset(i, -Ofst.X, -Ofst.Y, WOF_ADD);
				}
				A_OverlayOffset(Lay2, -Ofst.X, -Ofst.Y, WOF_ADD);
			}
			TNT1 A 3 {//Recover
				Vector2 Ofst = (Player.GetPSprite(Lay1).X, Player.GetPSprite(Lay1).Y);
				For(int i=Muzzle0;i<=Ring;i++){
					A_OverlayOffset(i, -Ofst.X, -Ofst.Y, WOF_ADD);
				}
				A_OverlayOffset(Lay2, -Ofst.X, -Ofst.Y, WOF_ADD);
			}
			Stop;
		AltFire:
			TNT1 A 1 {
				if(abs(invoker.Speed)<20)invoker.Speed--;
				
				For(int i=Muzzle0;i<=Muzzle5;i++){
					A_OverlayFlags(i, PSPF_INTERPOLATE, true);
				}
			}
			Goto Ready;
		GunFlash://Muzzle flash
			CH1F A 1 BRIGHT {
				A_OverlayOffset(Lay2, Player.GetPSprite(Lay1).X, Player.GetPSprite(Lay1).Y);
				Float Scale;
				if(invoker.Speed<10)Scale = .7;
				else Scale = 1;
				A_OverlayScale(Lay2, Scale*CFRandom(1, 1.025), Scale*CFRandom(1, 1.025), WOF_INTERPOLATE);
				
				A_OverlayFlags(Lay2, PSPF_RENDERSTYLE|PSPF_ALPHA|PSPF_FORCESTYLE, true);
				A_OverlayRenderStyle(Lay2, STYLE_ColorAdd);
				A_OverlayPivot(Lay2, .5, 1);
				if(GetCVar("MRIntW_NoMuzzleFlash"))A_OverlayRenderStyle(OverlayId(), STYLE_None);
			}
			CH1F ABBBB 1 BRIGHT {
				A_OverlayOffset(Lay2, Player.GetPSprite(Lay1).X, Player.GetPSprite(Lay1).Y, wof_interpolate);
				Float Scale = Min(1, invoker.Speed/MaxSpeed);
				A_OverlayScale(Lay2, Scale*CFRandom(.93, 1), Scale*CFRandom(.93, 1), WOF_INTERPOLATE);
			}
			Stop;
		Flash:
			TNT1 A 5 A_Light1();
			Goto LightDone;
			TNT1 A 5 A_Light2();
			Goto LightDone;
		
		HandIdle1:
			CH1H A 1{
				invoker.HandPos = 1;
				if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(OverlayId()).Sprite = GetSpriteIndex("CH2HA0");
				A_OverlayPivot(Hand, 1, 0);
				A_OverlayOffset(Hand, -5*invoker.HandPos, 20*invoker.HandPos);
				A_OverlayScale(Hand, .8+invoker.HandPos*.5, .8+invoker.HandPos*.5);
				
				invoker.IdleDelay = -35*CRandom(20, 40);
			}
			#### ######## 1{
				invoker.HandPos -= .125;
			
				Vector2 BarOfst;
				BarOfst.X += Cos(invoker.Rotation%60)*16;
				BarOfst.Y += Sin(invoker.Rotation%60)*15;
				
				A_OverlayOffset(Hand, -5*invoker.HandPos-BarOfst.X, 20*invoker.HandPos-BarOfst.Y, WOF_INTERPOLATE);
				A_OverlayScale(Hand, .8+invoker.HandPos*.5, .8+invoker.HandPos*.5, WOF_INTERPOLATE);
				A_OverlayRotate(Hand, -6+6*invoker.HandPos, WOF_INTERPOLATE);
			}
			#### # 0 A_Overlay(Hand+1, "IdleRotate");
			#### # 1{
				Vector2 BarOfst;
				BarOfst.X += Cos(invoker.Rotation%60)*16;
				BarOfst.Y += Sin(invoker.Rotation%60)*15;
				
				A_OverlayOffset(Hand, -5*invoker.HandPos-BarOfst.X, 20*invoker.HandPos-BarOfst.Y, WOF_INTERPOLATE);
				A_OverlayScale(Hand, .8+invoker.HandPos*.5, .8+invoker.HandPos*.5, WOF_INTERPOLATE);
				A_OverlayRotate(Hand, -6+6*invoker.HandPos, WOF_INTERPOLATE);
				
				invoker.HandVector = (Player.GetPSprite(Hand).X, Player.GetPSprite(Hand).Y);
			}
			#### # 1{
				invoker.HandVector = (Player.GetPSprite(Hand).X, Player.GetPSprite(Hand).Y);
				Vector2 BarOfst;
				BarOfst.X += Cos(invoker.Rotation%60)*16;
				BarOfst.Y += Sin(invoker.Rotation%60)*15;
				
				Vector2 Dir = (-5*invoker.HandPos-BarOfst.X, 20*invoker.HandPos-BarOfst.Y);
				
				Dir = ((Dir.X-invoker.HandVector.X)*.5, (Dir.Y-invoker.HandVector.Y)*.5);
				
				A_OverlayOffset(Hand, Dir.X, Dir.Y, WOF_ADD);
				A_OverlayScale(Hand, .8+invoker.HandPos*.5, .8+invoker.HandPos*.5, WOF_INTERPOLATE);
				A_OverlayRotate(Hand, -6+6*invoker.HandPos, WOF_INTERPOLATE);
				
				invoker.HandVector = (Player.GetPSprite(Hand).X, Player.GetPSprite(Hand).Y);
			}
			Wait;
		HandIdleEnd:
			CH1H A 1{
				if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(OverlayId()).Sprite = GetSpriteIndex("CH2HA0");
				A_OverlayPivot(Hand, 1, 0);
				
				A_OverlayOffset(Hand, invoker.HandVector.X, invoker.HandVector.Y);
				A_OverlayScale(Hand, .8+invoker.HandPos*.5, .8+invoker.HandPos*.5);
				A_OverlayRotate(Hand, -6+6*invoker.HandPos);
			}
			#### ########## 1{
				invoker.HandPos += .1;
				
				A_OverlayOffset(Hand, -1, 3, WOF_ADD);
				A_OverlayScale(Hand, .8+invoker.HandPos*.5, .8+invoker.HandPos*.5, WOF_INTERPOLATE);
				A_OverlayRotate(Hand, -6+6*invoker.HandPos, WOF_INTERPOLATE);
			}
			#### # 0 {invoker.HandPos = 1;}
			Stop;
		HandIdleBreak:
			CH1H A 1{
				if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(OverlayId()).Sprite = GetSpriteIndex("CH2HA0");
				A_OverlayPivot(Hand, 1, 0);
				A_ClearOverlays(Hand+1, Hand+1);
				
				A_OverlayOffset(Hand, invoker.HandVector.X-3, invoker.HandVector.Y-3);
				A_OverlayScale(Hand, .8+invoker.HandPos*.5, .8+invoker.HandPos*.5);
				A_OverlayRotate(Hand, -6+6*invoker.HandPos-2);
			}
			#### # 1{
				if(invoker.HandPos>=1){
					A_ClearOverlays(Hand, Hand);
					Return;
				}
				invoker.HandPos += .4;

				A_OverlayOffset(Hand, -1, 9, WOF_ADD);
				A_OverlayScale(Hand, .8+invoker.HandPos*.5, .8+invoker.HandPos*.5, WOF_INTERPOLATE);
				A_OverlayRotate(Hand, -6+6*invoker.HandPos-2, WOF_INTERPOLATE);
			}
			Wait;
		HandIdleEndSpeen:
			CH1H A 1{
				A_OverlayPivot(Hand, 1, 0);
				if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(OverlayId()).Sprite = GetSpriteIndex("CH2HA0");
				A_OverlayOffset(Hand, invoker.HandVector.X-2, invoker.HandVector.Y+3);
			}
			#### # 1{
				if(invoker.HandPos>=1.25){
					A_ClearOverlays(Hand, Hand);
					Return;
				}
				invoker.HandPos += .4;
				A_OverlayOffset(Hand, -4, 7, WOF_ADD);
				A_OverlayScale(Hand, .01, .01, WOF_ADD);
				invoker.Speed -= CRandom(40, 60);
			}
			Wait;
		IdleRotate:
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1{
				invoker.Speed -= CFRandom(.1, .5);
				A_SetTics(CRandom(1, 2));
			}
			#### # 0 A_JumpIf(CRandom(0, 10)>3, 2);
			#### A 0 A_Overlay(Hand, "HandIdleEndSpeen");
			Stop;
			TNT1 A 0 A_Overlay(Hand, "HandIdleEnd");
			Stop;
			
		Sprites:
			MRD2 A 0;
			CH2H A 0;
			Stop;
	}
	
	Action Void A_IdleAnimation(int Lay = 2)//Idle hand bobbing
	{
		Bool Bers = (GetCVar("MRIntW_BersShake")>1 && FindInventory("PowerStrength"))||(Health<20 && GetCVar("MRIntW_LowHealthShake"));
		Float Mult = (Bers?2:1);
		if(invoker.BobState>=0){
			For(int i=Muzzle0;i<=Ring;i++){
				A_OverlayOffset(i, CFRandom(-.005, .015)*Mult, CFRandom(.01, .02)*Mult, WOF_ADD);
			}
			if(invoker.BobState>90||Player.GetPSprite(Lay).Y>1.35){invoker.BobState = -invoker.BobState;}
			else invoker.BobState+=Mult;
		}
		else {
			For(int i=Muzzle0;i<=Ring;i++){
				A_OverlayOffset(i, -CFRandom(-.005, .015)*Mult, -CFRandom(.01, .02)*Mult, WOF_ADD);
			}
			
			if(Player.GetPSprite(Lay).Y<0){A_OverlayOffset(Lay, 0, 0, WOF_INTERPOLATE);invoker.BobState = 0;}
			else if(!(invoker.BobState>=-1&&Player.GetPSprite(Lay).Y>0))invoker.BobState+=Mult;
		}
		if(Bers && GetAge()%2==0){//Shaking hand if got berserk
			A_OverlayOffset(Lay, CFRandom(-.25, .25), 0, WOF_INTERPOLATE|WOF_KEEPY);
			Player.GetPSprite(Lay).Y += CFRandom(-.25, .25);
			if(Player.GetPSprite(Lay).Y<0){Player.GetPSprite(Lay).Y = 0;invoker.BobState = 0;}
			if(Player.GetPSprite(Lay).Y>1.35){Player.GetPSprite(Lay).Y = 1.35;invoker.BobState = -90;}
		}
		
		if(GetCVar("MRIntW_IdleAnims"))invoker.IdleDelay++;
	}
	
	Action Void A_BersShake()
	{
		if((GetCVar("MRIntW_BersShake")>1 && FindInventory("PowerStrength"))||(Health<20 && GetCVar("MRIntW_LowHealthShake")))A_OverlayOffset(Lay1, CFRandom(-.25, .25), CFRandom(-.25, .25), WOF_ADD);
	}
}