/*
	Super Shotgun casings, smoke and dying animations
	
	Moved from Effects.zsc and Dying.zsc for doom 1 compatibility
*/

Extend Class MRIntWeaps_SuperShotgun
{
	MRIntWeaps_SmokeContainer Smokes[20];
	Const SmokeStart = PSP_FLASH+10;
		
	Const CaseStart = PSP_FLASH+30;
	Const CaseLife = 20;
	Bool Casings[4];
	Float CasingDir[4], CasingSpeed;
	Void DoCasing(int index)
	{
		int Lay = index+CaseStart;
		Let PSP = Owner.Player.GetPSprite(Lay);
		if(!PSP)Return;
		int Age = PSP.Tics;
		if(Age<2||Age>=CaseLife)Return;
		
		if(PSP.Y>280+32){
			Owner.Player.SetPsprite(Lay, Null);
			Casings[index] = false;
			Return;
		}
		
		//if(Age<CaseLife)PSP.bInterpolate = true;
		Double VelZ = (Age-(CaseLife-4-Min(2, 1-CasingSpeed)))/2;
		VelZ /= CaseLife/2;
		if(VelZ==0)VelZ = -.01;
		
		Double VelMult = CFRandom(1, 2*CasingSpeed);
		
		if(Age<CaseLife-5)PSP.Y += Owner.Player.Cmd.Pitch*.005;
		PSP.Y += Owner.Vel.Z;
		PSP.Y -= VelZ*80*VelMult;
		if(Age<CaseLife-5)PSP.X += Owner.Player.Cmd.Yaw*.005;
		PSP.X -= 21*VelMult*PSP.Scale.X;
		PSP.Scale *= 1+.05*CasingDir[index]*CasingSpeed;
		PSP.Scale.X += (Owner.Vel.X*Cos(Owner.Angle) + Owner.Vel.Y*Sin(Owner.Angle))*.005;
		PSP.Scale.X = Max(0, PSP.Scale.X);
		PSP.Scale.Y = PSP.Scale.X;
		PSP.Rotation += 40;
		
		//Console.Printf("Casing "..CaseLife-Age..": "..PSP.Y);
	}
	
	States{
		SmokeLay:
			NSHS A 0{
				A_OverlayFlags(OverlayId(), PSPF_ADDBOB|PSPF_ADDWEAPON, false);
				A_OverlayFlags(OverlayId(), PSPF_ALPHA|PSPF_RENDERSTYLE|PSPF_FORCESTYLE, True);
				A_OverlayRenderStyle(OverlayId(), MRIntW_Smoke?STYLE_TRANSLUCENT:STYLE_NONE);
				A_OverlayPivot(OverlayId(), .5, .5);
				Player.GetPSprite(OverlayId()).Alpha=CFRandom(.3, .5);
			}
			NSHS A 1{
				A_OverlayFlags(OverlayId(), PSPF_ALPHA|PSPF_RENDERSTYLE|PSPF_FORCESTYLE, True);
				A_OverlayRenderStyle(OverlayId(), MRIntW_Smoke?STYLE_TRANSLUCENT:STYLE_NONE);
			}
			NSHS AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1{
				int Index = OverlayId()-SmokeStart;
				A_OverlayFlags(OverlayId(), PSPF_INTERPOLATE, true);
				
				Player.GetPSprite(OverlayId()).Alpha-=.01;
				
				Float Scale = (CFRandom(.09, .13));// + (Vel.X*Cos(Angle) + Vel.Y*Sin(Angle))*.2);
				Scale -= (invoker.Smokes[Index].Vel.Y);
				Scale *= .1;
				A_OverlayScale(OverlayId(), Max(0, Scale), Max(0, Scale), WOF_ADD);
				
				if(Player.GetPSprite(OverlayId()).Scale.Length()>15)A_OverlayRenderStyle(OverlayId(), STYLE_NONE);
				//Player.GetPSprite(OverlayId()).Y += Player.Cmd.Pitch*.02 + Max(0, (Vel.X*Cos(Angle) + Vel.Y*Sin(Angle))*.1) + Vel.Z;
				Player.GetPSprite(OverlayId()).Y -= invoker.Smokes[Index].Vel.Z*Cos(Pitch);
				Player.GetPSprite(OverlayId()).Y += invoker.Smokes[Index].Vel.Y*Sin(Pitch);
				//Player.GetPSprite(OverlayId()).X += Player.Cmd.Yaw*.02 + (Vel.X*Cos(Angle+90) + Vel.Y*Sin(Angle+90)*5);
				Player.GetPSprite(OverlayId()).X += invoker.Smokes[index].Vel.X;
				
				if(Player.GetPSprite(OverlayId()).Alpha<=0){
					invoker.Smokes[OverlayId()-SmokeStart].Destroy();invoker.Smokes[OverlayId()-SmokeStart]=Null;
					A_ClearOverlays(OverlayId(), OverlayId());
					Return;
				}
			}
			TNT1 A 0 {invoker.Smokes[OverlayId()-SmokeStart].Destroy();invoker.Smokes[OverlayId()-SmokeStart]=Null;}
			Stop;
		
		CasingLay:
			SHTC A 0 {
				A_OverlayFlags(Overlayid(), PSPF_ADDWEAPON, false);
				A_OverlayFlags(OverlayId(), PSPF_RENDERSTYLE|PSPF_FORCESTYLE, true);
				A_OverlayRenderStyle(OverlayID(), STYLE_NORMAL);
				Player.GetPSprite(OverlayId()).bInterpolate=false;
				A_OverlayPivot(OverlayId(), .5, .5);
				A_OverlayScale(OverlayId(), 1.14, 1.14);
				invoker.CasingSpeed = GetCVar("MRIntW_CasingsRandom");
			}
			SHTC A 20 A_SetTics(CaseLife);
			TNT1 A 0 {Player.GetPSprite(OverlayId()).bInterpolate=invoker.Casings[OverlayId()-CaseStart]=false;}
			Stop;
	}
}

Extend Class MRIntWeaps_SuperShotgun
{
	Const DieLay = -2;
	
	MRIntWeaps_FlyingWeapon Dropped;
	Action Void A_DropWeapon()
	{
		if(invoker.Dropped)invoker.Dropped.Destroy();
		
		Vector3 Pos = (Self.Pos.XY, Player.ViewZ);
		Pos.XY += AngleToVector(Angle, 5*Cos(abs(Pitch)));
		Pos.Z -= 5*Sin(Pitch);
		
		Double Strn = 8;
		Vector3 Vel = Self.Vel;
		Vel += AngleToVector(Angle, Strn*Cos(abs(Pitch)));
		Vel.Z -= Strn*Sin(Pitch);

		invoker.Dropped = MRIntWeaps_FlyingWeapon.Throw("SH2TA0", Pos, Vel, Caller:Self);
	}
	
	Double DeadAngle;
	int DeadHandL, DeadHandR;
	Array<int> DeadVariants;
	Void ResetDead()
	{
		DeadVariants.Clear();
		For(int i=0;i<4;i++)DeadVariants.Push(i);
		
		if(Dropped)Dropped.Destroy();
	}
	
	States
	{
		GunDie:
			TNT1 A 0 {
				A_DropWeapon();
			}
			MRD1 A 0 {if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(Lay1).Sprite = GetSPriteIndex("MRD2A0");}
		DyingRightHand:
			#### # 1 {
				int L, R;
				L = invoker.DeadVariants[CRandom(0, invoker.DeadVariants.Size()-1)];
				invoker.DeadVariants.Delete(L);
				R = invoker.DeadVariants[CRandom(0, invoker.DeadVariants.Size()-1)];
				invoker.DeadVariants.Delete(R);
				
				invoker.DeadHandL = L;
				invoker.DeadHandR = R;
				
				A_OverlayPivot(Lay1, 0, .5);
				A_OverlayScale(Lay1, .9, .9);
				A_OverlayOffset(Lay1, 5, -10, WOF_ADD);
			}
			#### BBB 1{
				A_OverlayScale(Lay1, .2, .2, WOF_ADD);
				A_OverlayOffset(Lay1, 5, 20, WOF_ADD);
			}
			#### # 0{
				A_Overlay(DieLay, "DeadLeftHand");
				invoker.DeadAngle = Angle;
				Switch(invoker.DeadHandR)
				{
					Case 1:
						A_Overlay(OverlayId(), "RightHandDead1");
						Break;
					Case 2:
						A_Overlay(OverlayId(), "RightHandDead2");
						Break;
					Case 3:
						A_Overlay(OverlayId(), "RightHandDead3");
				}
			}
			#### C 1{
				A_OverlayOffset(Lay1, 90, (Player.ViewZ-FloorZ)*6+20, WOF_INTERPOLATE);
				A_OverlayScale(Lay1, 1.2, 1.2);
				A_OverlayRotate(Lay1, 30);
			}
			#### D 1{
				A_OverlayOffset(Lay1, 90, (Player.ViewZ-FloorZ)*6+20, WOF_INTERPOLATE);
				A_OverlayScale(Lay1, 1.2, 1.2);
				A_OverlayRotate(Lay1, 30);
			}
			Wait;
		RightHandDead1:
			MRD1 A 0 {if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(OverlayId()).Sprite = GetSPriteIndex("MRD2A0");}
			#### D 1 {
				A_OverlayOffset(Lay1, 70-DeltaAngle(Angle, invoker.DeadAngle)*4, (Player.ViewZ-FloorZ)*6-20, WOF_INTERPOLATE);
				A_OverlayRotate(Lay1, 8);
				A_OverlayScale(Lay1, 1.4, 1.3);
			}
			Wait;
		RightHandDead2:
			MRD1 A 0 {if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(OverlayId()).Sprite = GetSPriteIndex("MRD2A0");}
			#### F 1 {
				A_OverlayOffset(Lay1, 150-DeltaAngle(Angle, invoker.DeadAngle)*4, (Player.ViewZ-FloorZ)*6+100, WOF_INTERPOLATE);
				A_OverlayRotate(Lay1, 80);
				A_OverlayScale(Lay1, 1, .9);
			}
			Wait;
		RightHandDead3:
			MRD1 A 0 {if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(OverlayId()).Sprite = GetSPriteIndex("MRD2A0");}
			#### G 1 {
				A_OverlayFlags(Lay1, PSPF_FLIP, true);
				A_OverlayOffset(Lay1, 240-DeltaAngle(Angle, invoker.DeadAngle)*4, (Player.ViewZ-FloorZ)*6, WOF_INTERPOLATE);
				A_OverlayRotate(Lay1, 10);
				A_OverlayScale(Lay1, 1.3, 1);
			}
			Wait;
			
		DyingLeftHand:
			MRD1 A 0 {if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(DieLay).Sprite = GetSPriteIndex("MRD2A0");}
			#### E 1 {
				A_OverlayFlags(DieLay, PSPF_FLIP, true);
				A_OverlayFlags(DieLay, PSPF_ADDWEAPON, false);
				A_OverlayOffset(DieLay, -140, 25);
				A_OverlayRotate(DieLay, -20);
			}
			#### # 1 {
				A_OverlayOffset(DieLay, -20, -15, WOF_ADD);
				A_OverlayRotate(DieLay, -5, WOF_ADD);
				A_OverlayScale(DieLay, .1, .1, WOF_ADD);
			}
			#### ### 1 {
				A_OverlayOffset(DieLay, -15, 15, WOF_ADD);
				A_OverlayRotate(DieLay, -5, WOF_ADD);
				A_OverlayScale(DieLay, .1, .1, WOF_ADD);
			}
		DeadLeftHand:
			MRD1 A 0 {if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(DieLay).Sprite = GetSPriteIndex("MRD2A0");}
			#### # 0 {
				Switch(invoker.DeadHandL)
				{
					Case 2:
						A_Overlay(OverlayId(), "LeftHandDead2");
						Break;
					Case 3:
						A_Overlay(OverlayId(), "LeftHandDead3");
				}
			}
			#### CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC 1 {
				A_OverlayFlags(DieLay, PSPF_FLIP, true);
				A_OverlayFlags(DieLay, PSPF_ADDWEAPON, false);
				A_OverlayOffset(DieLay, -130, (Player.ViewZ-FloorZ)*6, WOF_INTERPOLATE);
				A_OverlayRotate(DieLay, 30);
				A_OverlayScale(DieLay, 1, 1);
				A_SetTics(CRandom(0, 1));
			}
			#### D 1 {
				A_OverlayOffset(DieLay, -130, (Player.ViewZ-FloorZ)*6, WOF_INTERPOLATE);
				A_OverlayRotate(DieLay, 30);
				A_OverlayScale(DieLay, 1, 1);
			}
			Wait;
		LeftHandDead2:
			MRD1 A 0 {if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(OverlayId()).Sprite = GetSPriteIndex("MRD2A0");}
			#### F 1 {
				A_OverlayFlags(DieLay, PSPF_FLIP, true);
				A_OverlayOffset(DieLay, 0-DeltaAngle(Angle, invoker.DeadAngle)*4, (Player.ViewZ-FloorZ)*6+100, WOF_INTERPOLATE);
				A_OverlayRotate(DieLay, 80);
				A_OverlayScale(DieLay, 1, .9);
			}
			Wait;
		LeftHandDead3:
			MRD1 A 0 {if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(OverlayId()).Sprite = GetSPriteIndex("MRD2A0");}
			#### G 1 {
				A_OverlayFlags(DieLay, PSPF_FLIP, false);
				A_OverlayOffset(DieLay, 0-DeltaAngle(Angle, invoker.DeadAngle)*4, (Player.ViewZ-FloorZ)*6, WOF_INTERPOLATE);
				A_OverlayRotate(DieLay, 10);
				A_OverlayScale(DieLay, 1.3, 1);
			}
			Wait;
	}
}