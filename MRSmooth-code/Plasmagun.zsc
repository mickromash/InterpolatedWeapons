Class MRIntWeaps_PlasmaRifle: PlasmaRifle Replaces PlasmaRifle
{
	Default{
		Weapon.SlotNumber 6;
		Weapon.BobStyle "Smooth";
	}
	Const Lay1 = 2;//Main frame (body)
	Const Lay2 = -2;//Muzzle
	Const Lay3 = -3;//Muzzle flash
	
	int BobState;
	Double Muzzle,//Muzzle position
			BodyScal;//Rifle body sqiush during firing
	Bool MuzzleCycle,//Firing frame
		InterpolateMuzzle;
	
	Float Recoil;
	Override Void DoEffect()
	{
		if(Recoil>0){Recoil-=.5;Owner.A_SetPitch(Owner.Pitch+.25*MRIntW_RecoilAmount);}
		
		//Disabling weapon interpolation while game is loading
		Let Handler = MRIntWeaps_Handler(EventHandler.Find("MRIntWeaps_Handler"));
		if(Handler && Handler.LoadingDelay>0 && Owner.Player.FindPSprite(Lay1) && Owner.Player.FindPSprite(Lay1).CurState){
			Owner.Player.GetPSprite(Lay1).bInterpolate = false;
			Owner.Player.GetPSprite(Lay1).Y = Max(Owner.Player.GetPSprite(Lay1).Y, 0);
			if(!Owner.Player.FindPSprite(Lay2) || !Owner.Player.FindPSprite(Lay2).CurState)Return;
			Owner.Player.GetPSprite(Lay2).bInterpolate = false;
			Owner.Player.GetPSprite(Lay2).Y = Max(Owner.Player.GetPSprite(Lay2).Y, 0);
		}
	}
	
	States{
		Select:
			TNT1 A 0 A_Overlay(Lay1, "GunRaise");
		Raise:
			#### A 1 A_Raise();
			Loop;
		Deselect:
			TNT1 A 0 A_Overlay(Lay1, "GunLower");
		Lower:
			#### A 1 A_Lower();
			Loop;
		Ready:
			TNT1 A 1 A_WeaponReady();
			Loop;
			
		GunMuzzle:
			PLSO # 1{
				if(invoker.InterpolateMuzzle){
					Player.GetPSprite(Lay2).OldX = Player.GetPSprite(Lay1).OldX;
					Player.GetPSprite(Lay2).OldY = Player.GetPSprite(Lay1).OldY + invoker.Muzzle*3;
					Player.GetPSprite(Lay2).X = Player.GetPSprite(Lay1).X;
					Player.GetPSprite(Lay2).Y = Player.GetPSprite(Lay1).Y + invoker.Muzzle*3;
					Player.GetPSprite(Lay2).Scale = Player.GetPSprite(Lay1).Scale + (.01,.01)*invoker.Muzzle;
					Player.GetPSprite(Lay2).Scale.X -= invoker.BodyScal;
					Player.GetPSprite(Lay2).Rotation = Player.GetPSprite(Lay1).Rotation;
					Player.GetPSprite(Lay2).Pivot = Player.GetPSprite(Lay1).Pivot;
					A_OverlayFlags(Lay2, PSPF_INTERPOLATE, true);
				}
				else {
					Player.GetPSprite(Lay2).ResetInterpolation();
				}
				Player.GetPSprite(Lay2).Frame = Player.GetPSprite(Lay1).Frame;
				if(Player.FindPSprite(Lay3))Player.FindPSprite(Lay3).Frame = Player.GetPSprite(Lay2).Frame;
			}
			#### # 0 A_JumpIf(Player.GetPSprite(Lay1).Sprite == GetSpriteIndex("PLSRA0"), Null);
			Loop;
		GunMuzzleReload:
			PLSO H 1;
			Loop;
		GunMuzzleFlash:
			PLSF # 1 BRIGHT{
				A_OverlayFlags(Lay3, PSPF_RENDERSTYLE|PSPF_ALPHA, true);
				A_OverlayRenderStyle(Lay3, STYLE_ColorAdd);
				A_OverlayAlpha(Lay3, 1);
			}
			Loop;
		
		GunRaise:
			TNT1 AAAA 1 {
				A_OverlayFlags(Lay1, PSPF_ADDWEAPON , false);
				A_OverlayPivot(Lay1, .5, 0);
				A_OverlayOffset(Lay1, -20, 145);
				A_OverlayScale(Lay1, 1.1, 1.1);
				A_OverlayRotate(Lay1, 4);
			}
			PLSR B 1{if(MRIntW_BrownGloves)Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("PL2RA0");}
			#### ######## 1 {
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, 8, -8, WOF_ADD);
				A_OverlayScale(Lay1, -.03, -.03, WOF_ADD);
				A_OverlayRotate(Lay1, -.6, WOF_ADD);
			}
			#### ## 1 {
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, 8, -8, WOF_ADD);
				A_OverlayScale(Lay1, 0, -.03, WOF_ADD);
				A_OverlayRotate(Lay1, -.6, WOF_ADD);
			}
			#### A 0 {
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);
				invoker.InterpolateMuzzle = false;
				A_Overlay(Lay2, "GunMuzzle");
				A_OverlayScale(Lay2, .95, .95);
				A_OverlayRotate(Lay2, 0);
				A_OverlayOffset(Lay2, 0, -6);
			}
			PL1G A 1 {
				A_OverlayFlags(Lay1, PSPF_ADDWEAPON , true);
				A_OverlayOffset(Lay1, 0, 0);
				A_OverlayScale(Lay1, 1, 1);
				A_OverlayRotate(Lay1, 0);
				invoker.InterpolateMuzzle = true;
				A_OverlayPivot(Lay1, .5, 1);
				A_OverlayPivot(Lay2, .5, 1);
			}
		GunWait:
			PL1G A 1 {
				A_IdleAnimation(Lay1);
				if(invoker.Muzzle>.1)invoker.Muzzle *= .5;
				else invoker.Muzzle = 0;
			}
			#### # 0 {A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);A_OverlayFlags(Lay2, PSPF_INTERPOLATE, true);}
			Loop;
		GunLower:
			#### # 0 A_ClearOverlays(Lay2, Lay2);
			PLSR B 1 {
				A_OverlayFlags(Lay1, PSPF_ADDWEAPON, false);
				if(MRIntW_BrownGloves)Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("PL2RA0");
				A_OverlayPivot(Lay1, .5, 0);
				A_OverlayScale(Lay1, .8, .75);
				A_OverlayOffset(Lay1, 50, 45+(Player.GetPSprite(PSP_WEAPON).Y));
			}
			#### ## 1{
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, 6, -6, WOF_ADD);
				A_OverlayRotate(Lay1, -3.5, WOF_ADD);
			}
			#### # 1{
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, 1.5, -1.5, WOF_ADD);
				A_OverlayRotate(Lay1, -1, WOF_ADD);
			}
			#### ############# 1 {
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, -7, 10, WOF_ADD);
				A_OverlayRotate(Lay1, 2, WOF_ADD);
				A_OverlayScale(Lay1, .025, .025, WOF_ADD);
			}
			Stop;
			
		Fire:
			#### # 0 {Player.SetPsprite(Lay1, invoker.FindState("GunFire"));}
			TNT1 A 3 {
				A_FirePlasma();
				if(MRIntW_Recoil){invoker.Recoil+=1;A_SetPitch(Pitch-.5*MRIntW_RecoilAmount);}//Recoil
			}
			TNT1 B 20 A_ReFire();
			Goto Ready;
		GunFire:
			#### # 0 {A_Overlay(Lay2, "GunMuzzle", true);A_Overlay(Lay3, "GunMuzzleFlash", true);}
			#### # 0 {
				invoker.Muzzle = -1.5;
				invoker.InterpolateMuzzle = false;
				//A_OverlayOffset(Lay3, Player.GetPSprite(Lay2).X, Player.GetPSprite(Lay2).Y);
			}
			PL1G ### 1{
				Vector2 Rand = (FRandom(-1, 1), FRandom(0, 1));
				A_OverlayOffset(Lay1, Rand.X, Rand.Y, WOF_INTERPOLATE);
				A_OverlayOffset(Lay2, Rand.X, Rand.Y + invoker.Muzzle*3, WOF_INTERPOLATE);
				A_OverlayOffset(Lay3, Rand.X, Rand.Y + invoker.Muzzle*3, WOF_INTERPOLATE);
				
				Float Scal = FRandom(1, 1.1);
				invoker.BodyScal = -FRandom(-.05, .05);
				A_OverlayScale(Lay1, Scal+invoker.BodyScal, Scal, WOF_INTERPOLATE);
				

				invoker.Muzzle += .8;
				if(Player.GetPSprite(Lay1).Frame<1)Player.GetPSprite(Lay1).Frame = 1;
				else if(Player.GetPSprite(Lay1).Frame<6)Player.GetPSprite(Lay1).Frame++;
				else Player.GetPSprite(Lay1).Frame = 1;
				if(Player.GetPSprite(Lay1).Frame==1){
					Player.GetPSprite(Lay3).bFlip = Player.GetPSprite(Lay2).bFlip = CRandom(0, 1);
				}
				
				
				Player.GetPSprite(Lay3).Frame = Player.GetPSprite(Lay2).Frame = Player.GetPSprite(Lay1).Frame;
				Float MuzzleScale = Scal + .01*invoker.Muzzle;
				if(Player.GetPSprite(Lay2).Frame>2)MuzzleScale *= .9;
				A_OverlayScale(Lay2, MuzzleScale, MuzzleScale, WOF_INTERPOLATE);
				A_OverlayScale(Lay3, MuzzleScale, MuzzleScale, WOF_INTERPOLATE);
			}
			PLSR A 1 {
				if(MRIntW_BrownGloves)Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("PL2RA0");
				invoker.BodyScal = 0;
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);
				if(invoker.Muzzle<1)invoker.Muzzle = 1;
				A_OverlayPivot(Lay1, 1, 1);
				A_OverlayScale(Lay1, .85, .85);
				A_OverlayOffset(Lay1, 50, 30);
				A_Overlay(Lay2, "GunMuzzleReload");
				A_OverlayPivot(Lay2, 1, 1);
				A_OverlayOffset(Lay2, 50, 30+invoker.Muzzle*8);
				A_OverlayScale(Lay2, .85, .85);
				Player.GetPSprite(Lay2).bFlip = false;
				A_ClearOverlays(Lay3, Lay3);
			}
			#### A 1{
				A_OverlayRotate(Lay1, 1, WOF_ADD);
				A_OverlayOffset(Lay1, -20, -10, WOF_ADD);
				A_OverlayScale(Lay1, .05, .05, WOF_ADD);
				
				A_OverlayRotate(Lay2, 1, WOF_ADD);
				A_OverlayOffset(Lay2, -20, -10, WOF_ADD);
				A_OverlayScale(Lay2, .05, .05, WOF_ADD);
			}
			#### AA 1{
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayRotate(Lay1, 1, WOF_ADD);
				A_OverlayOffset(Lay1, -20, -10, WOF_ADD);
				A_OverlayScale(Lay1, .05, .05, WOF_ADD);
				
				A_OverlayRotate(Lay2, 1, WOF_ADD);
				A_OverlayOffset(Lay2, -20, -10, WOF_ADD);
				A_OverlayScale(Lay2, .05, .05, WOF_ADD);
				A_OverlayOffset(Lay2, 0, -7, WOF_ADD);
				
				if(invoker.Muzzle>.1)invoker.Muzzle *= .3;
				else invoker.Muzzle = 0;
			}
			#### AA 1{
				A_OverlayRotate(Lay1, 0, WOF_INTERPOLATE);
				A_OverlayRotate(Lay2, 0, WOF_INTERPOLATE);
				if(invoker.Muzzle>.1)invoker.Muzzle *= .3;
				else invoker.Muzzle = 0;
				
				A_IdleAnimation(Lay1);
				
				Player.GetPSprite(Lay2).X = Player.GetPSprite(Lay1).X;
				Player.GetPSprite(Lay2).Y = Player.GetPSprite(Lay1).Y + invoker.Muzzle*8;
			}
			#### AA 1{
				invoker.Muzzle = CFRandom(0, .5);
				A_IdleAnimation(Lay1);
				Player.GetPSprite(Lay2).X = Player.GetPSprite(Lay1).X;
				Player.GetPSprite(Lay2).Y = Player.GetPSprite(Lay1).Y + invoker.Muzzle*8;
			}
			#### AA 1{
				invoker.Muzzle = CFRandom(0, .2);
				A_IdleAnimation(Lay1);
				Player.GetPSprite(Lay2).X = Player.GetPSprite(Lay1).X;
				Player.GetPSprite(Lay2).Y = Player.GetPSprite(Lay1).Y + invoker.Muzzle*8;
			}
			#### AA 1{
				invoker.Muzzle = 0;
				A_IdleAnimation(Lay1);
				Player.GetPSprite(Lay2).X = Player.GetPSprite(Lay1).X;
				Player.GetPSprite(Lay2).Y = Player.GetPSprite(Lay1).Y + invoker.Muzzle*8;
			}
			#### # 1 A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);
			#### # 0 A_ClearOverlays(Lay2, Lay2);
			#### B 1;
			#### BBBB 1{
				invoker.Muzzle = 0;
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, 10, 5, WOF_ADD);
				A_OverlayRotate(Lay1, -1, WOF_ADD);
				A_OverlayScale(Lay1, -.05, -.05, WOF_ADD);
			}
			#### # 0 A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);
			PL1G A 1{
				A_OverlayScale(Lay1, 1, 1);
				A_OverlayOffset(Lay1, CFRandom(-4, 0), CFRandom(0, 2));
				A_OverlayRotate(Lay1, 0);
				A_Overlay(Lay2, "GunMuzzle");
				A_OverlayScale(Lay2, 1, 1);
				A_OverlayOffset(Lay2, Player.GetPSprite(Lay1).X, Player.GetPSprite(Lay1).Y);
				A_OverlayRotate(Lay2, 0);
				A_OverlayPivot(Lay1, .5, 1);
				A_OverlayPivot(Lay2, .5, 1);
			}
			PL1G A 1 {
				A_OverlayOffset(Lay1, CFRandom(-3, 2), CFRandom(0, 3), WOF_INTERPOLATE);
				A_OverlayOffset(Lay2, Player.GetPSprite(Lay1).X, Player.GetPSprite(Lay1).Y, WOF_INTERPOLATE);
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
			}
			#### # 0 {invoker.InterpolateMuzzle = true;}
			#### # 2 {
				A_OverlayOffset(Lay1, 0, 0, WOF_INTERPOLATE);
			}
			Goto GunWait;
			
		Flash:
			TNT1 A 4 A_Light1();
			Goto LightDone;
			TNT1 B 4 A_Light1();
			Goto LightDone;
		
		Sprites:
			PL2R AB 0;
			Stop;
	}
	
	Action Void A_IdleAnimation(int Lay = 2)//Idle hand bobbing
	{
		Bool Bers = MRIntW_BersShake>1 && FindInventory("PowerStrength");
		Float Mult = (Bers?2:1);
		if(invoker.BobState>=0){
			A_OverlayOffset(Lay, CFRandom(-.005, .015)*Mult, CFRandom(.01, .02)*Mult, WOF_ADD);
			if(invoker.BobState>90||Player.GetPSprite(Lay).Y>1.35){invoker.BobState = -invoker.BobState;}
			else invoker.BobState+=Mult;
		}
		else {
			A_OverlayOffset(Lay, -CFRandom(-.005, .015)*Mult, -CFRandom(.01, .02)*Mult, WOF_ADD);
			if(Player.GetPSprite(Lay).Y<0){A_OverlayOffset(Lay, 0, 0, WOF_INTERPOLATE);invoker.BobState = 0;}
			else if(!(invoker.BobState>=-1&&Player.GetPSprite(Lay).Y>0))invoker.BobState+=Mult;
		}
		if(Bers && GetAge()%2==0){//Shaking hand if got berserk
			A_OverlayOffset(Lay, CFRandom(-.25, .25), 0, WOF_INTERPOLATE|WOF_KEEPY);
			Player.GetPSprite(Lay).Y += CFRandom(-.25, .25);
			if(Player.GetPSprite(Lay).Y<0){Player.GetPSprite(Lay).Y = 0;invoker.BobState = 0;}
			if(Player.GetPSprite(Lay).Y>1.35){Player.GetPSprite(Lay).Y = 1.35;invoker.BobState = -90;}
		}
	}
	
	Action Void A_BersShake()
	{
		if(CountInv("PowerStrength"))A_OverlayOffset(Lay1, CFRandom(-.25, .25), CFRandom(-.25, .25), WOF_ADD);
	}
}