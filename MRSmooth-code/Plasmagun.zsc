Class MRIntWeaps_PlasmaRifle: PlasmaRifle Replaces PlasmaRifle
{
	Default{
		Weapon.SlotNumber 6;
	}
	
	Const Lay1 = 2;//Main frame (body)
	Const Lay2 = -2;//Muzzle
	Const Lay3 = -3;//Muzzle flash
	
	int BobState, Idle, animation;
	Double Muzzle,//Muzzle position
			BodyScal,//Rifle body sqiush during firing
			IdleDur;
	Bool MuzzleCycle,//Firing frame
		InterpolateMuzzle,
		Selecting;
	
	Float Recoil;
	Override Void DoEffect()
	{
		if(Recoil>0){Recoil-=.5;Owner.A_SetPitch(Owner.Pitch+.25*MRIntW_RecoilAmount);}
		
		//Disabling weapon interpolation while game is loading
		Let Handler = MRIntWeaps_Handler(EventHandler.Find("MRIntWeaps_Handler"));
		if(Handler && Handler.LoadingDelay>0 && Owner.Player.FindPSprite(Lay1) && Owner.Player.FindPSprite(Lay1).CurState){
			Owner.Player.GetPSprite(Lay1).bInterpolate = false;
			Owner.Player.GetPSprite(Lay1).Y = Max(Owner.Player.GetPSprite(Lay1).Y, 0);
			if(!Owner.Player.FindPSprite(Lay2) || !Owner.Player.FindPSprite(Lay2).CurState)Return;
			Owner.Player.GetPSprite(Lay2).bInterpolate = false;
			Owner.Player.GetPSprite(Lay2).Y = Max(Owner.Player.GetPSprite(Lay2).Y, 0);
		}
	}
	
	action void A_FirePlasma()
	{
		if (!player)Return;
		
		Weapon weap = player.ReadyWeapon;
		if (weap != null && invoker == weap && stateinfo != null && stateinfo.mStateType == STATE_Psprite)
		{
			if (!weap.DepleteAmmo (weap.bAltFire, true, 1))
				return;
			
			State flash = weap.FindState('Flash');
			if (flash != null)
			{
				player.SetSafeFlash(weap, flash, random[FirePlasma](0, 1));
			}
			
		}
		
		Actor a = SpawnPlayerMissile("PlasmaBall");
		if(a)a.A_StopSound();
		A_StartSound("MRIntW/PlasmaFire", CHAN_AUTO, pitch:i_timescale);
	}
	
	States{
		Select:
			TNT1 A 0 {invoker.ResetDead();A_Overlay(Lay1, "GunRaise");}
		Raise:
			#### A 1 A_Raise();
			Loop;
		Deselect:
			TNT1 A 0 {
				if(Health<1){A_Overlay(Lay1, "GunDie");Return;}
				A_Overlay(Lay1, "GunLower");
			}
		Lower:
			#### A 1 A_Lower();
			Loop;
		Ready:
			TNT1 A 1 A_WeaponReady();
			Loop;
			
		GunMuzzle:
			PLSO # 1{
				if(invoker.InterpolateMuzzle){
					Player.GetPSprite(Lay2).OldX = Player.GetPSprite(Lay1).OldX;
					Player.GetPSprite(Lay2).OldY = Player.GetPSprite(Lay1).OldY + invoker.Muzzle*3;
					Player.GetPSprite(Lay2).X = Player.GetPSprite(Lay1).X;
					Player.GetPSprite(Lay2).Y = Player.GetPSprite(Lay1).Y + invoker.Muzzle*3;
					Player.GetPSprite(Lay2).Scale = Player.GetPSprite(Lay1).Scale + (.01,.01)*invoker.Muzzle;
					Player.GetPSprite(Lay2).Scale.X -= invoker.BodyScal;
					Player.GetPSprite(Lay2).Rotation = Player.GetPSprite(Lay1).Rotation;
					Player.GetPSprite(Lay2).Pivot = Player.GetPSprite(Lay1).Pivot;
					A_OverlayFlags(Lay2, PSPF_INTERPOLATE, true);
				}
				else {
					Player.GetPSprite(Lay2).ResetInterpolation();
				}
				Player.GetPSprite(Lay2).Frame = Player.GetPSprite(Lay1).Frame;
				if(Player.FindPSprite(Lay3))Player.FindPSprite(Lay3).Frame = Player.GetPSprite(Lay2).Frame;
			}
			#### # 0 A_JumpIf(Player.GetPSprite(Lay1).Sprite == GetSpriteIndex("PLSRA0"), Null);
			Loop;
		GunMuzzleReload:
			PLSO I 1;
			Loop;
		GunMuzzleFlash:
			PLSF # 1 BRIGHT{
				A_OverlayFlags(Lay3, PSPF_RENDERSTYLE|PSPF_FORCESTYLE, true);
				A_OverlayRenderStyle(Lay3, STYLE_ColorAdd);
				if(!Player.FindPSprite(Lay1) || Player.GetPSprite(Lay1).Sprite != GetSpriteIndex("PL1GA0"))A_ClearOverlays(Lay3, Lay3);
				if(GetCVar("MRIntW_NoMuzzleFlash"))A_OverlayRenderStyle(OverlayId(), STYLE_None);
			}
			Loop;
		
		GunRaise:
			TNT1 AAAA 1 {
				invoker.Idle = Min(0, invoker.Idle);
				A_OverlayFlags(Lay1, PSPF_ADDWEAPON , false);
				A_OverlayPivot(Lay1, .5, 0);
				A_OverlayOffset(Lay1, -20, 145);
				A_OverlayScale(Lay1, 1.1, 1.1);
				A_OverlayRotate(Lay1, 4);
			}
			PLSR C 1{if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("PL2RA0");invoker.Selecting = true;}
			#### CCCCC 1 {
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, 6, -12, WOF_ADD);
				A_OverlayScale(Lay1, -.02, -.02, WOF_ADD);
				A_OverlayRotate(Lay1, -.6, WOF_ADD);
			}
			#### C 1 {
				A_OverlayOffset(Lay1, 6, -14, WOF_ADD);
				A_OverlayScale(Lay1, 0, 0, WOF_ADD);
				A_OverlayRotate(Lay1, -.6, WOF_ADD);
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
			}
			#### CC 1 {
				A_OverlayOffset(Lay1, 6, -12, WOF_ADD);
				A_OverlayScale(Lay1, 0, 0, WOF_ADD);
				A_OverlayRotate(Lay1, -.6, WOF_ADD);
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
			}
			#### C 1 {
				A_OverlayOffset(Lay1, 2, -4, WOF_ADD);
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
			}
			#### BA 1 {
				A_OverlayOffset(Lay1, 2, -4, WOF_ADD);
			}
			#### A 0 {
				invoker.InterpolateMuzzle = false;
				A_Overlay(Lay2, "GunMuzzle");
				A_OverlayScale(Lay2, 1.05, 1.05);
				A_OverlayRotate(Lay2, 0);
				A_OverlayOffset(Lay2, 0, 7);
				A_OverlayOffset(Lay1, 0, 32);
			}
			PL1G A 1 {
				A_OverlayFlags(Lay1, PSPF_ADDWEAPON , true);
				A_OverlayOffset(Lay1, 0, 0);
				A_OverlayScale(Lay1, 1, 1);
				A_OverlayRotate(Lay1, 0);
				invoker.InterpolateMuzzle = true;
				A_OverlayPivot(Lay1, .5, 1);
				A_OverlayPivot(Lay2, .5, 1);
				invoker.Selecting = false;
			}
		GunWait:
			PL1G A 1 {
				A_IdleAnimation(Lay1);
				if(invoker.Muzzle>.1)invoker.Muzzle *= .5;
				else invoker.Muzzle = 0;
			}
			#### # 0 {A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);A_OverlayFlags(Lay2, PSPF_INTERPOLATE, false);}
			Loop;
		GunLower:
			#### # 0 A_ClearOverlays(Lay2, Lay2);
			PLSR A 1 {
				invoker.Idle = Min(0, invoker.Idle);
				A_OverlayFlags(Lay1, PSPF_ADDWEAPON, false);
				if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("PL2RA0");
				A_OverlayPivot(Lay1, .5, 0);
				A_OverlayOffset(Lay1, 50, 0+(Player.GetPSprite(PSP_WEAPON).Y));
			}
			#### BB 1{
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, -7, 0, WOF_ADD);
			}
			#### # 0 A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);
			#### C 1{
				A_OverlayOffset(Lay1, -9, 20, WOF_ADD);
				Player.GetPSprite(Lay1).OldY = Player.GetPSprite(Lay1).Y;
				A_OverlayScale(Lay1, .95, .95);
			}
			#### # 0 A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
			#### ############# 1 {
				A_OverlayOffset(Lay1, -10, 10, WOF_ADD);
				A_OverlayRotate(Lay1, 2, WOF_ADD);
				A_OverlayScale(Lay1, .015, .015, WOF_ADD);
			}
			Stop;
			
		Fire:
			#### # 0 {Player.SetPsprite(Lay1, invoker.FindState("GunFire"));}
			TNT1 A 3 {
				A_FirePlasma();
				if(MRIntW_Recoil){invoker.Recoil+=1;A_SetPitch(Pitch-.5*MRIntW_RecoilAmount);}//Recoil
			}
			TNT1 B 20 A_ReFire();
			Goto Ready;
		GunFire:
			#### # 0 {
				invoker.Idle = Min(0, invoker.Idle);
				A_Overlay(Lay2, "GunMuzzle", true);
				A_Overlay(Lay3, "GunMuzzleFlash", true);
				if(invoker.Selecting){
					A_OverlayFlags(Lay1, PSPF_ADDWEAPON , true);
					A_OverlayOffset(Lay1, 0, 0);
					A_OverlayOffset(Lay2, 0, 0);
					A_OverlayScale(Lay1, 1, 1);
					A_OverlayRotate(Lay1, 0);
					A_OverlayPivot(Lay1, .5, 1);
					A_OverlayPivot(Lay2, .5, 1);
					A_OverlayRotate(Lay2, 0);
					A_OverlayFlags(Lay2, PSPF_INTERPOLATE, false);
				}
				invoker.Muzzle = -1.5;
				invoker.InterpolateMuzzle = false;
			}
			PL1G ### 1{
				Vector2 Rand = (CFRandom(-1, 1), CFRandom(3, 4));
				A_OverlayOffset(Lay1, Rand.X, Rand.Y, WOF_INTERPOLATE);
				if(!invoker.Selecting)A_OverlayOffset(Lay2, Rand.X, Rand.Y + invoker.Muzzle*3, WOF_INTERPOLATE);
				A_OverlayOffset(Lay3, Rand.X+CFRandom(-.5, .5), Rand.Y + invoker.Muzzle*3+CFRandom(-.5, .5), WOF_INTERPOLATE);
				
				Float Scal = CFRandom(1, 1.1);
				invoker.BodyScal = -CFRandom(-.05, .05);
				A_OverlayScale(Lay1, Scal+invoker.BodyScal, Scal, WOF_INTERPOLATE);
				

				invoker.Muzzle += .8;
				if(Player.GetPSprite(Lay1).Frame<1)Player.GetPSprite(Lay1).Frame = 1;
				else if(Player.GetPSprite(Lay1).Frame<7)Player.GetPSprite(Lay1).Frame++;
				else Player.GetPSprite(Lay1).Frame = 1;
				if(Player.GetPSprite(Lay1).Frame==1){
					Player.GetPSprite(Lay3).bFlip = Player.GetPSprite(Lay2).bFlip = CRandom(0, 1);
				}
				
				
				Player.GetPSprite(Lay3).Frame = Player.GetPSprite(Lay2).Frame = Player.GetPSprite(Lay1).Frame;
				Float MuzzleScale = Scal + .01*invoker.Muzzle;
				if(Player.GetPSprite(Lay2).Frame>2)MuzzleScale *= .9;
				if(!invoker.Selecting)A_OverlayScale(Lay2, MuzzleScale, MuzzleScale, WOF_INTERPOLATE);
				A_OverlayScale(Lay3, MuzzleScale+CFRandom(-.01, .01), MuzzleScale+CFRandom(-.01, .01), WOF_INTERPOLATE);
				
				if(!invoker.Selecting)A_OverlayFlags(Lay2, PSPF_INTERPOLATE, true);
				invoker.Selecting = false;
			}
			#### # 0 {
				A_ClearOverlays(Lay2, Lay2);
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);
				A_OverlayPivot(Lay3, .5, 1);
				A_OverlayScale(Lay3, .3, .3, WOF_INTERPOLATE);
			}
			PLSR A 1 {
				if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("PL2RA0");
				A_OverlayPivot(Lay1, .5, 0);
				A_OverlayOffset(Lay1, 30, 20);
			}
			#### # 0 A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
			#### B 1{
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, -7, 0, WOF_ADD);
			}
			PLSR D 1 {
				if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("PL2RA0");
				invoker.BodyScal = 0;
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);
				if(invoker.Muzzle<1)invoker.Muzzle = 1;
				A_OverlayPivot(Lay1, 1, 1);
				A_OverlayScale(Lay1, .9, .9);
				A_OverlayOffset(Lay1, 30, 20);
				A_OverlayRotate(Lay1, 1);
				A_Overlay(Lay2, "GunMuzzleReload");
				A_OverlayPivot(Lay2, 1, 1);
				A_OverlayOffset(Lay2, 30, 20+invoker.Muzzle*8);
				A_OverlayScale(Lay2, .9, .9);
				A_OverlayRotate(Lay2, 1);
				Player.GetPSprite(Lay2).bFlip = false;
				A_ClearOverlays(Lay3, Lay3);
			}
			#### # 1{
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				//A_OverlayRotate(Lay1, 1, WOF_ADD);
				A_OverlayOffset(Lay1, -20, -10, WOF_ADD);
				A_OverlayScale(Lay1, .05, .05, WOF_ADD);
				
				//A_OverlayRotate(Lay2, 1, WOF_ADD);
				A_OverlayOffset(Lay2, -20, -10, WOF_ADD);
				A_OverlayScale(Lay2, .05, .05, WOF_ADD);
				A_OverlayOffset(Lay2, 0, -7, WOF_ADD);
				
				if(invoker.Muzzle>.1)invoker.Muzzle *= .3;
				else invoker.Muzzle = 0;
			}
			#### ## 1{
				A_OverlayRotate(Lay1, 0, WOF_INTERPOLATE);
				A_OverlayRotate(Lay2, 0, WOF_INTERPOLATE);
				if(invoker.Muzzle>.1)invoker.Muzzle *= .3;
				else invoker.Muzzle = 0;
				
				A_IdleAnimation(Lay1);
				
				Player.GetPSprite(Lay2).X = Player.GetPSprite(Lay1).X;
				Player.GetPSprite(Lay2).Y = Player.GetPSprite(Lay1).Y + invoker.Muzzle*8;
			}
			#### ## 1{
				invoker.Muzzle = CFRandom(0, .5);
				A_IdleAnimation(Lay1);
				Player.GetPSprite(Lay2).X = Player.GetPSprite(Lay1).X;
				Player.GetPSprite(Lay2).Y = Player.GetPSprite(Lay1).Y + invoker.Muzzle*8;
			}
			#### ## 1{
				invoker.Muzzle = CFRandom(0, .2);
				A_IdleAnimation(Lay1);
				Player.GetPSprite(Lay2).X = Player.GetPSprite(Lay1).X;
				Player.GetPSprite(Lay2).Y = Player.GetPSprite(Lay1).Y + invoker.Muzzle*8;
			}
			#### ## 1{
				invoker.Muzzle = 0;
				A_IdleAnimation(Lay1);
				Player.GetPSprite(Lay2).X = Player.GetPSprite(Lay1).X;
				Player.GetPSprite(Lay2).Y = Player.GetPSprite(Lay1).Y + invoker.Muzzle*8;
			}
			#### # 1 A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);
			#### # 0 A_ClearOverlays(Lay2, Lay2);
			#### C 1;
			#### CCBA 1{
				invoker.Muzzle = 0;
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, 10, 0, WOF_ADD);
			}
			#### # 0 A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);
			PL1G A 1{
				A_OverlayScale(Lay1, 1, 1);
				A_OverlayOffset(Lay1, CFRandom(-4, 0), CFRandom(0, 2));
				A_OverlayRotate(Lay1, 0);
				A_Overlay(Lay2, "GunMuzzle");
				A_OverlayScale(Lay2, 1, 1);
				A_OverlayOffset(Lay2, Player.GetPSprite(Lay1).X, Player.GetPSprite(Lay1).Y);
				A_OverlayRotate(Lay2, 0);
				A_OverlayPivot(Lay1, .5, 1);
				A_OverlayPivot(Lay2, .5, 1);
			}
			PL1G A 1 {
				A_OverlayOffset(Lay1, CFRandom(-3, 2), CFRandom(0, 3), WOF_INTERPOLATE);
				A_OverlayOffset(Lay2, Player.GetPSprite(Lay1).X, Player.GetPSprite(Lay1).Y, WOF_INTERPOLATE);
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
			}
			#### # 0 {invoker.InterpolateMuzzle = true;}
			#### # 2 {
				A_OverlayOffset(Lay1, 0, 0, WOF_INTERPOLATE);
			}
			Goto GunWait;
			
		Flash:
			TNT1 A 4 A_Light1();
			Goto LightDone;
			TNT1 B 4 A_Light1();
			Goto LightDone;
		
		Sprites:
			PL2R A 0;
			MRD2 A 0;
			Stop;
	}
	
	Action Void A_IdleAnimation(int Lay = 2)//Idle hand bobbing
	{
		Bool Bers = GetCVar("MRIntW_BersShake")>1 && FindInventory("PowerStrength");
		Float Mult = (Bers?2:1);
		if(invoker.BobState>=0){
			A_OverlayOffset(Lay, CFRandom(-.005, .015)*Mult, CFRandom(.01, .02)*Mult, WOF_ADD);
			if(invoker.BobState>90||Player.GetPSprite(Lay).Y>1.35){invoker.BobState = -invoker.BobState;}
			else invoker.BobState+=Mult;
		}
		else {
			A_OverlayOffset(Lay, -CFRandom(-.005, .015)*Mult, -CFRandom(.01, .02)*Mult, WOF_ADD);
			if(Player.GetPSprite(Lay).Y<0){A_OverlayOffset(Lay, 0, 0, WOF_INTERPOLATE);invoker.BobState = 0;}
			else if(!(invoker.BobState>=-1&&Player.GetPSprite(Lay).Y>0))invoker.BobState+=Mult;
		}
		if(Bers && GetAge()%2==0){//Shaking hand if got berserk
			A_OverlayOffset(Lay, CFRandom(-.25, .25), 0, WOF_INTERPOLATE|WOF_KEEPY);
			Player.GetPSprite(Lay).Y += CFRandom(-.25, .25);
			if(Player.GetPSprite(Lay).Y<0){Player.GetPSprite(Lay).Y = 0;invoker.BobState = 0;}
			if(Player.GetPSprite(Lay).Y>1.35){Player.GetPSprite(Lay).Y = 1.35;invoker.BobState = -90;}
		}
		
		if(!GetCVar("MRIntW_IdleAnims"))Return;
		
		invoker.Idle++;
		if(invoker.Idle<35)Return;
		Float Muzle;
		Switch(invoker.Animation)
		{
			Default://Tinglelinguling
				if(invoker.Idle>35 && invoker.Idle<39)invoker.Muzzle+=CFRandom(.3, .6);
				if(invoker.Idle>45 && invoker.Idle<48)invoker.Muzzle+=CFRandom(.4, .7);
				if(invoker.Idle>48)StopIdleAnimation();
				Break;
			Case 1://Pull
				Muzle = invoker.Idle-35;
				
				invoker.Muzzle = Min(CFRandom(1.7, 1.9), Muzle/(invoker.IdleDur*35.)*CFRandom(1.7, 1.8));
				
				if(invoker.Muzzle>=1.7 && CRandom(0, 100)==1){
					StopIdleAnimation();
					invoker.Muzzle = -.2;
				}
				Break;
			Case 2://Pull-push-pull-push
				Float Round = 100*invoker.IdleDur;
				Muzle = (invoker.Idle-(35-Round*.5))%Round;
				
				invoker.Muzzle = abs(Muzle-Round*.5)/(Round*.15);
				
				if(invoker.Idle-35>Round*3){
					if(CRandom(0, Round)==1)StopIdleAnimation();
				}
				
				Return;
		}
	}
	
	Action Void StopIdleAnimation()
	{
		invoker.Idle = 0-CRandom(2, 6)*35*5;
		invoker.Animation = CRandom(0, 2);
		invoker.IdleDur = CFRandom(.7, 1.1);
	}
	
	Action Void A_BersShake()
	{
		if(CountInv("PowerStrength"))A_OverlayOffset(Lay1, CFRandom(-.25, .25), CFRandom(-.25, .25), WOF_ADD);
	}
}