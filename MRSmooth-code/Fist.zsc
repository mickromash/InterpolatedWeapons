Class MRIntWeaps_Fist: Fist Replaces Fist
{
	Default{
		Weapon.SlotNumber 1;
		Weapon.BobStyle "Smooth";
		+Weapon.NOALERT
	}
	Const RightLay = 2;//Right hand
	Const LeftLay = 3;//Left hand
	
	Override Void DoEffect()
	{
		//Disabling weapon interpolation while game is loading
		Let Handler = MRIntWeaps_Handler(EventHandler.Find("MRIntWeaps_Handler"));
		if(!Handler)Return;
		
		if(Handler.LoadingDelay>0){
			if(Owner.Player.FindPSprite(LeftLay) && Owner.Player.FindPSprite(LeftLay).CurState){
				Owner.Player.GetPSprite(LeftLay).bInterpolate = false;
				Owner.Player.GetPSprite(LeftLay).Y = Max(Owner.Player.GetPSprite(LeftLay).Y, 0);
				Owner.Player.GetPSprite(RightLay).bInterpolate = false;
				Owner.Player.GetPSprite(RightLay).Y = Max(Owner.Player.GetPSprite(RightLay).Y, 0);
			}
			Handler.LoadingDelay--;
		}
	}
	
	int BobState, IdleDelay;
	Vector2 RightHand, LeftHand;
	Bool HitWall;
	States{
		RightHandRaise://Select animation
			TNT1 AAAAA 1 {
				if(MRIntW_RightPunch)A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, true);
				A_OverlayFlags(RightLay, PSPF_ADDWEAPON , false);
				A_OverlayPivot(RightLay, 0, 0);
				A_OverlayOffset(RightLay, 40, 75);
				A_OverlayScale(RightLay, 1.7, 1.7);
			}
			PUN1 A 1{if(MRIntW_BrownGloves)Player.GetPSprite(RightLay).Sprite = GetSpriteIndex("PUN2A0");}
			#### AAAAAAAA 1 {
				A_OverlayFlags(RightLay, PSPF_INTERPOLATE, true);
				A_OverlayOffset(RightLay, -4.5, -4.5, WOF_ADD);
				A_OverlayScale(RightLay, -.08, -.08, WOF_ADD);
			}
			#### A 1 {
				A_OverlayFlags(RightLay, PSPF_ADDWEAPON , true);
				A_OverlayOffset(RightLay, 3, CFRandom(-3, -1));
				A_OverlayScale(RightLay, 1, 1);
			}
		RightHandWait://Ready animation start
			#### # 0 {
				if(MRIntW_RightPunch)A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, true);
				A_OverlayFlags(RightLay, PSPF_INTERPOLATE, true);
				A_OverlayOffset(RightLay, 0, 0, WOF_INTERPOLATE);
			}
			PUN1 A 2 {if(MRIntW_BrownGloves)Player.GetPSprite(RightLay).Sprite = GetSpriteIndex("PUN2A0");}
		RightHandWaitLoop://Ready animation
			#### A 1 A_IdleAnimation(RightLay);
			#### # 0 {
				A_OverlayFlags(RightLay, PSPF_INTERPOLATE, true);
				if(MRIntW_SecondFist||(CountInv("PowerStrength") && MRIntW_BersShake))A_Overlay(LeftLay, "LeftHandUp", true);
			}
			#### # 0 A_JumpIf(invoker.IdleDelay>35 && abs(invoker.BobState)<2, "GunIdle1");
			Loop;
		RightHandLower://Deselect animation
			PUN1 A 1{
				if(MRIntW_RightPunch)A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, true);
				if(MRIntW_BrownGloves)Player.GetPSprite(RightLay).Sprite = GetSpriteIndex("PUN2A0");
				A_OverlayFlags(RightLay, PSPF_ADDWEAPON, false);
				A_OverlayPivot(RightLay, 0, 0);
				A_OverlayOffset(RightLay, 0, Player.GetPSprite(PSP_WEAPON).Y);
				A_OverlayScale(RightLay, 1, 1);
			}
			#### AA 1{
				A_OverlayScale(RightLay, -.04, -.04, WOF_ADD);
				A_OverlayOffset(RightLay, -1, 0, WOF_ADD);
				A_OverlayRotate(RightLay, 2, WOF_ADD);
			}
			#### AAAAAAAAAA 1 {
				A_OverlayOffset(RightLay, 6, 3, WOF_ADD);
				A_OverlayScale(RightLay, .05, .05, WOF_ADD);
				A_OverlayRotate(RightLay, -3, WOF_ADD);
			}
			Stop;
		RightHandLowerFast://Before punching
			PUN1 A 1 {
				if(MRIntW_RightPunch)A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, true);
				A_OverlayPivot(RightLay, 0, 0);
				A_OverlayScale(RightLay, 1, 1);
				A_OverlayOffset(RightLay, invoker.RightHand.X, invoker.RightHand.Y);
				if(MRIntW_BrownGloves)Player.GetPSprite(RightLay).Sprite = GetSpriteIndex("PUN2A0");
			}
			#### AAA 1 {
				A_OverlayFlags(RightLay, PSPF_INTERPOLATE, true);
				A_OverlayOffset(RightLay, 15, 15, WOF_ADD);
				A_OverlayScale(RightLay, .05, .05, WOF_ADD);
				invoker.RightHand = (Player.GetPSprite(RightLay).X, Player.GetPSprite(RightLay).Y);
			}
			Stop;
		RightHandUp://After punching
			PUN1 A 1 {
				if(MRIntW_RightPunch)A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, true);
				A_OverlayPivot(RightLay, 0, 0);
				A_OverlayOffset(RightLay, 30, 30);
				A_OverlayScale(RightLay, 1.15, 1.15);
				invoker.RightHand = (Player.GetPSprite(RightLay).X, Player.GetPSprite(RightLay).Y);
				if(MRIntW_BrownGloves)Player.GetPSprite(RightLay).Sprite = GetSpriteIndex("PUN2A0");
			}
			#### AAA 1{
				A_OverlayOffset(RightLay, -CFRandom(9.5, 10.5), -CFRandom(9.5, 10.5), WOF_ADD);
				A_OverlayScale(RightLay, -.05, -.05, WOF_ADD);
				invoker.RightHand = (Player.GetPSprite(RightLay).X, Player.GetPSprite(RightLay).Y);
			}
			#### A 1 {A_OverlayOffset(RightLay, CFRandom(-4, 1), CFRandom(-3, -1), WOF_INTERPOLATE);A_OverlayScale(RightLay, 1, 1, WOF_INTERPOLATE);}
			Goto RightHandWait;
		
		LeftHandRaise://Plays along with select animation (with berserk)
			PUN1 BBBBBBBB 1 {
				if(MRIntW_RightPunch)A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, true);
				if(MRIntW_BrownGloves)Player.GetPSprite(LeftLay).Sprite = GetSpriteIndex("PUN2A0");
				A_OverlayOffset(LeftLay, -70, 70);
				A_OverlayPivot(LeftLay, 1, 1);
				A_OverlayScale(LeftLay, 1.3, 1.3);
			}
		LeftHandUp://After picking un berserk, or selecting fist (with berserk)
			PUN1 B 1 {
				if(MRIntW_RightPunch)A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, true);
				if(MRIntW_BrownGloves)Player.GetPSprite(LeftLay).Sprite = GetSpriteIndex("PUN2A0");
				A_OverlayOffset(LeftLay, -70, 70);
				A_OverlayPivot(LeftLay, 1, 1);
				A_OverlayScale(LeftLay, 1.3, 1.3);
			}
			#### BBBBBBB 1 {
				A_OverlayOffset(LeftLay, 10, -10, WOF_ADD);
				A_OverlayScale(LeftLay, -.05, -.05, WOF_ADD);
			}
			#### B 2 {
				A_OverlayOffset(LeftLay, 2, 5, WOF_INTERPOLATE);
				A_OverlayScale(LeftLay, 1, 1, WOF_INTERPOLATE);
			}
			#### C 1;
			#### C 1 A_Overlayoffset(LeftLay, 0, 0, WOF_INTERPOLATE);
		LeftHandWait://Plays along with ready animation (with berserk)
			#### # 0 {if(health<1)A_ClearOverlays(LeftLay, LeftLay);}
			#### C 1 A_IdleAnimation(LeftLay);
			Loop;
		LeftHandLower://Plays along with delesect animation (with berserk)
			PUN1 B 1{
				if(MRIntW_RightPunch)A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, true);
				if(MRIntW_BrownGloves)Player.GetPSprite(LeftLay).Sprite = GetSpriteIndex("PUN2A0");
				A_OverlayPivot(LeftLay, 1, 0);
			}
			#### BBBBBBBBB 1 {
				A_Overlayoffset(LeftLay, -9, 3, WOF_ADD);
				A_OverlayRotate(LeftLay, .1, WOF_ADD);
				A_OverlayScale(LeftLay, .05, .05, WOF_ADD);
			}
			Stop;
		LeftHandPunch://Punching
			#### # 0 A_JumpIf(MRIntW_SecondFist||(CountInv("PowerStrength")&& MRIntW_BersShake), "LeftHandPunchBerserk");
			PUN1 D 2 {
				invoker.HitWall = false;
				if(MRIntW_RightPunch)A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, true);
				A_StartSound("MRIntW/FistSwing", CHAN_AUTO, volume:CountInv("PowerStrength")?.4:.3, pitch:CFRandom(.9, 1.1));
				A_OverlayOffset(LeftLay, -20, 40);
				A_OverlayScale(LeftLay, .96, 1.1);
				A_OverlayPivot(LeftLay, 1, 0);
				if(MRIntW_BrownGloves)Player.GetPSprite(LeftLay).Sprite = GetSpriteIndex("PUN2A0");
			}
			#### D 1 {A_OverlayOffset(LeftLay, 20, -30, WOF_ADD);A_OverlayScale(Leftlay, .04, 0, WOF_ADD);}
			#### D 1 {A_OverlayOffset(LeftLay, CFRandom(77, 82), -CFRandom(73, 76), WOF_ADD);A_OverlayScale(LeftLay, .01, -.025, WOF_ADD);}//Punch
			#### DD 1 {
				A_OverlayOffset(LeftLay, CFRandom(-1, 1), CFRandom(-1, 1), WOF_ADD);
				if(invoker.HitWall){
					A_OverlayOffset(LeftLay, CFRandom(-8, 0), CFRandom(-3, 3), WOF_ADD);
					Float Scal = CFRandom(0, .025);
					A_OverlayScale(LeftLay, Scal, Scal, WOF_ADD);
					A_OverlayScale(LeftLay, CFRandom(-.01, .01), CFRandom(-.02, .02), WOF_ADD);
					A_OverlayRotate(LeftLay, CFRandom(1, 3), WOF_ADD);
				}
				A_OverlayScale(LeftLay, 0, -.025, WOF_ADD);
			}
			#### D 1 {
				A_OverlayOffset(LeftLay, CFRandom(-1, 1), CFRandom(-1, 1), WOF_ADD);
				if(invoker.HitWall){
					A_OverlayOffset(LeftLay, CFRandom(-9, 0), CFRandom(-4, 4), WOF_ADD);
					Float Scal = CFRandom(.05, .1);
					A_OverlayScale(LeftLay, Scal, Scal, WOF_ADD);
					A_OverlayScale(LeftLay, CFRandom(-.02, .02), CFRandom(-.02, .02), WOF_ADD);
					A_OverlayRotate(LeftLay, CFRandom(2, 4), WOF_ADD);
				}
				A_OverlayFlags(LeftLay, PSPF_INTERPOLATE, true);
			}
			#### DDDDD 1 {
				A_OverlayOffset(LeftLay, -24, 15, WOF_ADD);
				A_OverlayRotate(LeftLay, 5, WOF_ADD);
				A_OverlayScale(LeftLay, .05, .05, WOF_ADD);
				if(invoker.HitWall){
					A_OverlayOffset(LeftLay, CFRandom(-3, 0), CFRandom(1, 3), WOF_ADD);
					Float Scal = CFRandom(.01, .03);
					A_OverlayScale(LeftLay, Scal, Scal, WOF_ADD);
					A_OverlayScale(LeftLay, CFRandom(-.02, .02), CFRandom(-.02, .02), WOF_ADD);
					A_OverlayRotate(LeftLay, CFRandom(0, 4), WOF_ADD);
				}
			}
			Stop;
		LeftHandPunchThru://If killing an enemy
			#### D 1 {
				A_OverlayOffset(LeftLay, 10, -10, WOF_ADD);
				A_OverlayScale(LeftLay, -.05, -.05, WOF_ADD);
				invoker.LeftHand = (-30, 43);
			}
			#### DDDD 1 {
				A_OverlayOffset(LeftLay, 24, 24, WOF_ADD);
				A_OverlayRotate(LeftLay, -5, WOF_ADD);
				A_OverlayScale(LeftLay, -.05, -.05, WOF_ADD);
				invoker.LeftHand = (-30, 43);
			}
			#### # 0 A_JumpIf(MRIntW_SecondFist||(CountInv("PowerStrength") && MRIntW_BersShake), "LeftHandPunchBerserkThru");
			Stop;
			
		LeftHandPunchBerserk://Punching (with berserk)
			PUN1 C 1 {
				invoker.HitWall = false;
				if(MRIntW_RightPunch)A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, true);
				A_StartSound("MRIntW/FistSwing", CHAN_AUTO, volume:CountInv("PowerStrength")?.4:.3, pitch:CFRandom(.9, 1.1));
				A_OverlayOffset(LeftLay, invoker.LeftHand.X-3, invoker.LeftHand.Y+3);
				A_OverlayRotate(LeftLay, 1, WOF_INTERPOLATE);
				A_OverlayPivot(LeftLay, 1, 0);
				A_OverlayScale(LeftLay, 1.05, 1.05);
				if(MRIntW_BrownGloves)Player.GetPSprite(LeftLay).Sprite = GetSpriteIndex("PUN2A0");
			}
			#### C 1 {
				A_OverlayOffset(LeftLay, -40, 40, WOF_INTERPOLATE);
				A_OverlayScale(LeftLay, 1.2, 1.2, WOF_INTERPOLATE);
				A_OverlayRotate(LeftLay, 2, WOF_ADD);
			}
			#### D 1 {
				A_OverlayOffset(LeftLay, 40, -30, WOF_ADD);
				A_OverlayRotate(LeftLay, 0, WOF_INTERPOLATE);
				A_OverlayScale(LeftLay, 1, 1.1, WOF_INTERPOLATE);
			}
			#### D 1 {//Punch
				A_OverlayOffset(LeftLay, CFRandom(77, 82), -CFRandom(73, 76), WOF_ADD);
				A_OverlayScale(LeftLay, 0, -.025, WOF_ADD);
				if(invoker.HitWall){
					A_OverlayOffset(LeftLay, CFRandom(-8, 3), CFRandom(-8, 8), WOF_ADD);
					Float Scal = CFRandom(0, .025);
					A_OverlayScale(LeftLay, Scal, Scal, WOF_ADD);
					A_OverlayRotate(LeftLay, CFRandom(-1, 2), WOF_ADD);
				}
			}
			#### DD 1 {
				A_OverlayOffset(LeftLay, CFRandom(-1, 1), CFRandom(-1, 1), WOF_ADD);
				A_OverlayScale(LeftLay, 0, -.025, WOF_ADD);
				
				if(invoker.HitWall){
					A_OverlayOffset(LeftLay, CFRandom(-8, 0), CFRandom(-8, 8), WOF_ADD);
					Float Scal = CFRandom(0, .025);
					A_OverlayScale(LeftLay, Scal, Scal, WOF_ADD);
					A_OverlayRotate(LeftLay, CFRandom(1, 4), WOF_ADD);
				}
			}
			#### D 1 {
				A_OverlayOffset(LeftLay, CFRandom(-1, 1), CFRandom(-1, 1), WOF_ADD);
				if(invoker.HitWall){
					A_OverlayOffset(LeftLay, CFRandom(-8, -2), CFRandom(-2, 4), WOF_ADD);
					Float Scal = CFRandom(.05, .1);
					A_OverlayScale(LeftLay, Scal, Scal, WOF_ADD);
					A_OverlayRotate(LeftLay, CFRandom(4, 8), WOF_ADD);
				}
				A_OverlayFlags(LeftLay, PSPF_INTERPOLATE, true);
			}
			#### DDDD 1 {
				A_OverlayOffset(LeftLay, -26, 16, WOF_ADD);
				A_OverlayRotate(LeftLay, 4, WOF_ADD);
				A_OverlayScale(LeftLay, .02, .02, WOF_ADD);
				if(invoker.HitWall){
					A_OverlayOffset(LeftLay, CFRandom(-6, -1), CFRandom(-3, 1), WOF_ADD);
					Float Scal = CFRandom(0, .025);
					A_OverlayScale(LeftLay, Scal, Scal, WOF_ADD);
					A_OverlayRotate(LeftLay, CFRandom(2, 6), WOF_ADD);
				}
				invoker.LeftHand = (Player.GetPSprite(LeftLay).X, Player.GetPSprite(LeftLay).Y);
			}
			#### D 1 {
				A_OverlayOffset(LeftLay, -80, 40, WOF_INTERPOLATE);
				A_OverlayRotate(LeftLay, 4, WOF_ADD);
				invoker.LeftHand = (Player.GetPSprite(LeftLay).X, Player.GetPSprite(LeftLay).Y);
			}
			#### D 2 A_OverlayFlags(LeftLay, PSPF_INTERPOLATE, false);
			#### C 1 {
				A_OverlayOffset(LeftLay, -30, 43);
				A_OverlayRotate(LeftLay, 0);
				A_OverlayScale(LeftLay, 1.1, 1.1);
				invoker.LeftHand = (Player.GetPSprite(LeftLay).X, Player.GetPSprite(LeftLay).Y);
			}
			#### CC 1 {
				A_OverlayFlags(LeftLay, PSPF_INTERPOLATE, true);
				A_OverlayScale(LeftLay, -.05, -.05, WOF_ADD);
				A_OverlayOffset(LeftLay, 15, -20, WOF_ADD);
			}
			#### # 0 {
				A_Overlayoffset(LeftLay, 0, 0, WOF_INTERPOLATE);
				invoker.LeftHand = (Player.GetPSprite(LeftLay).X, Player.GetPSprite(LeftLay).Y);
			}
			Goto LeftHandWait;
		LeftHandPunchBerserkThru://If killing an enemy (with berserk)
			#### # 0 A_OverlayFlags(LeftLay, PSPF_INTERPOLATE, false);
			#### CC 2 {
				A_OverlayOffset(LeftLay, -30, 43);
				A_OverlayRotate(LeftLay, 0);
				A_OverlayScale(LeftLay, 1.1, 1.1);
				invoker.LeftHand = (Player.GetPSprite(LeftLay).X, Player.GetPSprite(LeftLay).Y);
			}
			#### CC 1 {
				A_OverlayFlags(LeftLay, PSPF_INTERPOLATE, true);
				A_OverlayScale(LeftLay, -.05, -.05, WOF_ADD);
				A_OverlayOffset(LeftLay, 15, -20, WOF_ADD);
			}
			#### # 0 {
				A_Overlayoffset(LeftLay, 0, 0, WOF_INTERPOLATE);
				invoker.LeftHand = (Player.GetPSprite(LeftLay).X, Player.GetPSprite(LeftLay).Y);
			}
			Goto LeftHandWait;
			
		GunIdle1://Idle animation 1
			#### # 0 {
				A_OverlayPivot(RightLay, .5, 1);
				invoker.BobState = 0;
				invoker.IdleDelay = CRandom(-10, -3)*70;
				if((CountInv("PowerStrength") && MRIntW_BersShake)||(Health<20 && MRIntW_LowHealthShake))invoker.IdleDelay = CRandom(1, -3)*35;
			}
			#### # 0 A_JumpIf(CRandom(0, 255)<100, "GunIdle2");
			#### #### 1 {A_Overlayoffset(RightLay, -.15, .05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### ## 1 {A_OverlayOffset(RightLay, -.1, .05, WOF_ADD);A_OverlayRotate(RightLay, -.1, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### ### 1 {A_OverlayOffset(RightLay, .15, -.1, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### ## 1 {A_OverlayOffset(RightLay, .15, -.025, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### # 1 {A_OverlayOffset(RightLay, .1, .0, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### ## 1 {A_OverlayOffset(RightLay, .1, .025, WOF_ADD);A_OverlayRotate(RightLay, .05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### ### 1 {A_OverlayOffset(RightLay, .025, .05, WOF_ADD);A_OverlayRotate(RightLay, .05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### ## 1 {A_OverlayOffset(RightLay, -.05, -.1, WOF_ADD);A_OverlayRotate(RightLay, -.05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### # 1 {A_OverlayOffset(RightLay, .0, .05, WOF_ADD);A_OverlayRotate(RightLay, .05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### # 2 {
				A_OverlayRotate(RightLay, 0, WOF_INTERPOLATE);
				A_Overlayoffset(RightLay, 0, 0, WOF_INTERPOLATE);
				A_SetTics(CRandom(1,2));
			}
			#### # 0 A_JumpIf(CRandom(0, 10)<4, "GunIdle2");
			#### # 0 A_JumpIf(CRandom(0, 40)==1, "GunIdle1");
			Goto RightHandWait;
		GunIdle2://Idle animation 2
			#### A 1 {A_Overlayoffset(RightLay, .0, .05, WOF_ADD);A_OverlayRotate(RightLay, -.05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AA 1 {A_Overlayoffset(RightLay, .05, -.1, WOF_ADD);A_OverlayRotate(RightLay, .05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AAA 1 {A_Overlayoffset(RightLay, -.025, .05, WOF_ADD);A_OverlayRotate(RightLay, -.05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AA 1 {A_Overlayoffset(RightLay, -.1, .025, WOF_ADD);A_OverlayRotate(RightLay, -.05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### A 1 {A_Overlayoffset(RightLay, -.1, .0, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AA 1 {A_Overlayoffset(RightLay, -.15, -.025, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AAA 1 {A_Overlayoffset(RightLay, -.15, -.1, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AA 1 {A_Overlayoffset(RightLay, .1, .05, WOF_ADD);A_OverlayRotate(RightLay, .1, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AAAA 1 {A_Overlayoffset(RightLay, .15, .05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			Goto RightHandWait;
		//Default gun states
		Select:
			TNT1 A 0 {
				A_Overlay(RightLay, "RightHandRaise");
				if(MRIntW_SecondFist||(CountInv("PowerStrength") && MRIntW_BersShake))A_Overlay(LeftLay, "LeftHandRaise");
			}
		Raise:
			#### A 1 A_Raise();
			Loop;
		Deselect:
			TNT1 A 0 {
				A_Overlay(RightLay, "RightHandLower");
				if(MRIntW_SecondFist||(CountInv("PowerStrength") && MRIntW_BersShake))A_Overlay(LeftLay, "LeftHandLower");
			}
		Lower:
			#### A 1 A_Lower();
			Loop;
			
		Ready:
			TNT1 A 1 A_WeaponReady();
			Loop;
		Fire://4, 4 Punch, 5, 4, 5 Refire
			TNT1 A 0 {
				A_Overlay(RightLay, "RightHandLowerFast");
				A_Overlay(LeftLay, "LeftHandPunch");
				if(!MRIntW_BetterAlert)A_AlertMonsters();
			}
			TNT1 A 4;
			TNT1 A 4 {
				FLineTraceData Punch;
				
				Double PZ = player.mo.height * 0.5 - player.mo.floorclip + player.mo.AttackZOffset*player.crouchFactor;
				LineTrace(Angle, 64, Pitch, 0, PZ, data:Punch);
				
				A_Punch();
				if(Punch.HitActor && MRIntW_FinishPunch && (FindInventory("PowerStrength")||MRIntW_FinishPunch>1)){
					if(Punch.HitActor.Health<1 && CFRandom(.1, 1)<=MRIntW_FinishPunchChance){
						Player.SetPsprite(LeftLay, invoker.FindState("LeftHandPunchThru"));
					}
					Return;
				}
				if(Punch.HitType!=TRACE_HitNone){
					invoker.HitWall = true;
					if(MRIntW_SurfacePunchRecoil && FindInventory("PowerStrength"))
					{
						Double Strength = CFRandom(.5, 1);
						
						Vel.XY += AngleToVector(Angle+180, Strength)*Cos(abs(Pitch));
						Vel.Z += Strength*2*Sin(Pitch);
					}
				}
			}
			TNT1 A 5;
			TNT1 A 5 {A_Refire();A_Overlay(RightLay, "RightHandUp", true);}
			Goto Ready;
		//Caching sprites
		Sprites:
			PUN2 AD 0;
			Stop;
	}
	
	//Select fists on berserk pickup
	Override Bool HandlePickup(Inventory item)
	{
		if(item is 'PowerStrength')Owner.A_SelectWeapon("MRIntWeaps_Fist");
		
		Return Super.HandlePickup(item);
	}
	
	Action Void A_IdleAnimation(int Lay = 2)//Idle hand bobbing
	{
		Bool Bers = (MRIntW_BersShake && FindInventory("PowerStrength"))||(Health<20 && MRIntW_LowHealthShake);
		Float Mult = (Bers?2:1);
		
		if(Lay!=LeftLay){
			if(invoker.BobState>=0){
				A_OverlayOffset(Lay, CFRandom(-.005, .015)*Mult, CFRandom(.01, .02)*Mult, WOF_ADD);
				if(invoker.BobState>90||Player.GetPSprite(Lay).Y>1.35){invoker.BobState = -invoker.BobState;}
				else invoker.BobState+=Mult;
			}
			else {
				A_OverlayOffset(Lay, -CFRandom(-.005, .015)*Mult, -CFRandom(.01, .02)*Mult, WOF_ADD);
				if(Player.GetPSprite(Lay).Y<0){A_OverlayOffset(Lay, 0, 0, WOF_INTERPOLATE);invoker.BobState = 0;}
				else if(!(invoker.BobState>=-1&&Player.GetPSprite(Lay).Y>0))invoker.BobState+=Mult;
			}
			if(Bers && GetAge()%2==0){//Shaking hand if got berserk
				A_OverlayPivot(Lay, .2, .7);
				A_OverlayOffset(Lay, CFRandom(-.5, .5), 0, WOF_INTERPOLATE|WOF_KEEPY);
				Player.GetPSprite(Lay).Y += CFRandom(-.5, .5);
				if(Player.GetPSprite(Lay).Y<0){Player.GetPSprite(Lay).Y = 0;invoker.BobState = 0;}
				if(Player.GetPSprite(Lay).Y>1.35){Player.GetPSprite(Lay).Y = 1.35;invoker.BobState = -90;}
				Player.GetPSprite(Lay).Rotation = CFRandom(-.1, .1);
				Player.GetPSprite(Lay).Scale = (CFRandom(.99, 1.01), CFRandom(.99, 1.01));
			}
			if(MRIntW_IdleAnims)invoker.IdleDelay++;
		}
		
		else {
			if(invoker.BobState>=0){
				A_OverlayOffset(Lay, CFRandom(-.005, .015)*Mult, CFRandom(.01, .02)*Mult, WOF_ADD);
			}
			else {
				A_OverlayOffset(Lay, -CFRandom(-.005, .015)*Mult, -CFRandom(.01, .02)*Mult, WOF_ADD);
				if(Player.GetPSprite(Lay).Y<0)A_OverlayOffset(Lay, 0, 0, WOF_INTERPOLATE);
			}
			if(Bers && GetAge()%2==0){//Shaking hand if got berserk
				A_OverlayPivot(Lay, .8, .5);
				A_OverlayOffset(Lay, CFRandom(-.5, .5), 0, WOF_INTERPOLATE|WOF_KEEPY);
				Player.GetPSprite(Lay).Y += CFRandom(-.5, .5);
				if(Player.GetPSprite(Lay).Y<0)Player.GetPSprite(Lay).Y = 0;
				if(Player.GetPSprite(Lay).Y>1.35)Player.GetPSprite(Lay).Y = 1.35;
				Player.GetPSprite(Lay).Rotation = CFRandom(-.1, .1);
				//Player.GetPSprite(Lay).Scale = (CFRandom(.99, 1.01), CFRandom(.99, 1.01));
			}
		}
	}
	
	Action Void A_BersShake()
	{
		if((CountInv("PowerStrength") && MRIntW_BersShake)||(Health<20 && MRIntW_LowHealthShake))A_OverlayOffset(RightLay, CFRandom(-.5, .5), CFRandom(-.5, .5), WOF_ADD);
	}
}