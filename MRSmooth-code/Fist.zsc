Class MRIntWeaps_Fist: Fist Replaces Fist
{
	Default{
		Weapon.SlotNumber 1;
		+Weapon.NOALERT
		MissileHeight .25;
	}
	
	Const RightLay = 2;//Right hand
	Const LeftLay = 3;//Left hand
	
	Double CurBounce, BounceStrength;
	
	Override Void DoEffect()
	{
		//Disabling weapon interpolation while game is loading
		Let Handler = MRIntWeaps_Handler(EventHandler.Find("MRIntWeaps_Handler"));
		if(!Handler)Return;

		if(Handler.LoadingDelay>0){
			if(Owner.Player.FindPSprite(LeftLay) && Owner.Player.FindPSprite(LeftLay).CurState){
				Owner.Player.GetPSprite(LeftLay).bInterpolate = false;
				Owner.Player.GetPSprite(LeftLay).Y = Max(Owner.Player.GetPSprite(LeftLay).Y, 0);
				Owner.Player.GetPSprite(RightLay).bInterpolate = false;
				Owner.Player.GetPSprite(RightLay).Y = Max(Owner.Player.GetPSprite(RightLay).Y, 0);
			}
			Handler.LoadingDelay--;
		}
		
		Let Weap = Owner.Player.ReadyWeapon;
		if(!Weap||!Owner.Player.FindPSprite(PSP_WEAPON))Return;
		
		Weap.BobStyle = MRIntW_BobStyle;
		
		if(!MRIntW_Bounce)Return;
		
		Let PSP = Owner.Player.GetPSprite(PSP_WEAPON);
		
		if(Owner.Pos.Z > Owner.FloorZ){
			BounceStrength = Owner.Vel.Z;
			CurBounce += (BounceStrength - CurBounce)*.5;
			CurBounce = Clamp(CurBounce, -abs(Weap.MissileHeight), 20);
		}
		else if(abs(BounceStrength)>0){
			if(CurBounce<-BounceStrength)CurBounce -= BounceStrength*.5;
			
			else {
				BounceStrength *= .7;
				if(abs(BounceStrength)<.05)BounceStrength = 0;
				CurBounce = -BounceStrength;
			}
		}
		
		if(!InStateSequence(PSP.curState, Weap.GetDownState()) && !InStateSequence(PSP.CurState, Weap.FindState("Select"))){
			Double Bounce = CurBounce*.5;
			if(Bounce<0)Bounce /= Max(1, Weap.MissileHeight/32);
			PSP.Y = 32 + CurBounce*.5;
		}
	}
	
	Action Void A_SelectPunchingFist()
	{
		Switch(GetCVar("MRIntW_PunchingFist"))
		{
			Case 0:
				A_Overlay(LeftLay, "LeftHandPunch");
				invoker.SecondFist = true;
				Return;
			Case 1:
				if(CFRandom(0, 10)>4){invoker.SecondFist = true;A_Overlay(LeftLay, "LeftHandPunch");}
				else {invoker.SecondFist = false;A_Overlay(RightLay, "RightHandPunch");}
				Return;
			Case 2:
				invoker.SecondFist = !invoker.SecondFist;
				if(invoker.SecondFist)A_Overlay(LeftLay, "LeftHandPunch");
				else A_Overlay(RightLay, "RightHandPunch");
				Return;
			Case 3:
				if(GetCVar("MRIntW_RightPunch")?Player.Cmd.Yaw<0:Player.Cmd.Yaw>0){invoker.SecondFist = false;A_Overlay(RightLay, "RightHandPunch");Return;}
				else if(GetCVar("MRIntW_RightPunch")?Player.Cmd.Yaw>0:Player.Cmd.Yaw<0){invoker.SecondFist = true;A_Overlay(LeftLay, "LeftHandPunch");Return;}
				
				if(GetCVar("MRIntW_RightPunch")?Player.Cmd.SideMove<0:Player.Cmd.SideMove>0){invoker.SecondFist = true;A_Overlay(LeftLay, "LeftHandPunch");Return;}
				else if(GetCVar("MRIntW_RightPunch")?Player.Cmd.SideMove>0:Player.Cmd.SideMove<0){invoker.SecondFist = false;A_Overlay(RightLay, "RightHandPunch");Return;}
				
				invoker.SecondFist = !invoker.SecondFist;
				if(invoker.SecondFist)A_Overlay(LeftLay, "LeftHandPunch");
				else A_Overlay(RightLay, "RightHandPunch");
				Return;
		}
	}
	
	Action Void A_SetHandsSprite()
	{
		if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(OverlayId()).Sprite = GetSpriteIndex("PUN2A0");
		if(GetCVar("MRIntW_BareHands"))Player.GetPSprite(OverlayId()).Sprite = GetSpriteIndex("PUN3A0");
	}
	
	int BobState, IdleDelay;
	Vector2 RightHand, LeftHand;
	Bool HitWall, SecondFist;
	States{
		RightHandRaise://Select animation
			TNT1 AAAAA 1 {
				if(GetCVar("MRIntW_RightPunch"))A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, true);
				A_OverlayFlags(RightLay, PSPF_ADDWEAPON , false);
				A_OverlayPivot(RightLay, 0, 0);
				A_OverlayOffset(RightLay, 40, 75);
				A_OverlayScale(RightLay, 1.7, 1.7);
			}
			PUN1 A 1 A_SetHandsSprite();
			#### AAAAAAAA 1 {
				A_OverlayFlags(RightLay, PSPF_INTERPOLATE, true);
				A_OverlayOffset(RightLay, -4.5, -4.5, WOF_ADD);
				A_OverlayScale(RightLay, -.08, -.08, WOF_ADD);
			}
			#### A 1 {
				A_OverlayFlags(RightLay, PSPF_ADDWEAPON , true);
				A_OverlayOffset(RightLay, 3, CFRandom(-3, -1));
				A_OverlayScale(RightLay, 1, 1);
			}
		RightHandWait://Ready animation start
			#### # 0 {
				if(GetCVar("MRIntW_RightPunch"))A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, true);
				A_OverlayFlags(RightLay, PSPF_INTERPOLATE, false);
				A_OverlayOffset(RightLay, 0, 0, WOF_INTERPOLATE);
			}
			PUN1 A 2 A_SetHandsSprite();
		RightHandWaitLoop://Ready animation
			#### A 1 A_IdleAnimation(RightLay);
			#### # 0 {
				A_OverlayFlags(RightLay, PSPF_INTERPOLATE, true);
				if(GetCVar("MRIntW_SecondFist")||(CountInv("PowerStrength") && GetCVar("MRIntW_BersShake")))A_Overlay(LeftLay, "LeftHandUp", true);
			}
			#### # 0 A_JumpIf(invoker.IdleDelay>35 && abs(invoker.BobState)<2, "GunIdle1");
			Loop;
		RightHandLower://Deselect animation
			PUN1 A 1{
				if(GetCVar("MRIntW_RightPunch"))A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, true);
				A_SetHandsSprite();
				A_OverlayFlags(RightLay, PSPF_ADDWEAPON, false);
				A_OverlayPivot(RightLay, 0, 0);
				A_OverlayOffset(RightLay, 0, Player.GetPSprite(PSP_WEAPON).Y);
				A_OverlayScale(RightLay, 1, 1);
			}
			#### AA 1{
				A_OverlayScale(RightLay, -.04, -.04, WOF_ADD);
				A_OverlayOffset(RightLay, -1, 0, WOF_ADD);
				A_OverlayRotate(RightLay, 2, WOF_ADD);
			}
			#### AAAAAAAAAA 1 {
				A_OverlayOffset(RightLay, 6, 3, WOF_ADD);
				A_OverlayScale(RightLay, .05, .05, WOF_ADD);
				A_OverlayRotate(RightLay, -3, WOF_ADD);
			}
			Stop;
		RightHandLowerFast://Before punching
			PUN1 A 1 {
				if(GetCVar("MRIntW_RightPunch"))A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, true);
				A_OverlayPivot(RightLay, 0, 0);
				A_OverlayScale(RightLay, 1, 1);
				A_OverlayOffset(RightLay, invoker.RightHand.X, invoker.RightHand.Y);
				A_SetHandsSprite();
			}
			#### AAA 1 {
				A_OverlayFlags(RightLay, PSPF_INTERPOLATE, true);
				A_OverlayOffset(RightLay, 15, 15, WOF_ADD);
				A_OverlayScale(RightLay, .05, .05, WOF_ADD);
				invoker.RightHand = (Player.GetPSprite(RightLay).X, Player.GetPSprite(RightLay).Y);
			}
			Stop;
		RightHandUp://After punching
			PUN1 A 1 {
				if(GetCVar("MRIntW_RightPunch"))A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, true);
				A_OverlayPivot(RightLay, 0, 0);
				A_OverlayOffset(RightLay, 38, 38);
				A_OverlayScale(RightLay, 1.15, 1.15);
				invoker.RightHand = (Player.GetPSprite(RightLay).X, Player.GetPSprite(RightLay).Y);
				A_SetHandsSprite();
			}
			#### AAA 1{
				A_OverlayOffset(RightLay, -CFRandom(9.5, 10.5), -CFRandom(9.5, 10.5), WOF_ADD);
				A_OverlayScale(RightLay, -.05, -.05, WOF_ADD);
				invoker.RightHand = (Player.GetPSprite(RightLay).X, Player.GetPSprite(RightLay).Y);
			}
			#### A 1 {A_OverlayOffset(RightLay, CFRandom(-4, 1), CFRandom(-3, -1), WOF_INTERPOLATE);A_OverlayScale(RightLay, 1, 1, WOF_INTERPOLATE);}
			Goto RightHandWait;
		
		LeftHandRaise://Plays along with select animation (with berserk)
			PUN1 BBBBBBBB 1 {
				if(GetCVar("MRIntW_RightPunch"))A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, true);
				A_SetHandsSprite();
				A_OverlayOffset(LeftLay, -70, 70);
				A_OverlayPivot(LeftLay, 1, 1);
				A_OverlayScale(LeftLay, 1.3, 1.3);
			}
		LeftHandUp://After picking un berserk, or selecting fist (with berserk)
			PUN1 B 1 {
				if(GetCVar("MRIntW_RightPunch"))A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, true);
				A_SetHandsSprite();
				A_OverlayOffset(LeftLay, -70, 70);
				A_OverlayPivot(LeftLay, 1, 1);
				A_OverlayScale(LeftLay, 1.3, 1.3);
			}
			#### BBBBBBB 1 {
				A_OverlayOffset(LeftLay, 10, -10, WOF_ADD);
				A_OverlayScale(LeftLay, -.05, -.05, WOF_ADD);
			}
			#### B 2 {
				A_OverlayOffset(LeftLay, 2, 5, WOF_INTERPOLATE);
				A_OverlayScale(LeftLay, 1, 1, WOF_INTERPOLATE);
			}
			#### C 1;
			#### C 1 A_Overlayoffset(LeftLay, 0, 0, WOF_INTERPOLATE);
		LeftHandWait://Plays along with ready animation (with berserk)
			#### # 0 {if(health<1)A_ClearOverlays(LeftLay, LeftLay);}
			#### C 1 A_IdleAnimation(LeftLay);
			Loop;
		LeftHandLower://Plays along with delesect animation (with berserk)
			PUN1 B 1{
				if(GetCVar("MRIntW_RightPunch"))A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, true);
				A_SetHandsSprite();
				A_OverlayPivot(LeftLay, 1, 0);
			}
			#### BBBBBBBBB 1 {
				A_Overlayoffset(LeftLay, -9, 3, WOF_ADD);
				A_OverlayRotate(LeftLay, .1, WOF_ADD);
				A_OverlayScale(LeftLay, .05, .05, WOF_ADD);
			}
			Stop;
		RightHandPunch:
			PUN1 A 1 {
				A_OverlayOffset(OverlayId(), invoker.RightHand.X+5, invoker.RightHand.Y+5, WOF_INTERPOLATE);
				A_SetHandsSprite();
			}
		LeftHandPunch://Punching
			#### # 0 A_JumpIf(GetCVar("MRIntW_SecondFist")||(CountInv("PowerStrength")&& GetCVar("MRIntW_BersShake")), "LeftHandPunchBerserk");
			PUN1 D 2 {
				invoker.HitWall = false;
				if(GetCVar("MRIntW_RightPunch"))A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, true);
				if(GetCVar("MRIntW_PunchingFist")>0){
					if(invoker.SecondFist)A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, GetCVar("MRIntW_RightPunch")?true:false);
					else {A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, GetCVar("MRIntW_RightPunch")?false:true);Player.GetPSprite(OverlayId()).Frame = 4;A_SetTics(1);}
				}
				A_StartSound("MRIntW/FistSwing", CHAN_AUTO, volume:CountInv("PowerStrength")?.4:.3, pitch:CFRandom(.9, 1.1)*i_timescale);
				A_OverlayOffset(OverlayId(), -20, 40);
				A_OverlayScale(OverlayId(), .96, 1.1);
				A_OverlayPivot(OverlayId(), 1, 0);
				A_SetHandsSprite();
			}
			#### # 1 {A_OverlayOffset(OverlayId(), 20, -30, WOF_ADD);A_OverlayScale(OverlayId(), .04, 0, WOF_ADD);}
			#### # 1 {A_OverlayOffset(OverlayId(), CFRandom(77, 82), -CFRandom(73, 76), WOF_ADD);A_OverlayScale(OverlayId(), .01, -.025, WOF_ADD);}//Punch
			#### ## 1 {
				A_OverlayOffset(OverlayId(), CFRandom(-1, 1), CFRandom(-1, 1), WOF_ADD);
				if(invoker.HitWall){
					A_OverlayOffset(OverlayId(), CFRandom(-8, 0), CFRandom(-3, 3), WOF_ADD);
					Float Scal = CFRandom(0, .025);
					A_OverlayScale(OverlayId(), Scal, Scal, WOF_ADD);
					A_OverlayScale(OverlayId(), CFRandom(-.01, .01), CFRandom(-.02, .02), WOF_ADD);
					A_OverlayRotate(OverlayId(), CFRandom(1, 3), WOF_ADD);
				}
				A_OverlayScale(OverlayId(), 0, -.025, WOF_ADD);
			}
			#### # 1 {
				A_OverlayOffset(OverlayId(), CFRandom(-1, 1), CFRandom(-1, 1), WOF_ADD);
				if(invoker.HitWall){
					A_OverlayOffset(OverlayId(), CFRandom(-9, 0), CFRandom(-4, 4), WOF_ADD);
					Float Scal = CFRandom(.05, .1);
					A_OverlayScale(OverlayId(), Scal, Scal, WOF_ADD);
					A_OverlayScale(OverlayId(), CFRandom(-.02, .02), CFRandom(-.02, .02), WOF_ADD);
					A_OverlayRotate(OverlayId(), CFRandom(2, 4), WOF_ADD);
				}
				A_OverlayFlags(OverlayId(), PSPF_INTERPOLATE, true);
			}
			#### ##### 1 {
				A_OverlayOffset(OverlayId(), -24, 15, WOF_ADD);
				A_OverlayRotate(OverlayId(), 5, WOF_ADD);
				A_OverlayScale(OverlayId(), .05, .05, WOF_ADD);
				if(invoker.HitWall){
					A_OverlayOffset(OverlayId(), CFRandom(-3, 0), CFRandom(1, 3), WOF_ADD);
					Float Scal = CFRandom(.01, .03);
					A_OverlayScale(OverlayId(), Scal, Scal, WOF_ADD);
					A_OverlayScale(OverlayId(), CFRandom(-.02, .02), CFRandom(-.02, .02), WOF_ADD);
					A_OverlayRotate(OverlayId(), CFRandom(0, 4), WOF_ADD);
				}
			}
			Stop;
		LeftHandPunchThru://If killing an enemy
			#### # 1 {
				A_OverlayOffset(OverlayId(), 10, -10, WOF_ADD);
				A_OverlayScale(OverlayId(), -.05, -.05, WOF_ADD);
				invoker.LeftHand = (-30, 43);
			}
			#### #### 1 {
				A_OverlayOffset(OverlayId(), 24, 24, WOF_ADD);
				A_OverlayRotate(OverlayId(), -5, WOF_ADD);
				A_OverlayScale(OverlayId(), -.05, -.05, WOF_ADD);
				invoker.LeftHand = (-30, 43);
			}
			#### # 0 A_JumpIf((GetCVar("MRIntW_SecondFist")||(CountInv("PowerStrength") && GetCVar("MRIntW_BersShake"))) && !(GetCVar("MRIntW_PunchingFist")>0 && !invoker.SecondFist), "LeftHandPunchBerserkThru");
			Stop;
			
		LeftHandPunchBerserk://Punching (with berserk)
			PUN1 C 1 {
				invoker.HitWall = false;
				if(GetCVar("MRIntW_RightPunch"))A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, true);
				
				if(GetCVar("MRIntW_PunchingFist")>0 && !invoker.SecondFist){
					Player.GetPSprite(OverlayId()).Frame = 0;
					A_OverlayOffset(OverlayId(), 40, 40, WOF_INTERPOLATE);
				}
				
				A_StartSound("MRIntW/FistSwing", CHAN_AUTO, volume:CountInv("PowerStrength")?.4:.3, pitch:CFRandom(.9, 1.1)*i_timescale);
				if(GetCVar("MRIntW_PunchingFist")<1||invoker.SecondFist)A_OverlayOffset(OverlayId(), invoker.LeftHand.X-3, invoker.LeftHand.Y+3);
				A_OverlayRotate(OverlayId(), 1, WOF_INTERPOLATE);
				A_OverlayPivot(OverlayId(), 1, 0);
				A_OverlayScale(OverlayId(), 1.05, 1.05);
				A_SetHandsSprite();
			}
			#### # 1 {
				A_OverlayOffset(OverlayId(), -40, 40, WOF_INTERPOLATE);
				A_OverlayScale(OverlayId(), 1.2, 1.2, WOF_INTERPOLATE);
				A_OverlayRotate(OverlayId(), 2, WOF_ADD);
				
				if(GetCVar("MRIntW_PunchingFist")>0 && !invoker.SecondFist)A_SetTics(0);
			}
			#### D 1 {
				if(GetCVar("MRIntW_PunchingFist")>0){
					if(invoker.SecondFist)A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, GetCVar("MRIntW_RightPunch")?true:false);
					else {A_OverlayFlags(OverlayId(), PSPF_MIRROR|PSPF_FLIP, GetCVar("MRIntW_RightPunch")?false:true);Player.GetPSprite(OverlayId()).Frame = -1;}
				}
				A_OverlayOffset(OverlayId(), 40, -30, WOF_ADD);
				A_OverlayRotate(OverlayId(), 0, WOF_INTERPOLATE);
				A_OverlayScale(OverlayId(), 1, 1.1, WOF_INTERPOLATE);
			}
			#### # 1 {//Punch
				if(GetCVar("MRIntW_PunchingFist")>0 && !invoker.SecondFist)Player.GetPSprite(OverlayId()).Frame = 4;
				
				A_OverlayOffset(OverlayId(), CFRandom(77, 82), -CFRandom(73, 76), WOF_ADD);
				A_OverlayScale(OverlayId(), 0, -.025, WOF_ADD);
				if(invoker.HitWall){
					A_OverlayOffset(OverlayId(), CFRandom(-8, 3), CFRandom(-8, 8), WOF_ADD);
					Float Scal = CFRandom(0, .025);
					A_OverlayScale(OverlayId(), Scal, Scal, WOF_ADD);
					A_OverlayRotate(OverlayId(), CFRandom(-1, 2), WOF_ADD);
				}
			}
			#### ## 1 {
				A_OverlayOffset(OverlayId(), CFRandom(-1, 1), CFRandom(-1, 1), WOF_ADD);
				A_OverlayScale(OverlayId(), 0, -.025, WOF_ADD);
				
				if(invoker.HitWall){
					A_OverlayOffset(OverlayId(), CFRandom(-8, 0), CFRandom(-8, 8), WOF_ADD);
					Float Scal = CFRandom(0, .025);
					A_OverlayScale(OverlayId(), Scal, Scal, WOF_ADD);
					A_OverlayRotate(OverlayId(), CFRandom(1, 4), WOF_ADD);
				}
			}
			#### # 1 {
				A_OverlayOffset(OverlayId(), CFRandom(-1, 1), CFRandom(-1, 1), WOF_ADD);
				if(invoker.HitWall){
					A_OverlayOffset(OverlayId(), CFRandom(-8, -2), CFRandom(-2, 4), WOF_ADD);
					Float Scal = CFRandom(.05, .1);
					A_OverlayScale(OverlayId(), Scal, Scal, WOF_ADD);
					A_OverlayRotate(OverlayId(), CFRandom(4, 8), WOF_ADD);
				}
				A_OverlayFlags(OverlayId(), PSPF_INTERPOLATE, true);
			}
			#### #### 1 {
				A_OverlayOffset(OverlayId(), -26, 16, WOF_ADD);
				A_OverlayRotate(OverlayId(), 4, WOF_ADD);
				A_OverlayScale(OverlayId(), .02, .02, WOF_ADD);
				if(invoker.HitWall){
					A_OverlayOffset(OverlayId(), CFRandom(-6, -1), CFRandom(-3, 1), WOF_ADD);
					Float Scal = CFRandom(0, .025);
					A_OverlayScale(OverlayId(), Scal, Scal, WOF_ADD);
					A_OverlayRotate(OverlayId(), CFRandom(2, 6), WOF_ADD);
				}
				if(GetCVar("MRIntW_PunchingFist")<1||invoker.SecondFist)invoker.LeftHand = (Player.GetPSprite(OverlayId()).X, Player.GetPSprite(OverlayId()).Y);
			}
			#### # 1 {
				A_OverlayOffset(OverlayId(), -80, 40, WOF_INTERPOLATE);
				A_OverlayRotate(OverlayId(), 4, WOF_ADD);
				if(GetCVar("MRIntW_PunchingFist")<1||invoker.SecondFist)invoker.LeftHand = (Player.GetPSprite(OverlayId()).X, Player.GetPSprite(OverlayId()).Y);
			}
			#### # 0 A_JumpIf(GetCVar("MRIntW_PunchingFist")>0 && !invoker.SecondFist, "Null");
			#### # 2 A_OverlayFlags(OverlayId(), PSPF_INTERPOLATE, false);
			#### C 1 {
				A_OverlayOffset(OverlayId(), -30, 43);
				A_OverlayRotate(OverlayId(), 0);
				A_OverlayScale(OverlayId(), 1.1, 1.1);
				invoker.LeftHand = (Player.GetPSprite(OverlayId()).X, Player.GetPSprite(OverlayId()).Y);
			}
			#### CC 1 {
				A_OverlayFlags(OverlayId(), PSPF_INTERPOLATE, true);
				A_OverlayScale(OverlayId(), -.05, -.05, WOF_ADD);
				A_OverlayOffset(OverlayId(), 15, -20, WOF_ADD);
			}
			#### # 0 {
				A_Overlayoffset(OverlayId(), 0, 0, WOF_INTERPOLATE);
				invoker.LeftHand = (Player.GetPSprite(OverlayId()).X, Player.GetPSprite(OverlayId()).Y);
			}
			Goto LeftHandWait;
		LeftHandPunchBerserkThru://If killing an enemy (with berserk)
			#### # 0 A_OverlayFlags(OverlayId(), PSPF_INTERPOLATE, false);
			#### CC 2 {
				A_OverlayOffset(OverlayId(), -30, 43);
				A_OverlayRotate(OverlayId(), 0);
				A_OverlayScale(OverlayId(), 1.1, 1.1);
				invoker.LeftHand = (Player.GetPSprite(OverlayId()).X, Player.GetPSprite(OverlayId()).Y);
			}
			#### CC 1 {
				A_OverlayFlags(OverlayId(), PSPF_INTERPOLATE, true);
				A_OverlayScale(OverlayId(), -.05, -.05, WOF_ADD);
				A_OverlayOffset(OverlayId(), 15, -20, WOF_ADD);
			}
			#### # 0 {
				A_Overlayoffset(OverlayId(), 0, 0, WOF_INTERPOLATE);
				invoker.LeftHand = (Player.GetPSprite(OverlayId()).X, Player.GetPSprite(OverlayId()).Y);
			}
			Goto LeftHandWait;
			
		GunIdle1://Idle animation 1
			#### # 0 {
				A_OverlayPivot(RightLay, .5, 1);
				invoker.BobState = 0;
				invoker.IdleDelay = CRandom(-10, -3)*70;
				if((CountInv("PowerStrength") && GetCVar("MRIntW_BersShake"))||(Health<20 && GetCVar("MRIntW_LowHealthShake")))invoker.IdleDelay = CRandom(1, -3)*35;
			}
			#### # 0 A_JumpIf(CRandom(0, 255)<100, "GunIdle2");
			#### #### 1 {A_Overlayoffset(RightLay, -.15, .05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### ## 1 {A_OverlayOffset(RightLay, -.1, .05, WOF_ADD);A_OverlayRotate(RightLay, -.1, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### ### 1 {A_OverlayOffset(RightLay, .15, -.1, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### ## 1 {A_OverlayOffset(RightLay, .15, -.025, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### # 1 {A_OverlayOffset(RightLay, .1, .0, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### ## 1 {A_OverlayOffset(RightLay, .1, .025, WOF_ADD);A_OverlayRotate(RightLay, .05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### ### 1 {A_OverlayOffset(RightLay, .025, .05, WOF_ADD);A_OverlayRotate(RightLay, .05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### ## 1 {A_OverlayOffset(RightLay, -.05, -.1, WOF_ADD);A_OverlayRotate(RightLay, -.05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### # 1 {A_OverlayOffset(RightLay, .0, .05, WOF_ADD);A_OverlayRotate(RightLay, .05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### # 2 {
				A_OverlayRotate(RightLay, 0, WOF_INTERPOLATE);
				A_Overlayoffset(RightLay, 0, 0, WOF_INTERPOLATE);
				A_SetTics(CRandom(1,2));
			}
			#### # 0 A_JumpIf(CRandom(0, 10)<4, "GunIdle2");
			#### # 0 A_JumpIf(CRandom(0, 40)==1, "GunIdle1");
			Goto RightHandWait;
		GunIdle2://Idle animation 2
			#### A 1 {A_Overlayoffset(RightLay, .0, .05, WOF_ADD);A_OverlayRotate(RightLay, -.05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AA 1 {A_Overlayoffset(RightLay, .05, -.1, WOF_ADD);A_OverlayRotate(RightLay, .05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AAA 1 {A_Overlayoffset(RightLay, -.025, .05, WOF_ADD);A_OverlayRotate(RightLay, -.05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AA 1 {A_Overlayoffset(RightLay, -.1, .025, WOF_ADD);A_OverlayRotate(RightLay, -.05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### A 1 {A_Overlayoffset(RightLay, -.1, .0, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AA 1 {A_Overlayoffset(RightLay, -.15, -.025, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AAA 1 {A_Overlayoffset(RightLay, -.15, -.1, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AA 1 {A_Overlayoffset(RightLay, .1, .05, WOF_ADD);A_OverlayRotate(RightLay, .1, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AAAA 1 {A_Overlayoffset(RightLay, .15, .05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			Goto RightHandWait;
		
		//Default gun states
		Select:
			TNT1 A 0 {
				A_Overlay(RightLay, "RightHandRaise");
				if(GetCVar("MRIntW_SecondFist")||(CountInv("PowerStrength") && GetCVar("MRIntW_BersShake")))A_Overlay(LeftLay, "LeftHandRaise");
			}
		Raise:
			#### A 1 A_Raise();
			Loop;
		Deselect:
			TNT1 A 0 {
				if(Health<1){
					A_Overlay(RightLay, "RightHandDie");
					if(GetCVar("MRIntW_SecondFist")||(CountInv("PowerStrength") && GetCVar("MRIntW_BersShake")))A_Overlay(LeftLay, "LeftHandDie");
					Return;
				}
				A_Overlay(RightLay, "RightHandLower");
				if(GetCVar("MRIntW_SecondFist")||(CountInv("PowerStrength") && GetCVar("MRIntW_BersShake")))A_Overlay(LeftLay, "LeftHandLower");
			}
		Lower:
			#### A 1 A_Lower();
			Loop;
			
		Ready:
			TNT1 A 1 A_WeaponReady();
			Loop;
		Fire://4, 4 Punch, 5, 4, 5 Refire
			TNT1 A 0 {
				A_Overlay(RightLay, "RightHandLowerFast");
				A_SelectPunchingFist();
				if(!MRIntW_BetterAlert)A_AlertMonsters();
			}
			TNT1 A 4;
			TNT1 A 4 {
				FLineTraceData Punch;
				
				Double PZ = player.mo.height * 0.5 - player.mo.floorclip + player.mo.AttackZOffset*player.crouchFactor;
				LineTrace(Angle, 64, Pitch, 0, PZ, data:Punch);
				
				A_Punch();
				if(Punch.HitActor && GetCVar("MRIntW_FinishPunch") && (FindInventory("PowerStrength")||GetCVar("MRIntW_FinishPunch")>1)){
					if(Punch.HitActor.Health<1 && CFRandom(.1, 1)<=GetCVar("MRIntW_FinishPunchChance")){
						Player.SetPsprite((GetCVar("MRIntW_PunchingFist")>0 && !invoker.SecondFist)?RightLay:LeftLay, invoker.FindState("LeftHandPunchThru"));
					}
					Return;
				}
				if(Punch.HitType!=TRACE_HitNone){
					invoker.HitWall = true;
					String Snd = "MRIntW/FistPunchWallKnuckles";
					if(GetCVar("MRIntW_PunchingFist")>0 && !invoker.SecondFist) Snd = "MRIntW/FistPunchWall";
					A_StartSound(Snd, CHAN_AUTO, pitch: CFRandom(.95, 1.05)*i_timescale);
					if(GetCVar("MRIntW_SurfacePunchRecoil") && FindInventory("PowerStrength"))
					{
						Double Strength = CFRandom(.5, 1);
						
						Vel.XY += AngleToVector(Angle+180, Strength)*Cos(abs(Pitch));
						Vel.Z += Strength*2*Sin(Pitch);
					}
				}
			}
			TNT1 A 5;
			TNT1 A 5 {A_Refire();if(!(Player.OldButtons&BT_ATTACK||Player.Cmd.Buttons&BT_ATTACK))A_Overlay(RightLay, "RightHandUp");}
			TNT1 A 0 {if(!(Player.OldButtons&BT_ATTACK||Player.Cmd.Buttons&BT_ATTACK))A_Overlay(RightLay, "RightHandUp", true);}
			Goto Ready;
		//Caching sprites
		Sprites:
			PUN2 A 0;
			PUN3 A 0;
			MRD2 A 0;
			Stop;
	}
	
	Action Void A_IdleAnimation(int Lay = 2)//Idle hand bobbing
	{
		Bool Bers = (GetCVar("MRIntW_BersShake") && FindInventory("PowerStrength"))||(Health<20 && GetCVar("MRIntW_LowHealthShake"));
		Float Mult = (Bers?2:1);
		A_OverlayFlags(Lay, PSPF_INTERPOLATE, false);
		if(Lay!=LeftLay){
			if(invoker.BobState>=0){
				A_OverlayOffset(Lay, CFRandom(-.005, .015)*Mult, CFRandom(.01, .02)*Mult, WOF_ADD);
				if(invoker.BobState>90||Player.GetPSprite(Lay).Y>1.35){invoker.BobState = -invoker.BobState;}
				else invoker.BobState+=Mult;
			}
			else {
				A_OverlayOffset(Lay, -CFRandom(-.005, .015)*Mult, -CFRandom(.01, .02)*Mult, WOF_ADD);
				if(Player.GetPSprite(Lay).Y<0){A_OverlayOffset(Lay, 0, 0, WOF_INTERPOLATE);invoker.BobState = 0;}
				else if(!(invoker.BobState>=-1&&Player.GetPSprite(Lay).Y>0))invoker.BobState+=Mult;
			}
			if(Bers && GetAge()%2==0){//Shaking hand if got berserk
				A_OverlayPivot(Lay, .2, .7);
				A_OverlayOffset(Lay, CFRandom(-.5, .5), 0, WOF_INTERPOLATE|WOF_KEEPY);
				Player.GetPSprite(Lay).Y += CFRandom(-.5, .5);
				if(Player.GetPSprite(Lay).Y<0){Player.GetPSprite(Lay).Y = 0;invoker.BobState = 0;}
				if(Player.GetPSprite(Lay).Y>1.35){Player.GetPSprite(Lay).Y = 1.35;invoker.BobState = -90;}
				Player.GetPSprite(Lay).Rotation = CFRandom(-.1, .1);
				Player.GetPSprite(Lay).Scale = (CFRandom(.99, 1.01), CFRandom(.99, 1.01));
			}
			if(GetCVar("MRIntW_IdleAnims"))invoker.IdleDelay++;
		}
		
		else {
			if(invoker.BobState>=0){
				A_OverlayOffset(Lay, CFRandom(-.005, .015)*Mult, CFRandom(.01, .02)*Mult, WOF_ADD);
			}
			else {
				A_OverlayOffset(Lay, -CFRandom(-.005, .015)*Mult, -CFRandom(.01, .02)*Mult, WOF_ADD);
				if(Player.GetPSprite(Lay).Y<0)A_OverlayOffset(Lay, 0, 0, WOF_INTERPOLATE);
			}
			if(Bers && GetAge()%2==0){//Shaking hand if got berserk
				A_OverlayPivot(Lay, .8, .5);
				A_OverlayOffset(Lay, CFRandom(-.5, .5), 0, WOF_INTERPOLATE|WOF_KEEPY);
				Player.GetPSprite(Lay).Y += CFRandom(-.5, .5);
				if(Player.GetPSprite(Lay).Y<0)Player.GetPSprite(Lay).Y = 0;
				if(Player.GetPSprite(Lay).Y>1.35)Player.GetPSprite(Lay).Y = 1.35;
				Player.GetPSprite(Lay).Rotation = CFRandom(-.1, .1);
			}
		}
	}
	
	Action Void A_BersShake()
	{
		if((CountInv("PowerStrength") && GetCVar("MRIntW_BersShake"))||(Health<20 && GetCVar("MRIntW_LowHealthShake")))A_OverlayOffset(RightLay, CFRandom(-.5, .5), CFRandom(-.5, .5), WOF_ADD);
	}
}

Class MRIntWeaps_FistSelector: CustomInventory
{
	Default
	{
		+INVENTORY.ALWAYSPICKUP
		+Inventory.Quiet
		Inventory.PickupMessage "";
		Inventory.PickupSound "";
	}
	States
	{
	Spawn:
		TNT1 A -1;
		Stop;
	Pickup:
		TNT1 A 0 {A_SelectWeapon("MRIntWeaps_Fist");invoker.Destroy();}
		Stop;
	}
}