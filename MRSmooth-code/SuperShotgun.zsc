Class MRIntWeaps_SuperShotgun: SuperShotgun Replaces SuperShotgun
{
	Default{
		Weapon.SlotNumber 3;
		Weapon.BobStyle "Smooth";
	}
	Const Lay1 = 2;//Main layer
	Const Lay2 = PSP_FLASH+1;//Muzzle flash
	Const LeftHand = PSP_FLASH+40;//Hand with shells
	Const Lay3 = 1500;//Barrels layer over shell
	
	int BobState, IdleDelay;
	Bool LowerOpen, //For switching mid reload
		Reloading;//Disable smoke calls from doeffect during reloading
		
	Float Recoil;
	Override Void DoEffect()
	{
		if(Recoil>0){Recoil-=.25;Owner.A_SetPitch(Owner.Pitch+.25*MRIntW_RecoilAmount);}
		
		For(int i=0;i<Casings.Size();i++){
			if(Casings[i])DoCasing(i);
		}
		
		MuzzleSmoke();
		
		//Disabling weapon interpolation while game is loading
		Let Handler = MRIntWeaps_Handler(EventHandler.Find("MRIntWeaps_Handler"));
		if(Handler && Handler.LoadingDelay>0 && Owner.Player.FindPSprite(Lay1) && Owner.Player.FindPSprite(Lay1).CurState){
			Owner.Player.GetPSprite(Lay1).bInterpolate = false;
			Owner.Player.GetPSprite(Lay1).Y = Max(Owner.Player.GetPSprite(Lay1).Y, 0);
		}
	}
	
	action void A_FireShotgun2()
	{
		if (!player)return;

		A_StartSound ("MRIntW/SSGFire", CHAN_WEAPON, pitch:i_timescale);
		Weapon weap = player.ReadyWeapon;
		if (weap != null && invoker == weap && stateinfo != null && stateinfo.mStateType == STATE_Psprite)
		{
			if (!weap.DepleteAmmo (weap.bAltFire, true, 2))
				return;
			
			player.SetPsprite(PSP_FLASH, weap.FindState('Flash'), true);
		}
		player.mo.PlayAttacking2 ();

		double pitch = BulletSlope ();
			
		for (int i = 0 ; i < 20 ; i++)
		{
			int damage = 5 * random[FireSG2](1, 3);
			double ang = angle + Random2[FireSG2]() * (11.25 / 256);

			LineAttack (ang, PLAYERMISSILERANGE, pitch + Random2[FireSG2]() * (7.097 / 256), damage, 'Hitscan', "BulletPuff");
		}
	}
	
	States{
		Select:
			TNT1 A 0 {invoker.ResetDead();A_Overlay(Lay1, "GunRaise");}
		Raise:
			#### A 1 A_Raise();
			Loop;
		Deselect:
			TNT1 A 0 {
				if(Health<1){A_Overlay(Lay1, "GunDie");Return;}
				if(!invoker.LowerOpen)A_Overlay(Lay1, "GunLower");
			}
		Lower:
			#### A 1 A_Lower();
			Loop;
		
		GunRaise:
			TNT1 AAAA 1 {
				invoker.Reloading = false;
				A_OverlayFlags(Lay1, PSPF_ADDWEAPON , false);
				A_OverlayPivot(Lay1, .5, 0);
				A_OverlayOffset(Lay1, 51, 89);
				A_OverlayScale(Lay1, 1.2, 1.2);
				A_OverlayRotate(Lay1, -10.5);
			}
			SH12 A 1{invoker.LowerOpen = false;if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("SH22A0");}
			#### AAAAAAA 1 {
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, -7, -9.5, WOF_ADD);
				A_OverlayScale(Lay1, -.02, -.02, WOF_ADD);
				A_OverlayRotate(Lay1, 1.2, WOF_ADD);
			}
			#### AA 1 {
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, -2, 4, WOF_ADD);
				A_OverlayScale(Lay1, -.02, -.02, WOF_ADD);
				A_OverlayRotate(Lay1, 1.4, WOF_ADD);
			}
			#### A 2{
				A_OverlayOffset(Lay1, 0, 32, WOF_INTERPOLATE);
				A_OverlayScale(Lay1, 1, 1, WOF_INTERPOLATE);
				A_OverlayRotate(Lay1, 0, WOF_INTERPOLATE);
			}
			#### A 1 {
				A_OverlayFlags(Lay1, PSPF_ADDWEAPON , true);
				A_OverlayOffset(Lay1, 0, 0);
				A_OverlayScale(Lay1, 1, 1);
				A_OverlayRotate(Lay1, 0);
			}
		GunWait:
			SH12 A 1 {
				invoker.LowerOpen = false;if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("SH22A0");
				A_IdleAnimation(Lay1);
				//A_OverlayRotate(Lay1, -.2, WOF_ADD);
			}
			#### # 0 A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
			//#### # 0 A_JumpIf(invoker.IdleDelay>35 && abs(invoker.BobState)<2, "GunIdle1");
			Loop;
		GunLower:
			#### # 0 A_JumpIf(invoker.LowerOpen, "GunLowerOpen");
			SH12 A 1 {
				invoker.Reloading = false;
				if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("SH22A0");
				A_OverlayFlags(Lay1, PSPF_ADDWEAPON, false);
				A_OverlayOffset(Lay1, 0, Player.GetPSprite(PSP_WEAPON).Y);
				A_OverlayRotate(Lay1, 0);
				A_OverlayScale(Lay1, 1, 1);
				A_OverlayPivot(Lay1, .5, 0);
			}
			#### ## 1 {
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, -6, -6, WOF_ADD);
				A_OverlayRotate(Lay1, 1, WOF_ADD);
			}
			#### # 1 {
				A_OverlayOffset(Lay1, -3, -3, WOF_ADD);
				A_OverlayRotate(Lay1, .5, WOF_ADD);
			}
			#### ## 1 {
				A_OverlayOffset(Lay1, 10, 14, WOF_ADD);
				A_OverlayRotate(Lay1, -1, WOF_ADD);
			}
			#### ####### 1 {
				A_OverlayOffset(Lay1, 7, 10, WOF_ADD);
				A_OverlayRotate(Lay1, -1, WOF_ADD);
				A_OverlayScale(Lay1, .025, .025, WOF_ADD);
			}
			Stop;
		GunLowerOpen:
			SH12 D 1 {
				if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("SH22A0");
				/*A_OverlayPivot(Lay1, .5, 0);
				A_OverlayOffset(Lay1, -105, -25);
				A_OverlayRotate(Lay1, 10);
				A_OverlayScale(Lay1, 1, 1);*/
				
				A_OverlayOffset(Lay1, -15, 2, WOF_ADD);
				A_OverlayRotate(Lay1, 2, WOF_ADD);
				A_OverlayScale(Lay1, .01, .01, WOF_ADD);
				
				Double Rot = Player.GetPSprite(Lay1).Rotation;
				
				invoker.MuzzleSmoke((-30+Rot, 0-Rot), (-15+Rot, -5-Rot));
			}
			#### ################ 1 {
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, -15, 3, WOF_ADD);
				A_OverlayRotate(Lay1, 3, WOF_ADD);
				A_OverlayScale(Lay1, .01, .01, WOF_ADD);
				
				Double Rot = Player.GetPSprite(Lay1).Rotation;
				
				invoker.MuzzleSmoke((-30+Rot, 0-Rot), (-15+Rot, -5-Rot));
			}
			Stop;
		GunIdle1:
			#### # 0 {
				A_OverlayPivot(Lay1, .5, 1);
				invoker.BobState = 0;
				invoker.IdleDelay = CRandom(-10, -3)*70;
				if((GetCVar("MRIntW_BersShake")>1 && FindInventory("PowerStrength"))||(Health<20 && GetCVar("MRIntW_LowHealthShake")))invoker.IdleDelay = CRandom(1, -3)*35;
			}
			#### # 0 A_JumpIf(CRandom(0, 255)<100, "GunIdle2");
			#### AAAA 1 {A_Overlayoffset(Lay1, -.15, .05, WOF_ADD);A_SetTics(CRandom(1,2));}
			#### AA 1 {A_OverlayOffset(Lay1, -.1, .05, WOF_ADD);A_OverlayRotate(Lay1, -.1, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AAA 1 {A_OverlayOffset(Lay1, .15, -.1, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AA 1 {A_OverlayOffset(Lay1, .15, -.025, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### A 1 {A_OverlayOffset(Lay1, .1, .0, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AA 1 {A_OverlayOffset(Lay1, .1, .025, WOF_ADD);A_OverlayRotate(Lay1, .05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AAA 1 {A_OverlayOffset(Lay1, .025, .05, WOF_ADD);A_OverlayRotate(Lay1, .05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AA 1 {A_OverlayOffset(Lay1, -.05, -.1, WOF_ADD);A_OverlayRotate(Lay1, -.05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### A 1 {A_OverlayOffset(Lay1, .0, .05, WOF_ADD);A_OverlayRotate(Lay1, .05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### A 2 {
				A_OverlayRotate(Lay1, 0, WOF_INTERPOLATE);
				A_Overlayoffset(Lay1, 0, 0, WOF_INTERPOLATE);
				A_BersShake();
				A_SetTics(CRandom(1,2));
			}
			#### # 0 A_JumpIf(CRandom(0, 10)<4, "GunIdle2");
			#### # 0 A_JumpIf(CRandom(0, 40)==1, "GunIdle1");
			Goto GunWait;
		GunIdle2:
			#### A 1 {A_Overlayoffset(Lay1, .0, .05, WOF_ADD);A_OverlayRotate(Lay1, -.05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AA 1 {A_Overlayoffset(Lay1, .05, -.1, WOF_ADD);A_OverlayRotate(Lay1, .05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AAA 1 {A_Overlayoffset(Lay1, -.025, .05, WOF_ADD);A_OverlayRotate(Lay1, -.05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AA 1 {A_Overlayoffset(Lay1, -.1, .025, WOF_ADD);A_OverlayRotate(Lay1, -.05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### A 1 {A_Overlayoffset(Lay1, -.1, .0, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AA 1 {A_Overlayoffset(Lay1, -.15, -.025, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AAA 1 {A_Overlayoffset(Lay1, -.15, -.1, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AA 1 {A_Overlayoffset(Lay1, .1, .05, WOF_ADD);A_OverlayRotate(Lay1, .1, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			#### AAAA 1 {A_Overlayoffset(Lay1, .15, .05, WOF_ADD);A_BersShake(); A_SetTics(CRandom(1,2));}
			Goto GunWait;
		
		Ready:
			TNT1 A 1 A_WeaponReady;
			Loop;
		Fire:
			TNT1 A 3 {A_Overlay(Lay1, "GunFire");if(MRIntW_InstantFire)A_SetTics(0);}
			TNT1 A 7 {
				A_FireShotgun2();
				A_Overlay(Lay2, "GunFlash");
				if(MRIntW_Recoil){invoker.Recoil+=3;A_SetPitch(Pitch-3*MRIntW_RecoilAmount);}//Recoil
			}
			TNT1 A 3 {if(!MRIntW_InstantFire)A_SetTics(0);}
			TNT1 A 7;//B
			TNT1 A 7 A_CheckReload();
			TNT1 A 7 A_StartSound ("MRIntW/SSGOpen", CHAN_AUTO, pitch:i_timescale);
			TNT1 A 7;
			TNT1 A 7 A_StartSound ("MRIntW/SSGLoad", CHAN_AUTO, pitch:i_timescale);
			TNT1 A 6;
			TNT1 A 6 {//H
				A_StartSound ("MRIntW/SSGClose", CHAN_WEAPON, pitch:i_timescale);
				A_Refire();
			}
			TNT1 A 5 A_ReFire();
			Goto Ready;
			
		GunFire:
			SH12 # 0 {
				A_OverlayPivot(Lay1, .5, .5);
				if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("SH22A0");
				invoker.Reloading = false;
			}
			#### # 0 A_JumpIf(MRIntW_InstantFire, 3);
			#### A 2;
			#### A 1 A_OverlayOffset(Lay1, 0, 1, WOF_ADD);
			#### A 1;//Shot
			#### A 1 {//Recoil
				Float Scal = CFRandom(.3, .4);
				Vector2 Ofst = (CRandom(-4, 2), CFRandom(10, 17));
				Float Rotate = CRandomPick(0, CFRandom(.9, 1.5));
				A_OverlayScale(Lay1, Scal, Scal, WOF_ADD);
				A_OverlayOffset(Lay1, Ofst.X, Ofst.Y, WOF_ADD);
				A_OverlayRotate(Lay1, Rotate, WOF_ADD);
				
				Player.GetPSprite(Lay2).Scale = Player.GetPSprite(Lay1).Scale;
				Player.GetPSprite(Lay2).Rotation = Player.GetPSprite(Lay1).Rotation;
				Player.GetPSprite(Lay2).X = Player.GetPSprite(Lay1).X;
				Player.GetPSprite(Lay2).Y = Player.GetPSprite(Lay1).Y;
			}
			#### A 1;
			#### AAA 1{//Recover
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				Player.GetPSprite(Lay1).Scale.X -= Min(.05, Max(0, Player.GetPSprite(Lay1).Scale.X-1));
				Player.GetPSprite(Lay1).Scale.Y = Player.GetPSprite(Lay1).Scale.X;
				Player.GetPSprite(Lay1).X *= .5;
				Player.GetPSprite(Lay1).Y *= .5;
				Player.GetPSprite(Lay1).Rotation *= .5;
				
				Player.GetPSprite(Lay2).Scale.X -= Min(.05, Max(0, Player.GetPSprite(Lay2).Scale.X-1));
				Player.GetPSprite(Lay2).Scale.Y = Player.GetPSprite(Lay2).Scale.X;
				Player.GetPSprite(Lay2).X *= .5;
				Player.GetPSprite(Lay2).Y *= .5;
				Player.GetPSprite(Lay2).Rotation *= .5;
			}
			#### A 2 {
				if(!MRIntW_InstantFire)A_SetTics(0);
				A_Overlayoffset(Lay1, CFRandom(-2, 2), CFRandom(2, 4), WOF_INTERPOLATE);
				Float Scal = CFRandom(.96, .99);
				A_OverlayScale(Lay1, Scal, Scal, WOF_INTERPOLATE);
				A_OverlayRotate(Lay1, 0, WOF_INTERPOLATE);
			}
			#### A 1 {if(!MRIntW_InstantFire)A_SetTics(0);A_OverlayOffset(Lay1, 0, 0, WOF_INTERPOLATE);A_OverlayScale(Lay1, 1, 1, WOF_INTERPOLATE);}
			#### A 1{
				A_OverlayScale(Lay1, 1, 1, WOF_INTERPOLATE);
				A_OverlayOffset(Lay1, CFRandom(-2, 2), CFRandom(0, 2), WOF_INTERPOLATE);
				A_OverlayRotate(Lay1, CFRandom(-.5, .5), WOF_INTERPOLATE);
			}
			#### B 1 {//Start Reload
				invoker.Reloading = true;
				invoker.LowerOpen = true;A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);A_OverlayPivot(Lay1, .5, 1);
				
				invoker.MuzzleSmoke((-17, 8), (-3, 3));
			}
			#### BCC 1 {
				A_OverlayOffset(Lay1, -10, -5, WOF_ADD);
				
				if(Player.GetPSprite(Lay1).Frame<2)invoker.MuzzleSmoke((-17, 8), (-3, 3));
				else invoker.MuzzleSmoke((-20, 0), (-12, -5));
			}
			#### CD 1 {
				A_OverlayOffset(Lay1, -20, -5, WOF_ADD);
				
				if(Player.GetPSprite(Lay1).Frame<3)invoker.MuzzleSmoke((-20, 0), (-12, -5));
				else invoker.MuzzleSmoke((-30, 0), (-15, -5));
			}
			#### DD 1 {//ReloadCheck
				A_OverlayOffset(Lay1, -10, -2, WOF_ADD);
				A_OverlayRotate(Lay1, 2, WOF_ADD);
				
				Double Rot = Player.GetPSprite(Lay1).Rotation;
				
				invoker.MuzzleSmoke((-30+Rot, 0-Rot), (-15+Rot, -5-Rot));
			}
			#### # 0 {invoker.IncreaseSmoke();}
			#### DD 1 {
				A_OverlayRotate(Lay1, 1.5, WOF_ADD);
				A_OverlayOffset(Lay1, -3, -1, WOF_ADD);
				
				Double Rot = Player.GetPSprite(Lay1).Rotation;
				
				invoker.MuzzleSmoke((-30+Rot, 0-Rot), (-20+Rot, -5-Rot));
			}
			#### # 0 A_JumpIf(InStateSequence(Player.GetPSprite(PSP_WEAPON).CurState, invoker.FindState("Deselect")), "GunLowerOpen");
			#### # 0 {invoker.LowerOpen = false;}
			#### DDD 1 {//Openin
				A_OverlayRotate(Lay1, -7, WOF_ADD);
				A_OverlayScale(Lay1, -.05, -.05, WOF_ADD);
				A_OverlayOffset(Lay1, 25, 8, WOF_ADD);
				
				Double Rot = Player.GetPSprite(Lay1).Rotation*2;
				Double Scal = Player.GetPSprite(lay1).Scale.X;
				
				invoker.MuzzleSmoke((-25*Scal-Rot*2, 5/Scal-Rot), (-15*Scal-Rot*2, 1/Scal-Rot));
			}
			#### # 0 {A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);A_OverlayPivot(Lay1, .5, 0);}
			#### P 0 A_JumpIf(GetCVar("MRIntW_Casings"), 2);
			#### E 0;
			#### # 1 {
				A_OverlayRotate(Lay1, 50);
				A_OverlayScale(Lay1, 1, 1);
				A_OverlayOffset(Lay1, -30, 10);
				
				invoker.MuzzleSmoke((40, 0), (50, -2));
			}
			#### ## 1 {
				A_OverlayRotate(Lay1, -43, WOF_ADD);
				A_OverlayOffset(Lay1, 30, 6, WOF_ADD);
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				
				int Frame = (Player.GetPSprite(Lay1).Rotation-7.)/-43;
				
				invoker.MuzzleSmoke((45, 10+Frame*10), (55, 8+Frame*10));
			}
			#### # 0{
				if(MRIntW_ShotgunSmoke){//Smoke
					Double Alpha = CFRandom(.01, .08)*3;
					int Amount = CRandom(0, 1);
					For(int i=Amount;i<3;i++){
						int index;
						For(index=0;index<invoker.Smokes.Size()+1;index++){
							if(index>=invoker.Smokes.Size()){index=-1;Break;}
							if(!invoker.Smokes[index])Break;
						}
						
						if(index<invoker.Smokes.Size() && index>-1){
							MRIntWeaps_SmokeContainer a = New('MRIntWeaps_SmokeContainer');
							a.Vel = (CFRandom(-1.4, .5), -CFRandom(.3, .5), CFRandom(-.5, .5));
							invoker.Smokes[index] = a;
														
							A_Overlay(SmokeStart+index, "SmokeLay");
							A_OverlayOffset(SmokeStart+index, 95, 137);
							Player.GetPSprite(SmokeStart+index).Scale = (1,1)*CFRandom(.3, .4);
							A_OverlayAlpha(SmokeStart+index, Alpha);
						}
					}
					
					For(int i=Amount;i<3;i++){
						int index;
						For(index=0;index<invoker.Smokes.Size()+1;index++){
							if(index>=invoker.Smokes.Size()){index=-1;Break;}
							if(!invoker.Smokes[index])Break;
						}
						
						if(index<invoker.Smokes.Size() && index>-1){
							MRIntWeaps_SmokeContainer a = New('MRIntWeaps_SmokeContainer');
							a.Vel = (CFRandom(-1.4, .5), -CFRandom(.3, .5), CFRandom(-.5, .5));
							invoker.Smokes[index] = a;
														
							A_Overlay(SmokeStart+index, "SmokeLay");
							A_OverlayOffset(SmokeStart+index, 95+20, 137);
							Player.GetPSprite(SmokeStart+index).Scale = (1,1)*CFRandom(.3, .4);
							A_OverlayAlpha(SmokeStart+index, Alpha);
						}
					}
				}
				
				//Casings
				if(!GetCVar("MRIntW_Casings"))Return;
				Float Dir = CFRandom(-.9, 1.5);

				For(int i=0;i<2;i++){
					int index;
					For(index=0;index<invoker.Casings.Size()+1;index++){
						if(index>=invoker.Casings.Size()){index=-1;Break;}
						if(!invoker.Casings[index]){invoker.Casings[index] = true;Break;}
					}
					
					if(index<invoker.Casings.Size() && index>-1){
						A_Overlay(CaseStart+index, "CasingLay");
						A_OverlayOffset(CaseStart+index, 100+Player.GetPSprite(Lay1).X+i*20, 157+Player.GetPSprite(Lay1).Y-i*3);
						invoker.CasingDir[index] = Dir;
					}
				}
			}
			#### # 0 A_Overlay(Lay3, "Barrels");
			#### # 0 {
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);
				A_OverlayRotate(Lay1, 4);
				
				A_OverlayOffset(Lay3, Player.GetPSprite(Lay1).X, Player.GetPSprite(Lay1).Y);
				A_OverlayRotate(Lay3, 4);
			}
			#### F 1 {
				A_OverlayRotate(Lay1, -2, WOF_ADD);
				A_OverlayOffset(Lay1, 10, 5);
				
				A_OverlayRotate(Lay3, -2, WOF_ADD);
				A_OverlayOffset(Lay3, 10, 5);
				
				invoker.MuzzleSmoke((60, 75), (62, 75));
			}
			#### F 1 {
				A_OverlayRotate(Lay1, -2, WOF_ADD);
				A_OverlayOffset(Lay1, -6, -3, WOF_ADD);
				
				A_OverlayRotate(Lay3, -2, WOF_ADD);
				A_OverlayOffset(Lay3, -6, -3, WOF_ADD);
				
				invoker.MuzzleSmoke((60, 75), (62, 75));
			}
			#### F 1  {A_OverlayOffset(Lay1, 0, 0, WOF_INTERPOLATE);A_OverlayOffset(Lay3, 0, 0, WOF_INTERPOLATE);invoker.MuzzleSmoke((60, 75), (62, 75));}
			#### # 0 A_Overlay(LeftHand, "HandLoading");
			#### FFFFFFFF 1 {//Waiting for the shell
				if(GetAge()%4==0){
					Vector2 Rand = (CFRandom(-.5, .5), CFRandom(0, 1));
					
					A_OverlayOffset(Lay1, Rand.X, Rand.Y, WOF_INTERPOLATE);
					
					A_OverlayOffset(Lay3, Rand.X, Rand.Y, WOF_INTERPOLATE);
				}
				
				invoker.MuzzleSmoke((60, 75), (62, 75));
			}
			#### F 1{invoker.MuzzleSmoke((60, 75), (62, 75));}//Load
			#### F 1 {
				A_OverlayOffset(LeftHand, 10, 7, WOF_ADD);
				A_OverlayOffset(Lay1, 8, 6, WOF_ADD);
				A_OverlayOffset(Lay3, 8, 6, WOF_ADD);
				
				invoker.MuzzleSmoke((60, 75), (62, 75));
			}
			#### F 1 {
				A_OverlayOffset(LeftHand, 14, 8, WOF_ADD);
				A_OverlayOffset(Lay1, 8, 6, WOF_ADD);
				A_OverlayOffset(Lay3, 8, 6, WOF_ADD);
				
				invoker.MuzzleSmoke((60, 75), (62, 75));
			}
			#### JJ 1 {A_OverlayOffset(Lay1, 8, 12, WOF_ADD);A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);invoker.MuzzleSmoke((60, 75), (62, 75));}
			#### Z 1 {invoker.MuzzleSmoke((60, 75), (62, 75));}
			#### K 1 {
				A_OverlayRotate(Lay1, -50);
				A_OverlayOffset(Lay1, 55, 45);
				
				invoker.MuzzleSmoke((45, 40), (55, 40));
			}
			#### KK 1 {
				A_OverlayRotate(Lay1, 1, WOF_ADD);
				A_OverlayOffset(Lay1, 3, -4, WOF_ADD);
				
				invoker.MuzzleSmoke((45, 40), (55, 40));
			}
			#### KKK 1 {
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayRotate(Lay1, 16, WOF_ADD);
				A_OverlayOffset(Lay1, -12, -20, WOF_ADD);
				
				invoker.MuzzleSmoke((45, 40), (55, 40));
			}
			#### KKK 1 {
				A_OverlayOffset(Lay1, -25, 11, WOF_ADD);
				A_OverlayRotate(Lay1, 13, WOF_ADD);
				
				invoker.MuzzleSmoke((45, 40), (55, 40));
			}
			#### # 0 A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);
			#### B 1 {A_OverlayRotate(Lay1, 0);A_OverlayOffset(Lay1, -5, -5);invoker.MuzzleSmoke((-17, 8), (-3, 3));}
			#### B 1 {A_OverlayOffset(Lay1, -10, 10, WOF_INTERPOLATE);A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);invoker.MuzzleSmoke((-17, 8), (-3, 3));}
			#### A 1 {A_OverlayOffset(Lay1, -CFRandom(4, 8), CFRandom(3, 6));A_OverlayRotate(Lay1, CFRandom(1, 2));invoker.Reloading = false;}
			#### A 2 {A_OverlayOffset(Lay1, 0, 0, WOF_INTERPOLATE);A_OverlayRotate(Lay1, 0, WOF_INTERPOLATE);}
			Goto GunWait;
		Barrels:
			SH12 I 1 A_OverlayOffset(Lay3, Player.GetPSprite(Lay1).X, Player.GetPSprite(Lay1).Y);
			SH12 I 13;
			Stop;
			
		HandLoading:
			SH12 G 1 {
				A_OverlayPivot(LeftHand, .5, .5);
				A_OverlayOffset(LeftHand, -70, 80);
				A_OverlayRotate(LeftHand, -15);
				if(GetCVar("MRIntW_BrownGloves"))Player.GetPSprite(LeftHand).Sprite = GetSpriteIndex("SH22A0");
			}
			#### GGG 1 {
				A_OverlayOffset(LeftHand, 10, -26.67, WOF_ADD);
				A_OverlayRotate(LeftHand, 5, WOF_ADD);
				A_OverlayScale(LeftHand, .02, .02, WOF_ADD);
				A_OverlayFlags(LeftHand, PSPF_INTERPOLATE, true);
			}
			#### G 1 {
				A_OverlayRotate(LeftHand, 4, WOF_ADD);
				A_OverlayOffset(LeftHand, -8, -3, WOF_ADD);
			}
			#### GGG 1 {
				A_OverlayOffset(LeftHand, 19, 2.8, WOF_ADD);
				A_OverlayRotate(LeftHand, -2, WOF_ADD);
				A_OverlayScale(LeftHand, -.025, -.025, WOF_ADD);
			}
			#### G 1 {
				A_OverlayOffset(LeftHand, 5, 0, WOF_ADD);
				A_OverlayRotate(LeftHand, -6, WOF_ADD);
				A_OverlayScale(LeftHand, -.025, -.025, WOF_ADD);
			}
			#### G 2;
			#### # 0 A_OverlayFlags(LeftHand, PSPF_INTERPOLATE, false);
			#### H 1 A_OverlayOffset(LeftHand, 45, 28);
			#### HHHHHH 1 {
				A_OverlayFlags(LeftHand, PSPF_INTERPOLATE, true);
				A_OverlayOffset(LeftHand, 9, 18, WOF_ADD);
				A_OverlayRotate(LeftHand, -3, WOF_ADD);
			}
			Stop;
			
		GunFlash:
			SH12 N 1 BRIGHT {
				A_OverlayOffset(Lay2, Player.GetPSprite(Lay1).X, Player.GetPSprite(Lay1).Y);
				Player.GetPSprite(Lay1).Scale = Player.GetPSprite(Lay1).Scale;
				A_OverlayFlags(Lay2, PSPF_RENDERSTYLE|PSPF_ALPHA, true);
				A_OverlayRenderStyle(Lay2, STYLE_ColorAdd);
				A_OverlayPivot(Lay2, .5, .5);
				if(GetCVar("MRIntW_NoMuzzleFlash"))A_OverlayRenderStyle(OverlayId(), STYLE_None);
			}
			#### OON 1 BRIGHT;
			Stop;
			
			
		Flash:
			TNT1 A 4 A_Light1();
			TNT1 A 3 A_Light2();
			Goto LightDone;
		
		Sprites:
			SH22 A 0;
			MRD2 A 0;
			Stop;
	}
	
	Action Void A_IdleAnimation(int Lay = 2)//Idle hand bobbing
	{
		Bool Bers = (GetCVar("MRIntW_BersShake")>1 && FindInventory("PowerStrength"))||(Health<20 && GetCVar("MRIntW_LowHealthShake"));
		Float Mult = (Bers?2:1);
		if(invoker.BobState>=0){
			A_OverlayOffset(Lay, CFRandom(-.005, .015)*Mult, CFRandom(.01, .02)*Mult, WOF_ADD);
			if(invoker.BobState>90||Player.GetPSprite(Lay).Y>1.35){invoker.BobState = -invoker.BobState;}
			else invoker.BobState+=Mult;
		}
		else {
			A_OverlayOffset(Lay, -CFRandom(-.005, .015)*Mult, -CFRandom(.01, .02)*Mult, WOF_ADD);
			if(Player.GetPSprite(Lay).Y<0){A_OverlayOffset(Lay, 0, 0, WOF_INTERPOLATE);invoker.BobState = 0;}
			else if(!(invoker.BobState>=-1&&Player.GetPSprite(Lay).Y>0))invoker.BobState+=Mult;
		}
		if(Bers && GetAge()%2==0){//Shaking hand if got berserk
			A_OverlayOffset(Lay, CFRandom(-.25, .25), 0, WOF_INTERPOLATE|WOF_KEEPY);
			Player.GetPSprite(Lay).Y += CFRandom(-.25, .25);
			if(Player.GetPSprite(Lay).Y<0){Player.GetPSprite(Lay).Y = 0;invoker.BobState = 0;}
			if(Player.GetPSprite(Lay).Y>1.35){Player.GetPSprite(Lay).Y = 1.35;invoker.BobState = -90;}
		}
		
		if(GetCVar("MRIntW_IdleAnims"))invoker.IdleDelay++;
	}
	
	Action Void A_BersShake()
	{
		if((GetCVar("MRIntW_BersShake")>1 && FindInventory("PowerStrength"))||(Health<20 && GetCVar("MRIntW_LowHealthShake")))A_OverlayOffset(Lay1, CFRandom(-.25, .25), CFRandom(-.25, .25), WOF_ADD);
	}
}