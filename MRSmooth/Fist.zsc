Class MRIntWeaps_Fist: Fist Replaces Fist
{
	Default{
		Weapon.SlotNumber 1;
		Weapon.BobStyle "Smooth";
		+Weapon.NOALERT
	}
	Const RightLay = 2;//Right hand
	Const LeftLay = 3;//Left hand
	
	int BobState;
	Vector2 RightHand;
	States{
		RightHandRaise:
			TNT1 AAAAA 1 {
				A_OverlayFlags(RightLay, PSPF_ADDWEAPON , false);
				A_OverlayPivot(RightLay, 0, 0);
				A_OverlayOffset(RightLay, 40, 75);
				A_OverlayScale(RightLay, 1.7, 1.7);
			}
			PUN1 A 1{if(MRIntW_BrownGloves)Player.GetPSprite(RightLay).Sprite = GetSpriteIndex("PUN2A0");}
			#### AAAAAAAA 1 {
				A_OverlayFlags(RightLay, PSPF_INTERPOLATE, true);
				A_OverlayOffset(RightLay, -4.5, -4.5, WOF_ADD);
				A_OverlayScale(RightLay, -.08, -.08, WOF_ADD);
			}
			#### A 1 {
				A_OverlayFlags(RightLay, PSPF_ADDWEAPON , true);
				A_OverlayOffset(RightLay, 3, CFRandom(-3, -1));
				A_OverlayScale(RightLay, 1, 1);
			}
		RightHandWait:
			#### # 0 {
				A_OverlayFlags(RightLay, PSPF_INTERPOLATE, true);
				A_OverlayOffset(RightLay, 0, 0, WOF_INTERPOLATE);
			}
			PUN1 A 2 {if(MRIntW_BrownGloves)Player.GetPSprite(RightLay).Sprite = GetSpriteIndex("PUN2A0");}
		RightHandWaitLoop:
			#### A 1 A_IdleAnimation(RightLay);
			#### # 0 A_OverlayFlags(RightLay, PSPF_INTERPOLATE, true);
			Loop;
		RightHandLower:
			PUN1 AAAAAA 1 {
				if(MRIntW_BrownGloves)Player.GetPSprite(RightLay).Sprite = GetSpriteIndex("PUN2A0");
				A_OverlayPivot(RightLay, 0, 0);
				A_OverlayOffset(RightLay, 8, 5, WOF_ADD);
				A_OverlayScale(RightLay, .06, .06, WOF_ADD);
			}
			Stop;
		RightHandLowerFast:
			PUN1 A 1 {
				A_OverlayPivot(RightLay, 0, 0);
				A_OverlayScale(RightLay, 1, 1);
				A_OverlayOffset(RightLay, invoker.RightHand.X, invoker.RightHand.Y);
				if(MRIntW_BrownGloves)Player.GetPSprite(RightLay).Sprite = GetSpriteIndex("PUN2A0");
			}
			#### AAA 1 {
				A_OverlayFlags(RightLay, PSPF_INTERPOLATE, true);
				A_OverlayOffset(RightLay, 15, 15, WOF_ADD);
				A_OverlayScale(RightLay, .05, .05, WOF_ADD);
				invoker.RightHand = (Player.GetPSprite(RightLay).X, Player.GetPSprite(RightLay).Y);
			}
			Stop;
		RightHandUp:
			PUN1 A 1 {
				A_OverlayPivot(RightLay, 0, 0);
				A_OverlayOffset(RightLay, 30, 30);
				A_OverlayScale(RightLay, 1.15, 1.15);
				invoker.RightHand = (Player.GetPSprite(RightLay).X, Player.GetPSprite(RightLay).Y);
				if(MRIntW_BrownGloves)Player.GetPSprite(RightLay).Sprite = GetSpriteIndex("PUN2A0");
			}
			#### AAA 1{
				A_OverlayOffset(RightLay, -CFRandom(9.5, 10.5), -CFRandom(9.5, 10.5), WOF_ADD);
				A_OverlayScale(RightLay, -.05, -.05, WOF_ADD);
				invoker.RightHand = (Player.GetPSprite(RightLay).X, Player.GetPSprite(RightLay).Y);
			}
			#### A 1 {A_OverlayOffset(RightLay, CFRandom(-4, 1), CFRandom(-3, -1), WOF_INTERPOLATE);A_OverlayScale(RightLay, 1, 1, WOF_INTERPOLATE);}
			Goto RightHandWait;
		
		LeftHandPunch:
			PUN1 D 2 {
				A_StartSound("MRIntW/FistSwing", CHAN_AUTO, volume:.3, pitch:CFRandom(.9, 1.1));
				A_OverlayOffset(LeftLay, -20, 40);
				A_OverlayScale(LeftLay, 1.1, 1.1);
				A_OverlayPivot(LeftLay, 1, 0);
				if(MRIntW_BrownGloves)Player.GetPSprite(LeftLay).Sprite = GetSpriteIndex("PUN2A0");
			}
			#### D 1 A_OverlayOffset(LeftLay, 20, -20, WOF_ADD);
			#### D 1 {A_OverlayOffset(LeftLay, CFRandom(77, 82), -CFRandom(73, 76), WOF_ADD);A_OverlayScale(LeftLay, -.025, -.025, WOF_ADD);}//Punch
			#### DD 1 {A_OverlayOffset(LeftLay, CFRandom(-1, 1), CFRandom(-1, 1), WOF_ADD);A_OverlayScale(LeftLay, -.025, -.025, WOF_ADD);}
			#### D 1 {A_OverlayOffset(LeftLay, CFRandom(-1, 1), CFRandom(-1, 1), WOF_ADD);A_OverlayFlags(LeftLay, PSPF_INTERPOLATE, true);}
			#### DDDDD 1 {
				A_OverlayOffset(LeftLay, -24, 15, WOF_ADD);
				A_OverlayRotate(LeftLay, 5, WOF_ADD);
				A_OverlayScale(LeftLay, .05, .05, WOF_ADD);
			}
			Stop;
			
		Select:
			TNT1 A 0 A_Overlay(RightLay, "RightHandRaise");
		Raise:
			#### A 1 A_Raise();
			Loop;
		Deselect:
			TNT1 A 0 A_Overlay(RightLay, "RightHandLower");
		Lower:
			#### A 1 A_Lower();
			Loop;
			
		Ready:
			TNT1 A 1 A_WeaponReady();
			Loop;
		Fire://4, 4 Punch, 5, 4, 5 Refire
			TNT1 A 0 {
				A_Overlay(RightLay, "RightHandLowerFast");
				A_Overlay(LeftLay, "LeftHandPunch");
				if(!MRIntW_BetterAlert)A_AlertMonsters();
			}
			TNT1 A 4;
			TNT1 A 4 A_Punch();
			TNT1 A 5;
			TNT1 A 5 {A_Refire();A_Overlay(RightLay, "RightHandUp", true);}
			Goto Ready;
			
		Sprites:
			PUN2 AD 0;
			Stop;
	}
	
	//Select fists on berserk pickup
	Override Bool HandlePickup(Inventory item)
	{
		if(item is 'PowerStrength')Owner.A_SelectWeapon("MRIntWeaps_Fist");
		
		Return Super.HandlePickup(item);
	}
	
	Action Void A_IdleAnimation(int Lay = 2)//Idle hand bobbing
	{
		Bool Bers = MRIntW_BersShake && FindInventory("PowerStrength");
		Float Mult = (Bers?2:1);
		if(invoker.BobState>=0){
			A_OverlayOffset(Lay, CFRandom(-.005, .015)*Mult, CFRandom(.01, .02)*Mult, WOF_ADD);
			if(invoker.BobState>90||Player.GetPSprite(Lay).Y>1.35){invoker.BobState = -invoker.BobState;}
			else invoker.BobState+=Mult;
		}
		else {
			A_OverlayOffset(Lay, -CFRandom(-.005, .015)*Mult, -CFRandom(.01, .02)*Mult, WOF_ADD);
			if(Player.GetPSprite(Lay).Y<0){A_OverlayOffset(Lay, 0, 0, WOF_INTERPOLATE);invoker.BobState = 0;}
			else if(!(invoker.BobState>=-1&&Player.GetPSprite(Lay).Y>0))invoker.BobState+=Mult;
		}
		if(Bers && GetAge()%2==0){//Shaking hand if got berserk
			A_OverlayPivot(Lay, .2, .7);
			A_OverlayOffset(Lay, CFRandom(-.5, .5), 0, WOF_INTERPOLATE|WOF_KEEPY);
			Player.GetPSprite(Lay).Y += CFRandom(-.5, .5);
			if(Player.GetPSprite(Lay).Y<0){Player.GetPSprite(Lay).Y = 0;invoker.BobState = 0;}
			if(Player.GetPSprite(Lay).Y>1.35){Player.GetPSprite(Lay).Y = 1.35;invoker.BobState = -90;}
			Player.GetPSprite(Lay).Rotation = CFRandom(-.1, .1);
			Player.GetPSprite(Lay).Scale = (CFRandom(.99, 1.01), CFRandom(.99, 1.01));
		}
	}
}