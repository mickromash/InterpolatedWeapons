Class MRIntWeaps_Pistol: Pistol Replaces Pistol
{
	Default{
		Weapon.SlotNumber 2;
		Weapon.BobStyle "Smooth";
	}
	Const Lay1 = 2;
	Const Lay2 = PSP_FLASH+1;
	
	int BobState;
	States{
		Select:
			TNT1 A 0 A_Overlay(Lay1, "GunRaise");
		Raise:
			#### A 1 A_Raise();
			Loop;
		Deselect:
			TNT1 A 0 A_Overlay(Lay1, "GunLower");
		Lower:
			TNT1 A 1 A_Lower();
			Loop;
		
		GunRaise:
			TNT1 AAAAAAAAAA 1 {
				A_OverlayFlags(Lay1, PSPF_ADDWEAPON , false);
				if(MRIntW_LHand)A_OverlayFlags(Lay1, PSPF_MIRROR|PSPF_FLIP, true);//Left handed
				A_OverlayPivot(Lay1, .5, 0);
				A_OverlayOffset(Lay1, -25, 95);
				A_OverlayScale(Lay1, .8, .8);
				A_OverlayRotate(Lay1, 10.5);
			}
			P1SG A 1{if(MRIntW_BrownGloves)Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("P2SGA0");}
			#### AAAAA 1 {
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, 5, -13, WOF_ADD);
				A_OverlayScale(Lay1, .04, .04, WOF_ADD);
				A_OverlayRotate(Lay1, -2, WOF_ADD);
			}
			#### A 1 {
				A_OverlayFlags(Lay1, PSPF_ADDWEAPON , true);
				A_OverlayOffset(Lay1, 0, 0);
				A_OverlayScale(Lay1, 1, 1);
				A_OverlayRotate(Lay1, 0);
			}
		GunWait:
			P1SG A 1 {
				if(MRIntW_BrownGloves)Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("P2SGA0");
				if(invoker.BobState>=0){
					A_OverlayOffset(Lay1, CFRandom(-.005, .015), CFRandom(.01, .02), WOF_ADD);
					if(invoker.BobState>90){invoker.BobState = -invoker.BobState;}
					else invoker.BobState++;
				}
				else {
					A_OverlayOffset(Lay1, -CFRandom(-.005, .015), -CFRandom(.01, .02), WOF_ADD);
					if(Player.GetPSprite(Lay1).Y<0){A_OverlayOffset(Lay1, 0, 0, WOF_INTERPOLATE);invoker.BobState = 0;}
					else if(!(invoker.BobState>=-1&&Player.GetPSprite(Lay1).Y>0))invoker.BobState++;
				}
				if(MRIntW_LHand)A_OverlayFlags(Lay1, PSPF_MIRROR|PSPF_FLIP, true);
				else A_OverlayFlags(Lay1, PSPF_MIRROR|PSPF_FLIP, false);
			}
			#### # 0 A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
			Loop;
		GunLower:
			P1SG A 1 {
				if(MRIntW_BrownGloves)Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("P2SGA0");
				if(MRIntW_LHand)A_OverlayFlags(Lay1, PSPF_MIRROR|PSPF_FLIP, true);
				A_OverlayOffset(Lay1, 0, 0);
				A_OverlayRotate(Lay1, 0);
				A_OverlayScale(Lay1, 1, 1);
			}
			#### ###### 1 {
				A_OverlayPivot(Lay1, .5, 0);
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, 5, 14, WOF_ADD);
				A_OverlayRotate(Lay1, 8, WOF_ADD);
				A_OverlayScale(Lay1, .025, .025, WOF_ADD);
			}
			Stop;
		
		Ready:
			TNT1 A 1 A_WeaponReady();
			Loop;
		
		Fire:
			TNT1 A 4 {A_Overlay(Lay1, "GunFire");if(MRIntW_InstantFire)A_SetTics(0);}
			TNT1 A 6 {A_FirePistol();A_Overlay(Lay2, "GunFlash");}
			TNT1 A 4;
			TNT1 A 4 A_JumpIf(!MRIntW_InstantFire, 1);
			TNT1 A 5 A_ReFire();
			Goto Ready;
			
		GunFire:
			P1SG A 0{
				if(MRIntW_LHand)A_OverlayFlags(Lay1, PSPF_MIRROR|PSPF_FLIP, true);
				A_OverlayPivot(Lay1, .5, .5);
				if(MRIntW_BrownGloves)Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("P2SGA0");
			}
			#### # 0 A_JumpIf(MRIntW_InstantFire, 5);
			#### A 1;
			#### B 1;
			#### BB 1 A_OverlayOffset(Lay1, 0, -1, WOF_ADD);
			#### B 1;//Shot
			#### BB 1 {
				Float Scal = CFRandom(.05, .06);
				Vector2 Ofst = (CFRandom(.1, 1), CFRandom(5, 10));
				Float Rotate = -CRandomPick(0, CFRandom(0, 1));
				A_OverlayScale(Lay1, Scal, Scal, WOF_ADD);
				A_OverlayOffset(Lay1, Ofst.X, Ofst.Y, WOF_ADD);
				A_OverlayRotate(Lay1, Rotate, WOF_ADD);
				
				Player.GetPSprite(Lay2).Scale = Player.GetPSprite(Lay1).Scale;
				Player.GetPSprite(Lay2).Rotation = Player.GetPSprite(Lay1).Rotation;
				Player.GetPSprite(Lay2).X = Player.GetPSprite(Lay1).X;
				Player.GetPSprite(Lay2).Y = Player.GetPSprite(Lay1).Y;
			}
			#### CDEE 1{
				Float Scal = CFRandom(.01, .03);
				Vector2 Ofst = (CFRandom(.1, 1), 0);
				Float Rotate = -CRandomPick(0, CFRandom(0, 1));
				A_OverlayScale(Lay1, Scal, Scal, WOF_ADD);
				A_OverlayOffset(Lay1, Ofst.X, Ofst.Y, WOF_ADD);
				A_OverlayRotate(Lay1, Rotate, WOF_ADD);
				
				Player.GetPSprite(Lay2).Scale = Player.GetPSprite(Lay1).Scale;
				Player.GetPSprite(Lay2).Rotation = Player.GetPSprite(Lay1).Rotation;
				Player.GetPSprite(Lay2).X = Player.GetPSprite(Lay1).X;
				Player.GetPSprite(Lay2).Y = Player.GetPSprite(Lay1).Y;
			}
			#### EDB 1{
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);
				Player.GetPSprite(Lay1).Scale.X -= Min(.05, Max(0, Player.GetPSprite(Lay1).Scale.X-1));
				Player.GetPSprite(Lay1).Scale.Y = Player.GetPSprite(Lay1).Scale.X;
				Player.GetPSprite(Lay1).X *= .4;
				Player.GetPSprite(Lay1).Y *= .4;
				Player.GetPSprite(Lay1).Rotation *= .4;
			}
			#### A 1{
				Float Scal = CFRandom(.9, 1.05);
				A_OverlayScale(Lay1, Scal, Scal, WOF_INTERPOLATE);
				A_OverlayOffset(Lay1, CFRandom(-2, 2), CRandom(-1, 4));
				A_OverlayRotate(Lay1, CFRandom(-.5, .5));
			}
			#### AA 1{
				Player.GetPSprite(Lay1).Scale = (1,1)+(Player.GetPSprite(Lay1).Scale-(1,1))*.4;
				Player.GetPSprite(Lay1).X *= .4;
				Player.GetPSprite(Lay1).Y *= .4;
				Player.GetPSprite(Lay1).Rotation *= .4;
			}
			#### A 2{
				A_OverlayScale(Lay1, 1, 1, WOF_INTERPOLATE);
				A_OverlayOffset(Lay1, 0, 0, WOF_INTERPOLATE);
				A_OverlayRotate(Lay1, 0, WOF_INTERPOLATE);
			}
			Goto GunWait;
		GunFlash:
			P1SF A 1 BRIGHT {
				A_OverlayOffset(Lay2, Player.GetPSprite(Lay1).X, Player.GetPSprite(Lay1).Y);
				Player.GetPSprite(Lay1).Scale = Player.GetPSprite(Lay1).Scale;
				A_OverlayFlags(Lay2, PSPF_RENDERSTYLE|PSPF_ALPHA, true);
				if(MRIntW_LHand)A_OverlayFlags(Lay2, PSPF_MIRROR|PSPF_FLIP, true);
				A_OverlayRenderStyle(Lay2, STYLE_ColorAdd);
				A_OverlayPivot(Lay2, .5, .5);
			}
			P1SF BA 1 BRIGHT;
			Stop;
		
		Flash:
			TNT1 A 7 A_Light1();
			Goto LightDone;
			TNT1 A 7 A_Light1();
			Goto LightDone;
		
		Sprites:
			P2SG ABCDE 0;
			Stop;
	}
}