Class MRIntWeaps_PlasmaRifle: PlasmaRifle Replaces PlasmaRifle
{
	Default{
		Weapon.SlotNumber 6;
		Weapon.BobStyle "Smooth";
	}
	Const Lay1 = 2;//Main frame (body)
	Const Lay2 = -2;//Muzzle
	
	int BobState;
	Double Muzzle,//Muzzle position
			BodyScal;//Rifle body sqiush during firing
	Bool MuzzleCycle,//Firing frame
		InterpolateMuzzle;
	States{
		Select:
			TNT1 A 0 A_Overlay(Lay1, "GunRaise");
		Raise:
			#### A 1 A_Raise();
			Loop;
		Deselect:
			TNT1 A 0 A_Overlay(Lay1, "GunLower");
		Lower:
			TNT1 A 1 A_Lower();
			Loop;
		Ready:
			TNT1 A 1 A_WeaponReady();
			Loop;
		GunMuzzle:
			PLSO # 1{
				if(invoker.InterpolateMuzzle){
					Player.GetPSprite(Lay2).OldX = Player.GetPSprite(Lay1).OldX;
					Player.GetPSprite(Lay2).OldY = Player.GetPSprite(Lay1).OldY + invoker.Muzzle*3;
					Player.GetPSprite(Lay2).X = Player.GetPSprite(Lay1).X;
					Player.GetPSprite(Lay2).Y = Player.GetPSprite(Lay1).Y + invoker.Muzzle*3;
					Player.GetPSprite(Lay2).Scale = Player.GetPSprite(Lay1).Scale + (.01,.01)*invoker.Muzzle;
					Player.GetPSprite(Lay2).Scale.X -= invoker.BodyScal;
					Player.GetPSprite(Lay2).Rotation = Player.GetPSprite(Lay1).Rotation;
					Player.GetPSprite(Lay2).Pivot = Player.GetPSprite(Lay1).Pivot;
					A_OverlayFlags(Lay2, PSPF_INTERPOLATE, true);
				}
				else {
					Player.GetPSprite(OverlayId()).ResetInterpolation();
				}
				Player.GetPSprite(Lay2).Frame = Player.GetPSprite(Lay1).Frame;
			}
			#### # 0 A_JumpIf(Player.GetPSprite(Lay1).Sprite == GetSpriteIndex("PLSRA0"), Null);
			Loop;
		
		GunRaise:
			TNT1 AAAAAAA 1 {
				A_OverlayFlags(Lay1, PSPF_ADDWEAPON , false);
				A_OverlayPivot(Lay1, .5, 0);
				A_OverlayOffset(Lay1, -20, 130);
				A_OverlayScale(Lay1, 1.1, 1.1);
				A_OverlayRotate(Lay1, 4);
			}
			PLSR B 1;
			PLSR ####### 1 {
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, 10, -9, WOF_ADD);
				A_OverlayScale(Lay1, -.04, -.04, WOF_ADD);
				A_OverlayRotate(Lay1, -1, WOF_ADD);}
			#### A 0 {
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);
				invoker.InterpolateMuzzle = false;
				A_Overlay(Lay2, "GunMuzzle");
				A_OverlayScale(Lay2, .95, .95);
				A_OverlayRotate(Lay2, 0);
				A_OverlayOffset(Lay2, 0, -6);
			}
			PLSG A 1 {
				A_OverlayFlags(Lay1, PSPF_ADDWEAPON , true);
				A_OverlayOffset(Lay1, 0, 0);
				A_OverlayScale(Lay1, 1, 1);
				A_OverlayRotate(Lay1, 0);
				invoker.InterpolateMuzzle = true;
			}
		GunWait:
			PLSG A 1 {
				if(invoker.BobState>=0){
					A_OverlayOffset(Lay1, CFRandom(-.005, .015), CFRandom(.01, .02), WOF_ADD);
					if(invoker.BobState>90){invoker.BobState = -invoker.BobState;}
					else invoker.BobState++;
				}
				else {
					A_OverlayOffset(Lay1, -CFRandom(-.005, .015), -CFRandom(.01, .02), WOF_ADD);
					if(Player.GetPSprite(Lay1).Y<0){A_OverlayOffset(Lay1, 0, 0, WOF_INTERPOLATE);invoker.BobState = 0;}
					else if(!(invoker.BobState>=-1&&Player.GetPSprite(Lay1).Y>0))invoker.BobState++;
				}
				if(invoker.Muzzle>.1)invoker.Muzzle *= .5;
				else invoker.Muzzle = 0;
			}
			#### # 0 {A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);A_OverlayFlags(Lay2, PSPF_INTERPOLATE, true);}
			Loop;
		GunLower:
			#### # 0 A_ClearOverlays(Lay2, Lay2);
			PLSR B 1 {
				A_OverlayPivot(Lay1, .5, 0);
				A_OverlayScale(Lay1, .9, .9);
				A_OverlayOffset(Lay1, 30, 20+(Player.GetPSprite(PSP_FLASH).Y));
			}
			PLSR ###### 1 {
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, -25, 14, WOF_ADD);
				A_OverlayRotate(Lay1, 2, WOF_ADD);
				A_OverlayScale(Lay1, .025, .025, WOF_ADD);
			}
			Stop;
			
		Fire:
			#### # 0 {Player.SetPsprite(Lay1, invoker.FindState("GunFire"));}
			TNT1 A 3 A_FirePlasma();
			TNT1 B 20 A_ReFire();
			Goto Ready;
		GunFire:
			#### # 0 A_Overlay(Lay2, "GunMuzzle", true);
			#### # 0 {invoker.Muzzle = -1.2;A_OverlayPivot(Lay1, .5, 1);A_OverlayPivot(Lay2, .5, 1);invoker.InterpolateMuzzle = false;}
			PLSG ### 1{
				Vector2 Rand = (FRandom(-1, 1), FRandom(0, 1));
				A_OverlayOffset(Lay1, Rand.X, Rand.Y, WOF_INTERPOLATE);
				A_OverlayOffset(Lay2, Rand.X, Rand.Y + invoker.Muzzle*3, WOF_INTERPOLATE);

				Float Scal = FRandom(1, 1.1);
				invoker.BodyScal = -FRandom(-.05, .05);
				A_OverlayScale(Lay1, Scal+invoker.BodyScal, Scal, WOF_INTERPOLATE);
				

				invoker.Muzzle += 1.3;
				if(Player.GetPSprite(Lay2).Frame<1)Player.GetPSprite(Lay2).Frame = 1;
				if(invoker.MuzzleCycle){
					if(Player.GetPSprite(Lay1).Frame<4)Player.GetPSprite(Lay1).Frame++;
					else invoker.MuzzleCycle = false;
				}
				else {
					if(Player.GetPSprite(Lay1).Frame>1)Player.GetPSprite(Lay1).Frame--;
					else invoker.MuzzleCycle = true;
				}
				
				
				Player.GetPSprite(Lay2).Frame = Player.GetPSprite(Lay1).Frame;
				Float MuzzleScale = Scal + .01*invoker.Muzzle;
				if(Player.GetPSprite(Lay2).Frame>2)MuzzleScale *= .9;
				A_OverlayScale(Lay2, MuzzleScale, MuzzleScale, WOF_INTERPOLATE);
			}
			PLSR A 1 {
				invoker.BodyScal = 0;
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);
				if(invoker.Muzzle<1)invoker.Muzzle = 1;
				A_OverlayPivot(Lay1, 1, 1);
				A_OverlayScale(Lay1, .85, .85);
				A_OverlayOffset(Lay1, 50, 30);
				A_Overlay(Lay2, "GunMuzzleReload");
				A_OverlayPivot(Lay2, 1, 1);
				A_OverlayOffset(Lay2, 50, 30+invoker.Muzzle*8);
				A_OverlayScale(Lay2, .85, .85);
			}
			PLSR A 1{
				A_OverlayRotate(Lay1, 1, WOF_ADD);
				A_OverlayOffset(Lay1, -20, -10, WOF_ADD);
				A_OverlayScale(Lay1, .05, .05, WOF_ADD);
				
				A_OverlayRotate(Lay2, 1, WOF_ADD);
				A_OverlayOffset(Lay2, -20, -10, WOF_ADD);
				A_OverlayScale(Lay2, .05, .05, WOF_ADD);
			}
			#### AA 1{
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayRotate(Lay1, 1, WOF_ADD);
				A_OverlayOffset(Lay1, -20, -10, WOF_ADD);
				A_OverlayScale(Lay1, .05, .05, WOF_ADD);
				
				A_OverlayRotate(Lay2, 1, WOF_ADD);
				A_OverlayOffset(Lay2, -20, -10, WOF_ADD);
				A_OverlayScale(Lay2, .05, .05, WOF_ADD);
				A_OverlayOffset(Lay2, 0, -7, WOF_ADD);
				
				if(invoker.Muzzle>.1)invoker.Muzzle *= .3;
				else invoker.Muzzle = 0;
			}
			PLSR AA 1{
				A_OverlayRotate(Lay1, 0, WOF_INTERPOLATE);
				A_OverlayRotate(Lay2, 0, WOF_INTERPOLATE);
				if(invoker.Muzzle>.1)invoker.Muzzle *= .3;
				else invoker.Muzzle = 0;
				
				Player.GetPSprite(Lay2).X = Player.GetPSprite(Lay1).X;
				Player.GetPSprite(Lay2).Y = Player.GetPSprite(Lay1).Y + invoker.Muzzle*8;
			}
			PLSR AA 1{
				A_OverlayRotate(Lay1, 0, WOF_INTERPOLATE);
				A_OverlayRotate(Lay2, 0, WOF_INTERPOLATE);
				invoker.Muzzle = CFRandom(0, .5);
				
				Player.GetPSprite(Lay2).X = Player.GetPSprite(Lay1).X;
				Player.GetPSprite(Lay2).Y = Player.GetPSprite(Lay1).Y + invoker.Muzzle*8;
			}
			PLSR AA 1{
				A_OverlayRotate(Lay1, 0, WOF_INTERPOLATE);
				A_OverlayRotate(Lay2, 0, WOF_INTERPOLATE);
				invoker.Muzzle = CFRandom(0, .2);
				
				Player.GetPSprite(Lay2).X = Player.GetPSprite(Lay1).X;
				Player.GetPSprite(Lay2).Y = Player.GetPSprite(Lay1).Y + invoker.Muzzle*8;
			}
			PLSR AA 1{
				A_OverlayRotate(Lay1, 0, WOF_INTERPOLATE);
				A_OverlayRotate(Lay2, 0, WOF_INTERPOLATE);
				invoker.Muzzle = 0;
				
				Player.GetPSprite(Lay2).X = Player.GetPSprite(Lay1).X;
				Player.GetPSprite(Lay2).Y = Player.GetPSprite(Lay1).Y + invoker.Muzzle*8;
			}
			#### # 1 A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);
			#### # 0 A_ClearOverlays(Lay2, Lay2);
			#### B 1;
			PLSR BBBB 1{
				invoker.Muzzle = 0;
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, 10, 5, WOF_ADD);
				A_OverlayRotate(Lay1, -1, WOF_ADD);
				A_OverlayScale(Lay1, -.05, -.05, WOF_ADD);
			}
			#### # 0 A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);
			PLSG A 1{
				A_OverlayScale(Lay1, 1, 1);
				A_OverlayOffset(Lay1, CFRandom(-4, 0), CFRandom(0, 2));
				A_OverlayRotate(Lay1, 0);
				A_Overlay(Lay2, "GunMuzzle");
				A_OverlayScale(Lay2, 1, 1);
				A_OverlayOffset(Lay2, Player.GetPSprite(Lay1).X, Player.GetPSprite(Lay1).Y);
				A_OverlayRotate(Lay2, 0);
			}
			PLSG A 1 {
				A_OverlayOffset(Lay1, CFRandom(-3, 2), CFRandom(0, 3), WOF_INTERPOLATE);
				A_OverlayOffset(Lay2, Player.GetPSprite(Lay1).X, Player.GetPSprite(Lay1).Y, WOF_INTERPOLATE);
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
			}
			#### # 0 {invoker.InterpolateMuzzle = true;}
			#### # 2 {
				A_OverlayOffset(Lay1, 0, 0, WOF_INTERPOLATE);
			}
			Goto GunWait;
		GunMuzzleReload:
			PLSO F 1;
			Loop;
		Flash:
			TNT1 A 4 A_Light1();
			Goto LightDone;
			TNT1 B 4 A_Light1();
			Goto LightDone;
	}
}