Class MRIntWeaps_SuperShotgun: SuperShotgun Replaces SuperShotgun
{
	Default{
		Weapon.SlotNumber 3;
		Weapon.BobStyle "Smooth";
	}
	Const Lay1 = 2;//Main layer
	Const Lay2 = PSP_FLASH+1;//Muzzle flash
	Const LeftHand = 3;//Hand with shells
	
	int BobState;
	Bool LowerOpen;//For switching mid reload
	States{
		Select:
			TNT1 A 0 A_Overlay(Lay1, "GunRaise");
		Raise:
			#### A 1 A_Raise();
			Loop;
		Deselect:
			TNT1 A 0 A_Overlay(Lay1, "GunLower");
		Lower:
			TNT1 A 1 A_Lower();
			Loop;
		
		GunRaise:
			TNT1 AAAAAAAA 1 {
				A_OverlayFlags(Lay1, PSPF_ADDWEAPON , false);
				A_OverlayPivot(Lay1, .5, 0);
				A_OverlayOffset(Lay1, 51, 90);
				A_OverlayScale(Lay1, 1.2, 1.2);
				A_OverlayRotate(Lay1, -10.5);
			}
			SH12 A 1{invoker.LowerOpen = false;if(MRIntW_BrownGloves)Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("SH22A0");}
			#### AAAAAAA 1 {
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, -7, -8, WOF_ADD);
				A_OverlayScale(Lay1, -.025, -.025, WOF_ADD);
				A_OverlayRotate(Lay1, 1.5, WOF_ADD);
			}
			#### A 1 {
				A_OverlayFlags(Lay1, PSPF_ADDWEAPON , true);
				A_OverlayOffset(Lay1, 0, 0);
				A_OverlayScale(Lay1, 1, 1);
				A_OverlayRotate(Lay1, 0);
			}
		GunWait:
			SH12 A 1 {
				if(MRIntW_BrownGloves)Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("SH22A0");
				if(invoker.BobState>=0){
					A_OverlayOffset(Lay1, CFRandom(-.005, .015), CFRandom(.01, .02), WOF_ADD);
					if(invoker.BobState>90){invoker.BobState = -invoker.BobState;}
					else invoker.BobState++;
				}
				else {
					A_OverlayOffset(Lay1, -CFRandom(-.005, .015), -CFRandom(.01, .02), WOF_ADD);
					if(Player.GetPSprite(Lay1).Y<0){A_OverlayOffset(Lay1, 0, 0, WOF_INTERPOLATE);invoker.BobState = 0;}
					else if(!(invoker.BobState>=-1&&Player.GetPSprite(Lay1).Y>0))invoker.BobState++;
				}
			}
			#### # 0 A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
			Loop;
		GunLower:
			#### # 0 A_JumpIf(invoker.LowerOpen, "GunLowerOpen");
			SH12 A 1 {
				if(MRIntW_BrownGloves)Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("SH22A0");
				A_OverlayOffset(Lay1, 0, 0);
				A_OverlayRotate(Lay1, 0);
				A_OverlayScale(Lay1, 1, 1);
			}
			#### ###### 1 {
				A_OverlayPivot(Lay1, .5, 0);
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, 25, 14, WOF_ADD);
				A_OverlayRotate(Lay1, -4, WOF_ADD);
				A_OverlayScale(Lay1, .025, .025, WOF_ADD);
			}
			Stop;
		GunLowerOpen:
			SH12 D 1 {
				if(MRIntW_BrownGloves)Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("SH22A0");
				A_OverlayPivot(Lay1, .5, 0);
				A_OverlayOffset(Lay1, -80, -25);
				A_OverlayRotate(Lay1, 4);
				A_OverlayScale(Lay1, 1, 1);
			}
			#### ####### 1 {
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, -15, 14, WOF_ADD);
				A_OverlayRotate(Lay1, 3, WOF_ADD);
				A_OverlayScale(Lay1, .025, .025, WOF_ADD);
			}
			Stop;
		
		Ready:
			TNT1 A 1 A_WeaponReady;
			Loop;
		Fire:
			TNT1 A 3 {A_Overlay(Lay1, "GunFire");if(MRIntW_InstantFire)A_SetTics(0);}
			TNT1 A 7 {A_FireShotgun2();A_Overlay(Lay2, "GunFlash");}
			TNT1 A 7;//B
			TNT1 A 7 A_CheckReload();
			TNT1 A 7 A_OpenShotgun2();
			TNT1 A 7;
			TNT1 A 7 A_LoadShotgun2();
			TNT1 A 6;
			TNT1 A 6 A_CloseShotgun2();//H
			TNT1 A 3 A_JumpIf(!MRIntW_InstantFire, 1);
			TNT1 A 5 A_ReFire();
			Goto Ready;
			
		GunFire:
			SH12 # 0 {A_OverlayPivot(Lay1, .5, .5);if(MRIntW_BrownGloves)Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("SH22A0");}
			#### # 0 A_JumpIf(MRIntW_InstantFire, 3);
			#### A 2;
			#### A 1 A_OverlayOffset(Lay1, 0, 1, WOF_ADD);
			#### A 1;//Shot
			#### A 1 {//Recoil
				Float Scal = CFRandom(.3, .4);
				Vector2 Ofst = (CRandom(-4, 2), CFRandom(10, 17));
				Float Rotate = CRandomPick(0, CFRandom(.9, 1.5));
				A_OverlayScale(Lay1, Scal, Scal, WOF_ADD);
				A_OverlayOffset(Lay1, Ofst.X, Ofst.Y, WOF_ADD);
				A_OverlayRotate(Lay1, Rotate, WOF_ADD);
				
				Player.GetPSprite(Lay2).Scale = Player.GetPSprite(Lay1).Scale;
				Player.GetPSprite(Lay2).Rotation = Player.GetPSprite(Lay1).Rotation;
				Player.GetPSprite(Lay2).X = Player.GetPSprite(Lay1).X;
				Player.GetPSprite(Lay2).Y = Player.GetPSprite(Lay1).Y;
			}
			#### A 1;
			#### AAA 1{//Recover
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				Player.GetPSprite(Lay1).Scale.X -= Min(.05, Max(0, Player.GetPSprite(Lay1).Scale.X-1));
				Player.GetPSprite(Lay1).Scale.Y = Player.GetPSprite(Lay1).Scale.X;
				Player.GetPSprite(Lay1).X *= .5;
				Player.GetPSprite(Lay1).Y *= .5;
				Player.GetPSprite(Lay1).Rotation *= .5;
			}
			#### A 1{
				A_OverlayScale(Lay1, 1, 1, WOF_INTERPOLATE);
				A_OverlayOffset(Lay1, CFRandom(-2, 2), CFRandom(0, 2), WOF_INTERPOLATE);
				A_OverlayRotate(Lay1, CFRandom(-.5, .5), WOF_INTERPOLATE);
			}
			#### B 1 {invoker.LowerOpen = true;A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);A_OverlayPivot(Lay1, .5, 1);}//Start Reload
			#### BCC 1 A_OverlayOffset(Lay1, -10, -5, WOF_ADD);
			#### CD 1 A_OverlayOffset(Lay1, -20, -5, WOF_ADD);
			#### DD 1 {A_OverlayOffset(Lay1, -10, -2, WOF_ADD);A_OverlayRotate(Lay1, 2, WOF_ADD);}//ReloadCheck
			#### DD 1 {A_OverlayRotate(Lay1, 1.5, WOF_ADD);A_OverlayOffset(Lay1, -3, -1, WOF_ADD);}
			#### # 0 {invoker.LowerOpen = false;}
			#### DDD 1 {A_OverlayRotate(Lay1, -7, WOF_ADD);A_OverlayScale(Lay1, -.05, -.05, WOF_ADD);A_OverlayOffset(Lay1, 30, 10, WOF_ADD);}//Openin
			#### # 0 {A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);A_OverlayPivot(Lay1, .5, 0);}
			#### E 1 {A_OverlayRotate(Lay1, 50);A_OverlayScale(Lay1, 1, 1);A_OverlayOffset(Lay1, -10, 20);}
			#### EE 1 {A_OverlayRotate(Lay1, -36, WOF_ADD);A_OverlayOffset(Lay1, 8, -8, WOF_ADD);A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);}
			#### # 0 {A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);A_OverlayRotate(Lay1, 4);}
			#### FF 1 {A_OverlayRotate(Lay1, -2, WOF_ADD);A_OverlayOffset(Lay1, -2, 2);}
			#### F 1  A_OverlayOffset(Lay1, 0, 0, WOF_INTERPOLATE);
			#### # 0 A_Overlay(LeftHand, "HandLoading");
			#### FF 4 A_OverlayOffset(Lay1, CFRandom(-.5, .5), CFRandom(0, 1), WOF_INTERPOLATE);//Wait for the shell
			#### H 1;//Load
			#### HI 1 A_OverlayOffset(Lay1, 8, 6, WOF_ADD);
			#### JJ 1 {A_OverlayOffset(Lay1, 8, 12, WOF_ADD);A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);}
			#### Z 1;
			#### K 1 {A_OverlayRotate(Lay1, -50);A_OverlayOffset(Lay1, 55, 45);}
			#### KK 1 {A_OverlayRotate(Lay1, 1, WOF_ADD);A_OverlayOffset(Lay1, 3, -4, WOF_ADD);}
			#### KKK 1 {A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);A_OverlayRotate(Lay1, 16, WOF_ADD);A_OverlayOffset(Lay1, -12, -20, WOF_ADD);}
			#### KKK 1 {A_OverlayOffset(Lay1, -25, 11, WOF_ADD);A_OverlayRotate(Lay1, 13, WOF_ADD);}
			#### # 0 A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);
			#### L 1 {A_OverlayRotate(Lay1, 0);A_OverlayOffset(Lay1, -5, -5);}
			#### L 1 {A_OverlayOffset(Lay1, -10, 10, WOF_INTERPOLATE);A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);}
			#### A 1 {A_OverlayOffset(Lay1, -CFRandom(4, 8), CFRandom(3, 6));A_OverlayRotate(Lay1, CFRandom(1, 2));}
			#### A 2 {A_OverlayOffset(Lay1, 0, 0, WOF_INTERPOLATE);A_OverlayRotate(Lay1, 0, WOF_INTERPOLATE);}
			Goto GunWait;
			
		HandLoading:
			SH12 GG 1 {
				A_OverlayPivot(LeftHand, .5, .5);
				A_OverlayOffset(LeftHand, -60, 60);
				A_OverlayRotate(LeftHand, -15);
				if(MRIntW_BrownGloves)Player.GetPSprite(LeftHand).Sprite = GetSpriteIndex("SH22A0");
			}
			#### GG 1 {A_OverlayOffset(LeftHand, 10, -30, WOF_ADD);A_OverlayRotate(LeftHand, 7.5, WOF_ADD);A_OverlayScale(LeftHand, .03, .03, WOF_ADD);A_OverlayFlags(LeftHand, PSPF_INTERPOLATE, true);}
			#### G 1 {A_OverlayRotate(LeftHand, 4, WOF_ADD);A_OverlayOffset(LeftHand, -8, -3, WOF_ADD);}
			#### GGG 1 {A_OverlayOffset(LeftHand, 20, 3, WOF_ADD);A_OverlayRotate(LeftHand, -2, WOF_ADD);A_OverlayScale(LeftHand, -.025, -.025, WOF_ADD);}//A_OverlayRotate(LeftHand, 5, WOF_ADD);}
			Stop;
			
		GunFlash:
			SH12 N 1 BRIGHT {
				A_OverlayOffset(Lay2, Player.GetPSprite(Lay1).X, Player.GetPSprite(Lay1).Y);
				Player.GetPSprite(Lay1).Scale = Player.GetPSprite(Lay1).Scale;
				A_OverlayFlags(Lay2, PSPF_RENDERSTYLE|PSPF_ALPHA, true);
				A_OverlayRenderStyle(Lay2, STYLE_ColorAdd);
				A_OverlayPivot(Lay2, .5, .5);
			}
			#### ON 1 BRIGHT;
			Stop;
			
			
		Flash:
			TNT1 A 4 A_Light1();
			TNT1 A 3 A_Light2();
			Goto LightDone;
		
		Sprites:
			SH22 ABCDEFGHIJKL 0;
			Stop;
	}
}