Class MRIntWeaps_Chainsaw: Chainsaw Replaces Chainsaw
{
	Default{
		Weapon.SlotNumber 1;
		Weapon.BobStyle "Smooth";
	}
	Const Lay1 = 2;
	
	int SawFrame;
	Float SawPos;
	
	MRIntWeaps_SmokeContainer Smokes[50];
	
	Float Recoil;
	Override Void DoEffect()
	{
		if(Recoil>0){Recoil-=.5;Owner.A_SetPitch(Owner.Pitch+.5*MRIntW_RecoilAmount);}
	}
	
	States{
		SmokeLay:
			NSHS A 0{
				A_OverlayFlags(OverlayId(), PSPF_ADDBOB|PSPF_ADDWEAPON, false);
				A_OverlayFlags(OverlayId(), PSPF_ALPHA|PSPF_RENDERSTYLE, True);
				A_OverlayRenderStyle(OverlayId(), MRIntW_Smoke?STYLE_TRANSLUCENT:STYLE_NONE);
				A_OverlayPivot(OverlayId(), .5, .5);
				Player.GetPSprite(OverlayId()).Alpha=CFRandom(.3, .5);
			}
			NSHS A 1{
				A_OverlayFlags(OverlayId(), PSPF_ALPHA|PSPF_RENDERSTYLE|PSPF_FORCESTYLE|PSPF_FORCEALPHA, True);
				A_OverlayRenderStyle(OverlayId(), MRIntW_Smoke?STYLE_TRANSLUCENT:STYLE_NONE);
			}
			NSHS AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1{
				int Index = OverlayId()-PSP_FLASH-10;
				A_OverlayFlags(OverlayId(), PSPF_INTERPOLATE, true);
				
				Player.GetPSprite(OverlayId()).Alpha-=.01;
				
				Float Scale = (CFRandom(.09, .13) + (Vel.X*Cos(Angle) + Vel.Y*Sin(Angle))*.2);
				Scale -= (invoker.Smokes[Index].Vel.Y);
				Scale *= .1;
				A_OverlayScale(OverlayId(), Max(0, Scale), Max(0, Scale), WOF_ADD);
				
				if(Player.GetPSprite(OverlayId()).Scale.Length()>15)A_OverlayRenderStyle(OverlayId(), STYLE_NONE);
				Player.GetPSprite(OverlayId()).Y += Player.Cmd.Pitch*.02 + Max(0, (Vel.X*Cos(Angle) + Vel.Y*Sin(Angle))*.1) + Vel.Z;
				Player.GetPSprite(OverlayId()).Y -= invoker.Smokes[Index].Vel.Z*Cos(Pitch);
				Player.GetPSprite(OverlayId()).Y += invoker.Smokes[Index].Vel.Y*Sin(Pitch);
				Player.GetPSprite(OverlayId()).X += Player.Cmd.Yaw*.02 + (Vel.X*Cos(Angle+90) + Vel.Y*Sin(Angle+90)*5);
				Player.GetPSprite(OverlayId()).X += invoker.Smokes[index].Vel.X;
			}
			TNT1 A 0 {invoker.Smokes[OverlayId()-PSP_FLASH-10].Destroy();invoker.Smokes[OverlayId()-PSP_FLASH-10]=Null;}
			Stop;
		
		
		Select:
			TNT1 A 0 A_Overlay(Lay1, "SawRaise");
		Raise:
			#### A 1 A_Raise();
			Loop;
		Deselect:
			TNT1 A 0 A_Overlay(Lay1, "SawLower");
		Lower:
			#### A 1 A_Lower();
			Loop;
		
		SawRaise:
			TNT1 AAAAA 1 {
				A_OverlayFlags(Lay1, PSPF_ADDWEAPON , false);
				A_OverlayPivot(Lay1, .5, 1);
				A_OverlayOffset(Lay1, -12, 65);
				A_OverlayScale(Lay1, .4, .4);
				A_OverlayRotate(Lay1, 20);
			}
			SAW1 A 1{if(MRIntW_BrownGloves)Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("SAW2A0");}
			#### AAAA 1 {
				A_OverlayOffset(Lay1, 1, -6, WOF_ADD);
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayScale(Lay1, .07, .07, WOF_ADD);
				A_OverlayRotate(Lay1, -3, WOF_ADD);
			}
			#### # 0 A_OverlayScale(Lay1, Player.GetPSprite(Lay1).Scale.X, Player.GetPSprite(Lay1).Scale.X-.2);
			#### CC 1 {
				A_OverlayOffset(Lay1, 1, -2, WOF_ADD);
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayScale(Lay1, .07, .16, WOF_ADD);
				A_OverlayRotate(Lay1, -2, WOF_ADD);
			}
			#### CC 1 {
				A_OverlayOffset(Lay1, 1, -2, WOF_ADD);
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayScale(Lay1, .05, .05, WOF_ADD);
				A_OverlayRotate(Lay1, -1, WOF_ADD);
			}
			#### D 1 {
				A_OverlayFlags(Lay1, PSPF_ADDWEAPON , true);
				A_OverlayOffset(Lay1, 0, 0);
				A_OverlayScale(Lay1, 1, .9);
				A_OverlayPivot(Lay1, .5, 1);
				A_OverlayRotate(Lay1, 0);
				invoker.SawFrame = 2;
			}
		SawReady:
			SAW1 # 1 {
				A_OverlayOffset(Lay1, CFRandom(-.5, .5), CFRandom(-.5, .5), WOF_INTERPOLATE);
				A_OverlayRotate(Lay1, CFRandom(-.25, .25), WOF_INTERPOLATE);
				Player.GetPSprite(Lay1).Frame = invoker.SawFrame;
				A_OverlayScale(Lay1, CFRandom(1, 1.025), CFRandom(1, 1.025), WOF_INTERPOLATE);
				if(MRIntW_BrownGloves)Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("SAW2A0");
				
				if(MRIntW_Smoke){
					int index;
					For(index=0;index<invoker.Smokes.Size();index++){
						if(!invoker.Smokes[index])Break;
					}
					if(index<invoker.Smokes.Size()){
						MRIntWeaps_SmokeContainer a = New('MRIntWeaps_SmokeContainer');
						a.Vel = (CFRandom(2.5, 3), -CFRandom(1, 1.2), CFRandom(.2, .6));
						invoker.Smokes[index] = a;
						
						Vector2 Bob = PlayerPawn(Self).BobWeapon(35);
						Bob.Y = Clamp(Bob.Y, 0, 50);
						
						A_Overlay(PSP_FLASH+10+index, "SmokeLay");
						A_OverlayOffset(PSP_FLASH+10+index, 190+Bob.X, 180+Bob.Y);
						Player.GetPSprite(PSP_FLASH+10+index).Scale = (1,1)*CFRandom(.5, .6);
						A_OverlayAlpha(PSP_FLASH+10+index, CFRandom(.01, .08)*4);
					}
				}
			}
			#### ### 1 {
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, CFRandom(-.5, .5), CFRandom(-.5, .5), WOF_INTERPOLATE);
				A_OverlayRotate(Lay1, CFRandom(-.25, .25), WOF_INTERPOLATE);
				A_OverlayScale(Lay1, CFRandom(1, 1.025), CFRandom(1, 1.025), WOF_INTERPOLATE);
				if(GetAge()%2==0)invoker.SawFrame++;
				if(invoker.SawFrame>5)invoker.SawFrame = 2;
				Player.GetPSprite(Lay1).Frame = invoker.SawFrame;
			}
			#### # 0{A_OverlayFlags(Lay1, PSPF_INTERPOLATE, false);A_OverlayPivot(Lay1, .5, 1);}
			Loop;
		SawLower:
			SAW1 # 1 {
				A_OverlayPivot(Lay1, .5, 1);
				A_OverlayOffset(Lay1, CFRandom(-.5, .5), CFRandom(-.5, .5));
				A_OverlayRotate(Lay1, CFRandom(-.5, .5));
				Player.GetPSprite(Lay1).Frame = invoker.SawFrame;
				A_OverlayScale(Lay1, CFRandom(1, 1.05), CFRandom(1, 1.05), WOF_INTERPOLATE);
				if(MRIntW_BrownGloves)Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("SAW2A0");
			}
			#### # 1{
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayOffset(Lay1, -2, 8, WOF_ADD);
				A_OverlayRotate(Lay1, 1, WOF_ADD);
				A_OverlayScale(Lay1, 0, -.1, WOF_ADD);
			}
			#### ####### 1 {
				A_OverlayOffset(Lay1, -2, 1, WOF_ADD);
				A_OverlayRotate(Lay1, 1, WOF_ADD);
				A_OverlayScale(Lay1, -.025, -.025, WOF_ADD);
			}
			Stop;
		
		Ready:
			TNT1 AA 4 A_WeaponReady();
			#### # 0 {A_Overlay(Lay1, "SawReady", false);if(MRIntW_BetterAlert)A_AlertMonsters(1000);}
			Loop;
		
		SawStartCut:
			SAW1 # 1{
				Player.GetPSprite(Lay1).Frame = invoker.SawFrame;
				A_OverlayPivot(Lay1, .5, 1);
				if(MRIntW_BrownGloves)Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("SAW2A0");
			}
			#### # 1 {A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);A_OverlayScale(Lay1, 1, .8, WOF_INTERPOLATE);}
		SawCutLoop:
			#### # 0 {invoker.SawPos = 1;}
			#### BABA 1{
				A_OverlayPivot(Lay1, .7, .5);
				A_OverlayOffset(Lay1, -invoker.SawPos*2, 6-invoker.SawPos*2.5, WOF_INTERPOLATE);
				A_OverlayRotate(Lay1, CFRandom(-1, 1), WOF_INTERPOLATE);
				Float Scal = 1-.1*invoker.SawPos;
				A_OverlayScale(Lay1, Scal, Scal, WOF_INTERPOLATE);
				if(invoker.SawPos>0)invoker.SawPos -= .25;
				
				if(MRIntW_Smoke){
					int index;
					For(index=0;index<invoker.Smokes.Size();index++){
						if(!invoker.Smokes[index])Break;
					}
					if(index<invoker.Smokes.Size()){
						MRIntWeaps_SmokeContainer a = New('MRIntWeaps_SmokeContainer');
						a.Vel = (CFRandom(5, 6), -CFRandom(1, 1.2), -CFRandom(.6, 1));
						invoker.Smokes[index] = a;
						
						Vector2 Bob = PlayerPawn(Self).BobWeapon(35);
						
						A_Overlay(PSP_FLASH+10+index, "SmokeLay");
						A_OverlayOffset(PSP_FLASH+10+index, 180+Bob.X, 170+Bob.Y);
						Player.GetPSprite(PSP_FLASH+10+index).Scale = (1,1)*CFRandom(.3, .4);
						A_OverlayAlpha(PSP_FLASH+10+index, CFRandom(.01, .08)*3);
					}
				}
			}
			Loop;
		SawStopCut:
			SAW1 A 1{if(MRIntW_BrownGloves)Player.GetPSprite(Lay1).Sprite = GetSpriteIndex("SAW2A0");}
			#### # 1{
				Player.GetPSprite(Lay1).Frame = invoker.SawFrame;
				A_OverlayPivot(Lay1, .5, 1);
				A_OverlayScale(Lay1, 1, .8);
			}
			#### ## 1{
				A_OverlayFlags(Lay1, PSPF_INTERPOLATE, true);
				A_OverlayScale(Lay1, 0, .1, WOF_ADD);
				if(GetAge()%2==0)invoker.SawFrame++;
				if(invoker.SawFrame>5)invoker.SawFrame = 2;
				Player.GetPSprite(Lay1).Frame = invoker.SawFrame;
			}
			Goto SawReady;
		
		Fire:
			TNT1 A 0 A_Overlay(Lay1, "SawStartCut");
		FireLoop:
			TNT1 AA 4 {
				A_Saw();
				if(MRIntW_Recoil && Player.GetPSprite(Overlayid()).Tics%2==0){//Screen shake
					invoker.Recoil+=1;
					A_SetPitch(Pitch-1*MRIntW_RecoilAmount);
				}
			}
			SAW1 B 0 A_ReFire("FireLoop");
			TNT1 A 0 A_Overlay(Lay1, "SawStopCut");
			Goto Ready;
			
		Sprites:
			SAW2 ABCDEF 0;
			Stop;
	}
}